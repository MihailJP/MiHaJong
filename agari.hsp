/*=============================
 * HSP麻雀クライアントMiHaJong
 *     [和了処理ルーチン]
 *=============================
 */

#module
#include "const.as"
#deffunc agariproc int FurikomiPlayer, int ActivePlayer, int _RoundEndType, \
var yakuInfo, var GameStat, var GameEnv, \
var tmpUraFlag, var tmpAliceFlag, var ResultDesc
	RoundEndType = _RoundEndType
	tmpagariflag = 0
	FirstAgariPlayer = getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)
	OyaAgari = -1
	ResultDesc = ""
	tmpUraFlag = 0
	tmpAliceFlag = 0
	AgariPlayerPriority = -1
	origDoraPointer = getDoraPointer(GameStat)
	exportYakuPoint yakuInfo, AgariPointRaw
	if ((RoundEndType == ENDKYOKU_AGARI)||(RoundEndType == ENDKYOKU_CHONBO)) {
		repeat NUM_OF_PLAYERS-1
			switch getRule(RULE_MULTIPLE_MAHJONG)
				case 0: case 1:
					if (cnt > 0) {break}
					swbreak
				case 2: case 3:
					if (cnt > 1) {break}
					swbreak
			swend
			if (cnt > 0) {
				if (getTsumoAgariFlag(GameStat) == 0) {
					RoundEndType = ENDKYOKU_AGARI
					setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, (getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)+1) \ NUM_OF_PLAYERS
					if (getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE) == furikomiPlayer) {break}
					if (getDeclarationFlag(GameStat, DECLARATIONFLAG_RON, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS) == 1) {
						setHand GameStat, HAND_TILECODE, TSUMOHAI_INDEX, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getCurrentDiscard(GameStat, CURRENTDISCARD_TILECODE)
						setHand GameStat, HAND_REDTILE, TSUMOHAI_INDEX, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getCurrentDiscard(GameStat, CURRENTDISCARD_REDTILE)
						countyaku GameStat, yakuInfo, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS, furikomiPlayer
						chkfuriten FuritenFlag, MachihaiFlag, MachihaiCount, MachihaiTotal, MachiMen, GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS
						// 縛りを満たさないか、振聴のとき
						if ((getYakuInfo(yakuInfo, YAKUINF_HAN_BASE) <= getShibari(GameStat))||(FuritenFlag == 1)||(getDoujunFuriten(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)||((getRule(RULE_RIICHI_SHIBARI) != 0)&&(getRichiFlag(GameStat, RICHI_FLAG, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == RIICHI_NO))) {
							RoundEndType = ENDKYOKU_CHONBO // チョンボにする
						}
					} else {
						continue cnt
					}
				}
			}
			if (getTsumoAgariFlag(GameStat) == 1) {break}
		loop
	}
	repeat NUM_OF_PLAYERS-1
		setDoraPointer GameStat, origDoraPointer
		switch getRule(RULE_MULTIPLE_MAHJONG)
			case 0: case 1:
				if (cnt > 0) {break}
				swbreak
			case 2: case 3:
				if (cnt > 1) {break}
				swbreak
		swend
		if (cnt == 0) {
			setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, FirstAgariPlayer
			if (getTsumoAgariFlag(GameStat) == 0) {
				RoundEndType = ENDKYOKU_AGARI
				setHand GameStat, HAND_TILECODE, TSUMOHAI_INDEX, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getCurrentDiscard(GameStat, CURRENTDISCARD_TILECODE)
				setHand GameStat, HAND_REDTILE, TSUMOHAI_INDEX, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getCurrentDiscard(GameStat, CURRENTDISCARD_REDTILE)
				countyaku GameStat, yakuInfo, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS, furikomiPlayer
				chkfuriten FuritenFlag, MachihaiFlag, MachihaiCount, MachihaiTotal, MachiMen, GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS
				// 縛りを満たさないか、振聴のとき
				if ((getYakuInfo(yakuInfo, YAKUINF_HAN_BASE) <= getShibari(GameStat))||(FuritenFlag == 1)||(getDoujunFuriten(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)||((getRule(RULE_RIICHI_SHIBARI) != 0)&&(getRichiFlag(GameStat, RICHI_FLAG, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == RIICHI_NO))) {
					RoundEndType = ENDKYOKU_CHONBO // チョンボにする
				}
			} else {
				RoundEndType = ENDKYOKU_AGARI
				countyaku GameStat, yakuInfo, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS, furikomiPlayer
				chkfuriten FuritenFlag, MachihaiFlag, MachihaiCount, MachihaiTotal, MachiMen, GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS
				if ((getYakuInfo(yakuInfo, YAKUINF_HAN_BASE) <= getShibari(GameStat))||((getRule(RULE_RIICHI_SHIBARI) != 0)&&(getRichiFlag(GameStat, RICHI_FLAG, ActivePlayer) == RIICHI_NO))) {
					RoundEndType = ENDKYOKU_CHONBO // チョンボにする
				}
			}
		}
		if (cnt > 0) {
			if (getTsumoAgariFlag(GameStat) == 0) {
				RoundEndType = ENDKYOKU_AGARI: setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, (getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)+1) \ NUM_OF_PLAYERS
				if (getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE) == furikomiPlayer) {break}
				if (getDeclarationFlag(GameStat, DECLARATIONFLAG_RON, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS) == 1) {
					setHand GameStat, HAND_TILECODE, TSUMOHAI_INDEX, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getCurrentDiscard(GameStat, CURRENTDISCARD_TILECODE)
					setHand GameStat, HAND_REDTILE, TSUMOHAI_INDEX, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getCurrentDiscard(GameStat, CURRENTDISCARD_REDTILE)
					countyaku GameStat, yakuInfo, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS, furikomiPlayer
					chkfuriten FuritenFlag, MachihaiFlag, MachihaiCount, MachihaiTotal, MachiMen, GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)\NUM_OF_PLAYERS
					// 縛りを満たさないか、振聴のとき
					if ((getYakuInfo(yakuInfo, YAKUINF_HAN_BASE) <= getShibari(GameStat))||(FuritenFlag == 1)||(getDoujunFuriten(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)||((getRule(RULE_RIICHI_SHIBARI) != 0)&&(getRichiFlag(GameStat, RICHI_FLAG, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == RIICHI_NO))) {
						RoundEndType = ENDKYOKU_CHONBO // チョンボにする
					}
					exportYakuPoint yakuInfo, AgariPointRaw
				} else {
					continue cnt
				}
			}
		}
		haifualicedoraupd
		/**************/
		/* 和了成立時 */
		/**************/
		if (RoundEndType == ENDKYOKU_AGARI) {
			tmpagariflag = 1
			if ((getAgariHouki(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)||(IsRemotePlayer(GameEnv, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == -1)) {
				RoundEndType = ENDKYOKU_CHONBO // 和了り放棄時の処理→誤ロン・誤ツモとして罰符とする
			}
		}
		if (RoundEndType == ENDKYOKU_AGARI) {
			chatrecv GameStat, GameEnv
			endround_agariproc GameStat, GameEnv, ResultDesc, FurikomiPlayer, AgariPlayerPriority, origDoraPointer, AgariPointRaw, YakuInfo, tmpAliceFlag
		}
		/**************/
		/* 錯和発生時 */
		/**************/
		if (RoundEndType == ENDKYOKU_CHONBO) {
			endround_chonboproc GameStat, ResultDesc
		}

		if (getTsumoAgariFlag(GameStat) == 1) {break /* ツモ和了りの時は終了 */}
		setDeposit GameStat, 0
		redrscreen: redraw 1
	loop
	RoundEndType = ENDKYOKU_CHONBO-tmpagariflag
	switch getRule(RULE_SIMULTANEOUS_MAHJONG)
		case 0:
			if (OyaAgari == -1) {setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, FirstAgariPlayer}
			else {setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, OyaAgari}
			swbreak
		case 1: setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, FirstAgariPlayer: swbreak
		case 2:
			if (OyaAgari == FirstAgariPlayer) {setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, FirstAgariPlayer+1}
			else {setCurrentPlayer GameStat, CURRENTPLAYER_ACTIVE, FirstAgariPlayer}
			swbreak
	swend

	switch getRule(RULE_PENALTY_NEGATIVE)
		case 1: case 2:
#ifdef SANMAT
			if ((isDobon(GameStat,PLAYER_EAST))||(isDobon(GameStat,PLAYER_SOUTH))||(isDobon(GameStat,PLAYER_WEST))) {
#else
			if ((isDobon(GameStat,PLAYER_EAST))||(isDobon(GameStat,PLAYER_SOUTH))||(isDobon(GameStat,PLAYER_WEST))||(isDobon(GameStat,PLAYER_NORTH))) {
#endif
				dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
				repeat NUM_OF_ACTUAL_PLAYERS
					if (isDobon(GameStat,cnt)) {
						PointDelta(cnt) -= 100*getRule(RULE_PENALTY_NEGATIVE)
						PointDelta(AgariPlayerPriority) += 100*getRule(RULE_PENALTY_NEGATIVE)
					}
				loop
				setCenterTitle "飛び罰符"
				putdelta PointDelta
				redraw 1: await 3000
				pointcalc GameStat, PointDelta
			}
			swbreak
		case 3: case 4: case 5:
			if (getRule(RULE_CHIP) != 0) {
#ifdef SANMAT
				if ((isDobon(GameStat,PLAYER_EAST))||(isDobon(GameStat,PLAYER_SOUTH))||(isDobon(GameStat,PLAYER_WEST))) {
#else
				if ((isDobon(GameStat,PLAYER_EAST))||(isDobon(GameStat,PLAYER_SOUTH))||(isDobon(GameStat,PLAYER_WEST))||(isDobon(GameStat,PLAYER_NORTH))) {
#endif
					dim PointDelta, NUM_OF_PLAYERS,NUM_OF_DIGIT_GROUPS
					repeat NUM_OF_ACTUAL_PLAYERS
						if (isDobon(GameStat,cnt)) {
							PointDelta(cnt) -= (getRule(RULE_PENALTY_NEGATIVE)-2)
							PointDelta(AgariPlayerPriority) += (getRule(RULE_PENALTY_NEGATIVE)-2)
						}
					loop
					setCenterTitle "飛び罰符"
					putchipdelta PointDelta
					redraw 1: await 1500
					repeat NUM_OF_PLAYERS
						addChip GameStat, cnt, PointDelta(cnt)
					loop
				}
			}
			swbreak
	swend
	return
#global

/* 和了成立時の処理 */
#module
#include "const.as"
#deffunc endround_agariproc var GameStat, var GameEnv, var ResultDesc, int FurikomiPlayer, var AgariPlayerPriority, \
int origDoraPointer, array AgariPointRaw, array YakuInfo, var tmpAliceFlag
	if (AgariPlayerPriority == -1) {AgariPlayerPriority = getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)}
	if (ResultDesc != "") {ResultDesc += "\n"}
	tmpResultDesc = ""
	switch playerWind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat))
		case PLAYER_EAST: tmpResultDesc += "東家": swbreak
		case PLAYER_SOUTH: tmpResultDesc += "南家": swbreak
		case PLAYER_WEST: tmpResultDesc += "西家": swbreak
#ifndef SANMAT
		case PLAYER_NORTH: tmpResultDesc += "北家": swbreak
#endif
	swend
	if (getTsumoAgariFlag(GameStat)) {
		tmpResultDesc += "のツモ和了り"
	} else {
		switch playerWind(furikomiPlayer, getRound(GameStat))
			case PLAYER_EAST: tmpResultDesc += "が東家からロン和了り": swbreak
			case PLAYER_SOUTH: tmpResultDesc += "が南家からロン和了り": swbreak
			case PLAYER_WEST: tmpResultDesc += "が西家からロン和了り": swbreak
#ifndef SANMAT
			case PLAYER_NORTH: tmpResultDesc += "が北家からロン和了り": swbreak
#endif
		swend
	}
	ResultDesc += tmpResultDesc
	chatappend "*** "+tmpResultDesc+"\n"
	statmes tmpResultDesc
	await 1500
	tmpDoraPointer = origDoraPointer
	AlicePointer = tmpDoraPointer-getYakuInfo(YakuInfo, YAKUINF_ALICEDORA)*2-2
	if ((getMenzen(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)&&(getRule(RULE_ALICE) != 0)) {
		setCenterTitle "アリス判定"
		repeat
			if (getDoraPointer(GameStat) <= AlicePointer) {break}
			setDoraPointer GameStat, getDoraPointer(GameStat) - 2
			dsplay@ SND_MEKURI
			redrscreen: await 1200
		loop
		setDoraPointer GameStat, tmpDoraPointer
		tmpAliceFlag = 1
	}
	bgmstop
	dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
	if (isPaoAgari(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE))) {
		// 包適用時
		if (playerwind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat)) == PLAYER_EAST) {
			// 親の和了り
			OyaAgari = getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)
			if (getTsumoAgariFlag(GameStat)) {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						agaricalc agariPointArray, AgariPointRaw, 6, 0, 0
						deltacalcplus AgariPointRaw, PointDelta, 6, cnt
					}
					if (isPao(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), cnt)) {
						deltacalcminus AgariPointRaw, PointDelta, 6, cnt
					}
				loop
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						agaricalc agariPointArray, AgariPointRaw, 3, 3, 1
						deltacalcplus AgariPointRaw, PointDelta, 3, cnt
						deltacalcplus AgariPointRaw, PointDelta, 3, cnt
					}
					if (cnt == furikomiPlayer) {
						deltacalcminus AgariPointRaw, PointDelta, 3, cnt
					}
					if (isPao(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), cnt)) {
						deltacalcminus AgariPointRaw, PointDelta, 3, cnt
					}
				loop
			}
		} else {
			// 子の和了り
			if (getTsumoAgariFlag(GameStat)) {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						agaricalc agariPointArray, AgariPointRaw, 4, 0, 0
						deltacalcplus AgariPointRaw, PointDelta, 4, cnt
					}
					if (isPao(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), cnt)) {
						deltacalcminus AgariPointRaw, PointDelta, 4, cnt
					}
				loop
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						agaricalc agariPointArray, AgariPointRaw, 2, 2, 1
						deltacalcplus AgariPointRaw, PointDelta, 2, cnt
						deltacalcplus AgariPointRaw, PointDelta, 2, cnt
					}
					if (cnt == furikomiPlayer) {
						deltacalcminus AgariPointRaw, PointDelta, 2, cnt
					}
					if (isPao(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), cnt)) {
						deltacalcminus AgariPointRaw, PointDelta, 2, cnt
					}
				loop
			}
		}
	} else {
		// 通常時
		if (playerwind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat)) == PLAYER_EAST) {
			// 親の和了り
			OyaAgari = getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)
			if (getTsumoAgariFlag(GameStat)) {
#ifdef ALLSANMA
				switch getRule(RULE_TSUMO_PAYMENT)
#ifdef SANMA4
					case 0:
#endif
#endif
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc agariPointArray, AgariPointRaw, 2, 2, 2
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
							} else {
								deltacalcminus AgariPointRaw, PointDelta, 2, cnt
							}
						loop
#ifdef ALLSANMA
#ifdef SANMA4
						swbreak
#endif
					case 2: case 3
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc agariPointArray, AgariPointRaw, 2, 2, 1
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								if (getRule(RULE_TSUMO_PAYMENT) == 3) {PointDelta(cnt, 0) += 20}
							} else: if (playerwind(cnt, getRound(GameStat)) != PLAYER_NORTH) {
								deltacalcminus AgariPointRaw, PointDelta, 2, cnt
								if (getRule(RULE_TSUMO_PAYMENT) == 3) {PointDelta(cnt, 0) -= 10}
							}
						loop
						swbreak
					default:
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc agariPointArray, AgariPointRaw, 3, 3, 1
								deltacalcplus AgariPointRaw, PointDelta, 3, cnt
								deltacalcplus AgariPointRaw, PointDelta, 3, cnt
							} else: if (playerwind(cnt, getRound(GameStat)) != PLAYER_NORTH) {
								deltacalcminus AgariPointRaw, PointDelta, 3, cnt
							}
						loop
						swbreak
				swend
#endif
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						agaricalc agariPointArray, AgariPointRaw, 6, 0, 0
						deltacalcplus AgariPointRaw, PointDelta, 6, cnt
					}
					if (cnt == furikomiPlayer) {
						deltacalcminus AgariPointRaw, PointDelta, 6, cnt
					}
				loop
			}
		} else {
			// 子の和了り
			if (getTsumoAgariFlag(GameStat)) {
#ifdef ALLSANMA
				switch getRule(RULE_TSUMO_PAYMENT)
#ifdef SANMA4
					case 0:
#endif
#endif
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc agariPointArray, AgariPointRaw, 2, 1, 2
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								deltacalcplus AgariPointRaw, PointDelta, 1, cnt
								deltacalcplus AgariPointRaw, PointDelta, 1, cnt
							} else: if (playerwind(cnt, getRound(GameStat)) == PLAYER_EAST) {
								deltacalcminus AgariPointRaw, PointDelta, 2, cnt
							} else {
								deltacalcminus AgariPointRaw, PointDelta, 1, cnt
							}
						loop
#ifdef ALLSANMA
#ifdef SANMA4
						swbreak
					case 1:
#else
					case 0:
#endif
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc agariPointArray, AgariPointRaw, 2, 2, 1
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
							} else: if (playerwind(cnt, getRound(GameStat)) != PLAYER_NORTH) {
								deltacalcminus AgariPointRaw, PointDelta, 2, cnt
							}
						loop
						swbreak
					case 2: case 3:
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc agariPointArray, AgariPointRaw, 2, 1, 1
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								deltacalcplus AgariPointRaw, PointDelta, 1, cnt
								if (getRule(RULE_TSUMO_PAYMENT) == 3) {PointDelta(cnt, 0) += 20}
							} else: if (playerwind(cnt, getRound(GameStat)) == PLAYER_EAST) {
								deltacalcminus AgariPointRaw, PointDelta, 2, cnt
								if (getRule(RULE_TSUMO_PAYMENT) == 3) {PointDelta(cnt, 0) -= 10}
							} else: if (playerwind(cnt, getRound(GameStat)) != PLAYER_NORTH) {
								deltacalcminus AgariPointRaw, PointDelta, 1, cnt
								if (getRule(RULE_TSUMO_PAYMENT) == 3) {PointDelta(cnt, 0) -= 10}
							}
						loop
						swbreak
					case 4:
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc2 agariPointArray, AgariPointRaw, 8.0/3.0, 4.0/3.0, 1, 0, 0
								deltacalcplusd AgariPointRaw, PointDelta, 8.0/3.0, cnt
								deltacalcplusd AgariPointRaw, PointDelta, 4.0/3.0, cnt
							} else: if (playerwind(cnt, getRound(GameStat)) == PLAYER_EAST) {
								deltacalcminusd AgariPointRaw, PointDelta, 8.0/3.0, cnt
							} else: if (playerwind(cnt, getRound(GameStat)) != PLAYER_NORTH) {
								deltacalcminusd AgariPointRaw, PointDelta, 4.0/3.0, cnt
							}
						loop
						swbreak
					case 5:
						repeat NUM_OF_ACTUAL_PLAYERS
							if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
								agaricalc2 agariPointArray, AgariPointRaw, 2, 1, 1, 0.5, 2
								deltacalcplus AgariPointRaw, PointDelta, 2, cnt
								deltacalcplus AgariPointRaw, PointDelta, 1, cnt
								deltacalcplusd AgariPointRaw, PointDelta, 0.5, cnt
								deltacalcplusd AgariPointRaw, PointDelta, 0.5, cnt
							} else: if (playerwind(cnt, getRound(GameStat)) == PLAYER_EAST) {
								deltacalcminus AgariPointRaw, PointDelta, 2, cnt
								deltacalcminusd AgariPointRaw, PointDelta, 0.5, cnt
							} else: if (playerwind(cnt, getRound(GameStat)) != PLAYER_NORTH) {
								deltacalcminus AgariPointRaw, PointDelta, 1, cnt
								deltacalcminusd AgariPointRaw, PointDelta, 0.5, cnt
							}
						loop
						swbreak
				swend
#endif
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						agaricalc agariPointArray, AgariPointRaw, 4, 0, 0
						deltacalcplus AgariPointRaw, PointDelta, 4, cnt
					}
					if (cnt == furikomiPlayer) {
						deltacalcminus AgariPointRaw, PointDelta, 4, cnt
					}
				loop
			}
		}
	}
	if (getRule(RULE_WAREME) != 0) {
		// 割れ目ルール
		if (getWareme(GameStat) == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
			// 割れ目の人が和了ったとき
			deltadouble PointDelta, PLAYER_EAST
			deltadouble PointDelta, PLAYER_SOUTH
			deltadouble PointDelta, PLAYER_WEST
#ifndef SANMAT
			deltadouble PointDelta, PLAYER_NORTH
#endif
		} else {
			// 割れ目の人以外が和了ったとき
			deltawareme PointDelta, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getWareme(GameStat)
			deltadouble PointDelta, getWareme(GameStat)
		}
	}
	if ((getRule(RULE_WAREME) == 2)&&(getDice(GameStat, 0, 0) == getDice(GameStat, 1, 0))) {
		// サイコロがゾロ目の時はさらに倍
		if (getWareme(GameStat) == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
			// 割れ目の人が和了ったとき
			deltadouble PointDelta, PLAYER_EAST
			deltadouble PointDelta, PLAYER_SOUTH
			deltadouble PointDelta, PLAYER_WEST
#ifndef SANMAT
			deltadouble PointDelta, PLAYER_NORTH
#endif
		} else {
			// 割れ目の人以外が和了ったとき
			deltawareme PointDelta, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getWareme(GameStat)
			deltadouble PointDelta, getWareme(GameStat)
		}
	}
	if (getRule(RULE_DOUKASEN) != 0) {
		// 導火線ルール
		if (getDoukasen(GameStat) == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
			// 導火線の人が和了ったとき
			deltadouble PointDelta, PLAYER_EAST
			deltadouble PointDelta, PLAYER_SOUTH
			deltadouble PointDelta, PLAYER_WEST
#ifndef SANMAT
			deltadouble PointDelta, PLAYER_NORTH
#endif
		} else {
			// 導火線の人以外が和了ったとき
			deltawareme PointDelta, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getDoukasen(GameStat)
			deltadouble PointDelta, getDoukasen(GameStat)
		}
	}
	agariscrproc GameStat, GameEnv, yakuInfo, furikomiPlayer, agariPointArray, ChipAmount, ResultDesc, tmpUraFlag /* 和了画面 */
	if ((getMenzen(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)&&(getRule(RULE_ALICE) != 0)) {
		setDoraPointer GameStat, AlicePointer
	}
	
	setCenterTitle "和了点"
	putdelta PointDelta
	redraw 1: await 1500
	pointcalc GameStat, PointDelta
	
	if ((getHonba(GameStat))&&(getRule(RULE_TSUMIBOH_RATE) != 3)) {
		dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
#ifdef SANMAT
		if (playerwind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat)) == PLAYER_EAST) {
			if (getTsumoAgariFlag(GameStat)) {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*2)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					} else {
						PointDelta(cnt) -= getHonba(GameStat)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*2)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
					if (cnt == furikomiPlayer) {
						PointDelta(cnt) -= (getHonba(GameStat)*2)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			}
		} else {
			if (getTsumoAgariFlag(GameStat)) {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*2)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					} else {
						PointDelta(cnt) -= getHonba(GameStat)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*2)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
					if (cnt == furikomiPlayer) {
						PointDelta(cnt) -= (getHonba(GameStat)*2)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			}
		}
#else
		if (playerwind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat)) == PLAYER_EAST) {
			if (getTsumoAgariFlag(GameStat)) {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*3)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					} else {
						PointDelta(cnt) -= getHonba(GameStat)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*3)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
					if (cnt == furikomiPlayer) {
						PointDelta(cnt) -= (getHonba(GameStat)*3)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			}
		} else {
			if (getTsumoAgariFlag(GameStat)) {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*3)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					} else: if (playerwind(cnt, getRound(GameStat)) == PLAYER_EAST) {
						PointDelta(cnt) -= getHonba(GameStat)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					} else {
						PointDelta(cnt) -= getHonba(GameStat)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
						PointDelta(cnt) += (getHonba(GameStat)*3)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
					if (cnt == furikomiPlayer) {
						PointDelta(cnt) -= (getHonba(GameStat)*3)*(getRule(RULE_TSUMIBOH_RATE)*2+1)
					}
				loop
			}
		}
#endif
		// 割れ目で積み棒も２倍になる
		if (getRule(RULE_WAREME) != 0) {
			// 割れ目ルール
			if (getWareme(GameStat) == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
				// 割れ目の人が和了ったとき
				PointDelta(PLAYER_EAST) += PointDelta(PLAYER_EAST)
				PointDelta(PLAYER_SOUTH) += PointDelta(PLAYER_SOUTH)
				PointDelta(PLAYER_WEST) += PointDelta(PLAYER_WEST)
#ifndef SANMAT
				PointDelta(PLAYER_NORTH) += PointDelta(PLAYER_NORTH)
#endif
			} else {
				// 割れ目の人以外が和了ったとき
				PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) -= PointDelta(getWareme(GameStat))
				PointDelta(getWareme(GameStat)) += PointDelta(getWareme(GameStat))
			}
		}
		if ((getRule(RULE_WAREME) == 2)&&(getDice(GameStat, 0, 0) == getDice(GameStat, 1, 0))) {
			// サイコロがゾロ目の時はさらに倍
			if (getWareme(GameStat) == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
				// 割れ目の人が和了ったとき
				PointDelta(PLAYER_EAST) += PointDelta(PLAYER_EAST)
				PointDelta(PLAYER_SOUTH) += PointDelta(PLAYER_SOUTH)
				PointDelta(PLAYER_WEST) += PointDelta(PLAYER_WEST)
#ifndef SANMAT
				PointDelta(PLAYER_NORTH) += PointDelta(PLAYER_NORTH)
#endif
			} else {
				// 割れ目の人以外が和了ったとき
				PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) -= PointDelta(getWareme(GameStat))
				PointDelta(getWareme(GameStat)) += PointDelta(getWareme(GameStat))
			}
		}
		// 割れ目で積み棒も２倍になる
		if (getRule(RULE_DOUKASEN) != 0) {
			// 導火線ルール
			if (getDoukasen(GameStat) == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
				// 割れ目の人が和了ったとき
				PointDelta(PLAYER_EAST) += PointDelta(PLAYER_EAST)
				PointDelta(PLAYER_SOUTH) += PointDelta(PLAYER_SOUTH)
				PointDelta(PLAYER_WEST) += PointDelta(PLAYER_WEST)
#ifndef SANMAT
				PointDelta(PLAYER_NORTH) += PointDelta(PLAYER_NORTH)
#endif
			} else {
				// 導火線の人以外が和了ったとき
				PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) -= PointDelta(getDoukasen(GameStat))
				PointDelta(getDoukasen(GameStat)) += PointDelta(getDoukasen(GameStat))
			}
		}
		/* 包の場合 */
		if (isPaoAgari(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE))) {
			PaoPlayer = getPaoPlayer(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE))
			if (getTsumoAgariFlag(GameStat)) {
				repeat NUM_OF_ACTUAL_PLAYERS
					if ((PaoPlayer != cnt)&&(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE) != cnt)) {
						PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) += PointDelta(cnt)
						PointDelta(cnt) -= PointDelta(cnt)
					}
				loop
			} else {
				repeat NUM_OF_ACTUAL_PLAYERS
					if ((PaoPlayer != cnt)&&(furikomiPlayer != cnt)&&(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE) != cnt)) {
						PointDelta(PaoPlayer) += PointDelta(cnt)
						PointDelta(cnt) -= PointDelta(cnt)
					}
				loop
			}
		}

		setCenterTitle "積棒清算"
		putdelta PointDelta
		redraw 1: await 1500
		pointcalc GameStat, PointDelta
	}
	
	if (getDeposit(GameStat)) {
		dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
		PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), 0) += getDeposit(GameStat)*10
		
		setCenterTitle "供託清算"
		putdelta PointDelta
		redraw 1: await 1500
		pointcalc GameStat, PointDelta
	}
	
	if ((ChipAmount > 0)&&(getRule(RULE_CHIP) != 0)) {
		dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
		if (getTsumoAgariFlag(GameStat) == 0) {
			PointDelta(furikomiPlayer) = -ChipAmount
			PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) = ChipAmount
		} else {
			PointDelta(PLAYER_EAST) = -ChipAmount
			PointDelta(PLAYER_SOUTH) = -ChipAmount
			PointDelta(PLAYER_WEST) = -ChipAmount
#ifndef SANMAT
			PointDelta(PLAYER_NORTH) = -ChipAmount
#endif
			PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) = ChipAmount*(NUM_OF_ACTUAL_PLAYERS-1)
		}
		setCenterTitle "祝儀清算"
		putchipdelta PointDelta
		redraw 1: await 1500
		repeat NUM_OF_PLAYERS
			addChip GameStat, cnt, PointDelta(cnt)
		loop
	}
	/* 四馬路が北家の放銃だった場合 */
#ifndef SANMAT
	if (playerWind(furikomiPlayer, getRound(GameStat)) == PLAYER_NORTH) {
		yakuname = getYakuInfo(yakuInfo, YAKUINF_YAKULIST)
		if (notesearch(yakuname, "四馬路") >= 0) {
			dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
			PointDelta(PLAYER_EAST, 0) = 10
			PointDelta(PLAYER_SOUTH, 0) = 10
			PointDelta(PLAYER_WEST, 0) = 10
			PointDelta(PLAYER_NORTH, 0) = 10
			PointDelta(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), 0) = -30
			setCenterTitle "北枕罰符"
			putdelta PointDelta
			redraw 1: await 1500
			pointcalc GameStat, PointDelta
		}
	}
#endif
	return
#global

/* 錯和発生時の処理 */
#module
#include "const.as"
#deffunc endround_chonboproc var GameStat, var ResultDesc
	if (ResultDesc != "") {ResultDesc += "\n"}
	tmpResultDesc = ""
	switch playerWind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat))
		case PLAYER_EAST: tmpResultDesc += "東家のチョンボ": swbreak
		case PLAYER_SOUTH: tmpResultDesc += "南家のチョンボ": swbreak
		case PLAYER_WEST: tmpResultDesc += "西家のチョンボ": swbreak
#ifndef SANMAT
		case PLAYER_NORTH: tmpResultDesc += "北家のチョンボ": swbreak
#endif
	swend
	ResultDesc += tmpResultDesc+"です"
	chatappend "*** "+tmpResultDesc+"\n"
	statmes tmpResultDesc
	await 1500
	bgmplay_norep MUS_RYUUKYOKU
	setCenterTitle "錯和"
	// 誤ロンまたは誤ツモ
	if ((getTsumoAgariFlag(GameStat) == 0)||(getTsumoAgariFlag(GameStat) == 1)) {
		if ((getPao(GameStat, PAO_PLAYER_AGARI, PAO_YAKU_MINKAN) != 1)&&(getRule(RULE_MINKAN_PAO) == 6)) {
			// 大明槓の嶺上開花禁止ルールの場合
			setCenterTitle "明槓錯和": ResultDesc += "(大明槓の嶺上牌での和了り)"
		}
		else:if ((getRichiFlag(GameStat, RICHI_FLAG, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == RIICHI_NO)&&(getRule(RULE_RIICHI_SHIBARI) != 0)) {
			// 立直縛りで立直してないなら錯和
			setCenterTitle "黙聴錯和": ResultDesc += "(ダマ聴での和了り)"
		}
		else:if ((FuritenFlag == 1)||(getDoujunFuriten(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)) {
			// 振聴なのにロンした場合は錯和
			setCenterTitle "振聴錯和": ResultDesc += "(振聴でのロン和了り)"
		}
		else {
			// 和了りが成立していない場合錯和
			Shanten = countshanten(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), SHANTEN_ALL)
			if (Shanten >= 0) {setCenterTitle "不聴錯和": ResultDesc += "(成立していない和了り)"}
			else {
				// 役がなかった場合は錯和
				countyaku GameStat, yakuInfo, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), furikomiPlayer
				if (getYakuInfo(yakuInfo, YAKUINF_HAN_BASE) == 0) {setCenterTitle "無飜錯和": ResultDesc += "(役のない和了り)"}
				else:if ((getYakuInfo(yakuInfo, YAKUINF_HAN_BASE) == 1)&&(getShibari(GameStat) == 1)) {
					// 二飜縛りで１飜しかないなら錯和
					setCenterTitle "飜不足錯和": ResultDesc += "(一飜しかない和了り)"
				} else:if ((getAgariHouki(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)||(IsRemotePlayer(GameEnv, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == -1)) {
					// 和了り放棄なのに和了ろうとした
					setCenterTitle "和了放棄錯和": ResultDesc += "(和了り放棄適用中)"
				} else {setCenterTitle "　錯和　": ResultDesc += ""}
			}
		}
	}
	// 誤ロン、誤ツモ以外の錯和
	if (getTsumoAgariFlag(GameStat) == AGARI_KUIKAE) {setCenterTitle "喰替錯和": ResultDesc += "(喰い替え)"} // 喰い替えをしたとき
	if (getTsumoAgariFlag(GameStat) > 1) {dsplay@ SND_PAGE} else {dsplay@ SND_CUOHU}
	redrscreen: await 5000

	dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
#ifdef SANMAT
	if (playerWind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat)) == PLAYER_EAST) {
		repeat NUM_OF_ACTUAL_PLAYERS
			if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
				PointDelta(cnt) -= 120
			} else {
				PointDelta(cnt) += 60
			}
		loop
	} else {
		repeat NUM_OF_ACTUAL_PLAYERS
			if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
				PointDelta(cnt) -= 80
			} else {
				PointDelta(cnt) += 40
			}
		loop
	}
#else
	if (playerWind(getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), getRound(GameStat)) == PLAYER_EAST) {
		repeat NUM_OF_PLAYERS
			if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
				PointDelta(cnt) -= 120
			} else {
				PointDelta(cnt) += 40
			}
		loop
	} else {
		repeat NUM_OF_PLAYERS
			if (cnt == getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) {
				PointDelta(cnt) -= 80
			} else {
				if (playerWind(cnt, getRound(GameStat)) == PLAYER_EAST) {
					PointDelta(cnt) += 40
				} else {
					PointDelta(cnt) += 20
				}
			}
		loop
	}
#endif
	setCenterTitle "錯和罰符"
	putdelta PointDelta
	redraw 1: await 1500
	pointcalc GameStat, PointDelta
	return
#global
