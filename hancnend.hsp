/*=============================
 * HSP麻雀クライアントMiHaJong
 *     [半荘終了ルーチン]
 *=============================
 */

/* 半荘終了時の処理 */
#module FINIS
#include "const.as"
#deffunc endgame var GameStat, var GameEnv, int origTurn, int origHonba
	calcRank Rank, GameStat
	chatappend "-------------\n*** 終了\n"
	if (GetWatchModeFlag(GameEnv) == 0) {
		if (Rank(getPlayer(GameStat)) == 1) {
			bgmplay MUS_ENDING
			statmes "終了 あなたがトップです"
		} else {
			bgmplay MUS_ENDING2
			statmes "終了 あなたは"+Rank(getPlayer(GameStat))+"位でした"
		}
	} else {
		bgmplay MUS_ENDING2
		statmes "終了 Watch Mode"
	}
	vanish@: commonswitch GameStat, GameEnv
	setHonba GameStat, OrigHonba
	setRound GameStat, OrigTurn
	repeat NUM_OF_PLAYERS
		setCall cnt, ""
	loop
	setCenterTitle "終了"
	dsplay@ SND_PAGE: redrscreen: await 500
	repeat NUM_OF_ACTUAL_PLAYERS
		if (Rank(cnt) == 1) {setCall cnt, "１位"}
	loop: dsplay@ SND_TYPE: redrscreen: await 500
	repeat NUM_OF_ACTUAL_PLAYERS
		if (Rank(cnt) == 2) {setCall cnt, "２位"}
	loop: dsplay@ SND_TYPE: redrscreen: await 500
	repeat NUM_OF_ACTUAL_PLAYERS
		if (Rank(cnt) == 3) {setCall cnt, "３位"}
#ifndef SANMAT
	loop: dsplay@ SND_TYPE: redrscreen: await 500
	repeat NUM_OF_ACTUAL_PLAYERS
		if (Rank(cnt) == 4) {setCall cnt, "４位"}
#endif
	loop: dsplay@ SND_TYPE: redrscreen: await 3000
	if (getDeposit(GameStat)) {
		dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
		repeat NUM_OF_PLAYERS
			if (Rank(cnt) == 1) {PointDelta(cnt) += getDeposit(GameStat)*10}
		loop
		setCenterTitle "供託清算"
		putdelta PointDelta
		redraw 1: await 3000
		pointcalc GameStat, PointDelta
	}
	setDeposit GameStat, 0

	if ((getRule(RULE_YAKITORI) == 1)||(getRule(RULE_YAKITORI) == 2)) {
#ifdef SANMAT
		if ((getYakitori(GameStat, 0)+getYakitori(GameStat, 1)+getYakitori(GameStat, 2)) > 0) {
#else
		if ((getYakitori(GameStat, 0)+getYakitori(GameStat, 1)+getYakitori(GameStat, 2)+getYakitori(GameStat, 3)) > 0) {
#endif
			dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
			repeat NUM_OF_ACTUAL_PLAYERS
				if (getYakitori(GameStat, cnt) == 1) {
					PointDelta(cnt, 0) -= 50*getRule(RULE_YAKITORI)
					repeat NUM_OF_ACTUAL_PLAYERS
						if (Rank(cnt) == 1) {
							PointDelta(cnt, 0) += 50*getRule(RULE_YAKITORI)
						}
					loop
				}
			loop
			setCenterTitle "焼鳥罰符"
			putdelta PointDelta
			redraw 1: await 3000
			pointcalc GameStat, PointDelta
		}
	}
	if ((getRule(RULE_YAKITORI) == 3)||(getRule(RULE_YAKITORI) == 4)||(getRule(RULE_YAKITORI) == 5)) {
		if (getRule(RULE_CHIP) != 0) {
#ifdef SANMAT
			if ((getYakitori(GameStat, 0)+getYakitori(GameStat, 1)+getYakitori(GameStat, 2)) > 0) {
#else
			if ((getYakitori(GameStat, 0)+getYakitori(GameStat, 1)+getYakitori(GameStat, 2)+getYakitori(GameStat, 3)) > 0) {
#endif
				dim PointDelta, NUM_OF_PLAYERS
				repeat NUM_OF_ACTUAL_PLAYERS
					if (getYakitori(GameStat, cnt) == 1) {
						PointDelta(cnt) -= (getRule(RULE_YAKITORI)-2)
						repeat NUM_OF_ACTUAL_PLAYERS
							if (Rank(cnt) == 1) {
								PointDelta(cnt) += (getRule(RULE_YAKITORI)-2)
							}
						loop
					}
				loop
				setCenterTitle "焼鳥罰符"
				putchipdelta PointDelta
				redraw 1: await 3000
				repeat NUM_OF_PLAYERS
					addChip GameStat, cnt, PointDelta(cnt)
				loop
			}
		}
	}

	dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
	if (getRule(RULE_POINT_BASIS) != 12) {
		PlusCount = 0
		repeat NUM_OF_ACTUAL_PLAYERS // 沈みウマの計算用に浮いてる人数を数える
			if (isAboveBase(GameStat,cnt)) {PlusCount++}
		loop
#ifdef SANMAT
		repeat NUM_OF_ACTUAL_PLAYERS
			switch getRule(RULE_POINT_BASIS)
				/* 順位ウマ */
				case 13:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 30: swbreak
						case 3: PointDelta(cnt,0) -= 30: swbreak
					swend
					swbreak
				case 14:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 50: swbreak
						case 3: PointDelta(cnt,0) -= 50: swbreak
					swend
					swbreak
				case 15:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 80: swbreak
						case 3: PointDelta(cnt,0) -= 80: swbreak
					swend
					swbreak
				case 0:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 100: swbreak
						case 3: PointDelta(cnt,0) -= 100: swbreak
					swend
					swbreak
				case 1:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 200: swbreak
						case 3: PointDelta(cnt,0) -= 200: swbreak
					swend
					swbreak
				case 2:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 300: swbreak
						case 3: PointDelta(cnt,0) -= 300: swbreak
					swend
					swbreak
				case 3:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 400: swbreak
						case 3: PointDelta(cnt,0) -= 400: swbreak
					swend
					swbreak
				case 4:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 500: swbreak
						case 3: PointDelta(cnt,0) -= 500: swbreak
					swend
					swbreak
				case 5:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 600: swbreak
						case 3: PointDelta(cnt,0) -= 600: swbreak
					swend
					swbreak
				/* 複合ウマ */
				case 6:
					switch Rank(cnt)
						case 1:
							if (PlusCount < 2) {PointDelta(cnt,0) += 120}
							else {PointDelta(cnt) += 80}
							swbreak
						case 2:
							if (PlusCount < 2) {PointDelta(cnt,0) -= 40}
							swbreak
						case 3:
							PointDelta(cnt) -= 80
							swbreak
					swend
					swbreak
				case 7:
					switch Rank(cnt)
						case 1:
							if (PlusCount < 2) {PointDelta(cnt,0) += 150}
							else {PointDelta(cnt) += 100}
							swbreak
						case 2:
							if (PlusCount < 2) {PointDelta(cnt,0) -= 50}
							swbreak
						case 3:
							PointDelta(cnt) -= 100
							swbreak
					swend
					swbreak
				case 8:
					switch Rank(cnt)
						case 1:
							if (PlusCount < 2) {PointDelta(cnt,0) += 300}
							else {PointDelta(cnt) += 200}
							swbreak
						case 2:
							if (PlusCount < 2) {PointDelta(cnt,0) -= 100}
							swbreak
						case 3:
							PointDelta(cnt) -= 200
							swbreak
					swend
					swbreak
				/* 沈みウマ */
				case 9:
					if (Rank(cnt) == 1) {PointDelta(cnt,0) += 50*(3-PlusCount)}
					if (Rank(cnt) > PlusCount) {PointDelta(cnt,0) -= 50}
					swbreak
				case 10:
					if (Rank(cnt) == 1) {PointDelta(cnt,0) += 100*(3-PlusCount)}
					if (Rank(cnt) > PlusCount) {PointDelta(cnt,0) -= 100}
					swbreak
				case 11:
					if (Rank(cnt) == 1) {PointDelta(cnt,0) += 200*(3-PlusCount)}
					if (Rank(cnt) > PlusCount) {PointDelta(cnt,0) -= 200}
					swbreak
			swend
		loop
#else
		repeat NUM_OF_ACTUAL_PLAYERS
			switch getRule(RULE_POINT_BASIS)
				/* 順位ウマ */
				case 13:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 30: swbreak
						case 4: PointDelta(cnt,0) -= 30: swbreak
					swend
					swbreak
				case 14:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 50: swbreak
						case 4: PointDelta(cnt,0) -= 50: swbreak
					swend
					swbreak
				case 15:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 50: swbreak
						case 2: PointDelta(cnt,0) += 30: swbreak
						case 3: PointDelta(cnt,0) -= 30: swbreak
						case 4: PointDelta(cnt,0) -= 50: swbreak
					swend
					swbreak
				case 0:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 100: swbreak
						case 2: PointDelta(cnt,0) += 50: swbreak
						case 3: PointDelta(cnt,0) -= 50: swbreak
						case 4: PointDelta(cnt,0) -= 100: swbreak
					swend
					swbreak
				case 1:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 200: swbreak
						case 2: PointDelta(cnt,0) += 100: swbreak
						case 3: PointDelta(cnt,0) -= 100: swbreak
						case 4: PointDelta(cnt,0) -= 200: swbreak
					swend
					swbreak
				case 2:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 300: swbreak
						case 2: PointDelta(cnt,0) += 100: swbreak
						case 3: PointDelta(cnt,0) -= 100: swbreak
						case 4: PointDelta(cnt,0) -= 300: swbreak
					swend
					swbreak
				case 3:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 400: swbreak
						case 2: PointDelta(cnt,0) += 200: swbreak
						case 3: PointDelta(cnt,0) -= 200: swbreak
						case 4: PointDelta(cnt,0) -= 400: swbreak
					swend
					swbreak
				case 4:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 500: swbreak
						case 2: PointDelta(cnt,0) += 200: swbreak
						case 3: PointDelta(cnt,0) -= 200: swbreak
						case 4: PointDelta(cnt,0) -= 500: swbreak
					swend
					swbreak
				case 5:
					switch Rank(cnt)
						case 1: PointDelta(cnt,0) += 600: swbreak
						case 2: PointDelta(cnt,0) += 200: swbreak
						case 3: PointDelta(cnt,0) -= 200: swbreak
						case 4: PointDelta(cnt,0) -= 600: swbreak
					swend
					swbreak
				/* 複合ウマ */
				case 6:
					switch Rank(cnt)
						case 1: 
							switch PlusCount
								case 1: PointDelta(cnt,0) += 150: swbreak
								case 3: PointDelta(cnt,0) += 70: swbreak
								default: PointDelta(cnt,0) += 100: swbreak
							swend
							swbreak
						case 2: 
							switch PlusCount
								case 1: PointDelta(cnt,0) -= 30: swbreak
								default: PointDelta(cnt,0) += 50: swbreak
							swend
							swbreak
						case 3: 
							switch PlusCount
								case 3: PointDelta(cnt,0) += 30: swbreak
								default: PointDelta(cnt,0) -= 50: swbreak
							swend
							swbreak
						case 4: 
							switch PlusCount
								case 1: PointDelta(cnt,0) -= 70: swbreak
								case 3: PointDelta(cnt,0) -= 150: swbreak
								default: PointDelta(cnt,0) -= 100: swbreak
							swend
							swbreak
					swend
					swbreak
				case 7:
					switch Rank(cnt)
						case 1: 
							switch PlusCount
								case 1: PointDelta(cnt,0) += 120: swbreak
								default: PointDelta(cnt,0) += 80: swbreak
							swend
							swbreak
						case 2: 
							switch PlusCount
								case 1: PointDelta(cnt,0) -= 10: swbreak
								case 3: PointDelta(cnt,0) += 30: swbreak
								default: PointDelta(cnt,0) += 40: swbreak
							swend
							swbreak
						case 3: 
							switch PlusCount
								case 1: PointDelta(cnt,0) -= 30: swbreak
								case 3: PointDelta(cnt,0) += 10: swbreak
								default: PointDelta(cnt,0) -= 40: swbreak
							swend
							swbreak
						case 4: 
							switch PlusCount
								case 3: PointDelta(cnt,0) -= 120: swbreak
								default: PointDelta(cnt,0) -= 80: swbreak
							swend
							swbreak
					swend
					swbreak
				case 8:
					switch Rank(cnt)
						case 1: 
							switch PlusCount
								case 1: PointDelta(cnt,0) += 600: swbreak
								case 2: PointDelta(cnt,0) += 400: swbreak
								case 3: PointDelta(cnt,0) += 300: swbreak
							swend
							swbreak
						case 2: 
							switch PlusCount
								case 1: PointDelta(cnt,0) -= 100: swbreak
							swend
							swbreak
						case 3: 
							switch PlusCount
								case 1: PointDelta(cnt,0) -= 200: swbreak
								case 2: PointDelta(cnt,0) -= 100: swbreak
							swend
							swbreak
						case 4: 
							switch PlusCount
								case 1: PointDelta(cnt,0) -= 300: swbreak
								case 2: PointDelta(cnt,0) -= 300: swbreak
								case 3: PointDelta(cnt,0) -= 300: swbreak
							swend
							swbreak
					swend
					swbreak
				/* 沈みウマ */
				case 9:
					if (Rank(cnt) == 1) {PointDelta(cnt,0) += 50*(NUM_OF_PLAYERS-PlusCount)}
					if (Rank(cnt) > PlusCount) {PointDelta(cnt,0) -= 50}
					swbreak
				case 10:
					if (Rank(cnt) == 1) {PointDelta(cnt,0) += 100*(NUM_OF_PLAYERS-PlusCount)}
					if (Rank(cnt) > PlusCount) {PointDelta(cnt,0) -= 100}
					swbreak
				case 11:
					if (Rank(cnt) == 1) {PointDelta(cnt,0) += 200*(NUM_OF_PLAYERS-PlusCount)}
					if (Rank(cnt) > PlusCount) {PointDelta(cnt,0) -= 200}
					swbreak
			swend
		loop
#endif
		setCenterTitle "順位点"
		putdelta PointDelta
		redraw 1: await 3000
		pointcalc GameStat, PointDelta
	}
	
	dim PointDelta, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
#ifdef SANMAT
	if (getRule(RULE_STARTING_POINT) != 1) {
		repeat NUM_OF_ACTUAL_PLAYERS
			if (Rank(cnt) == 1) {
				switch getRule(RULE_STARTING_POINT)
					case 0: case 4: PointDelta(cnt) += 150: swbreak
					case 5: PointDelta(cnt) += 90: swbreak
					case 7: PointDelta(cnt) += 450: swbreak
					case 8: PointDelta(cnt) += 300: swbreak
				swend
			}
			if (Rank(cnt) == 3) {
				switch getRule(RULE_STARTING_POINT)
					case 2: PointDelta(cnt) -= 150: swbreak
					case 3: PointDelta(cnt) -= 300: swbreak
				swend
			}
		loop
		if ((getRule(RULE_STARTING_POINT) == 2)||(getRule(RULE_STARTING_POINT) == 3)) {
			setCenterTitle "ラスペナ"
		} else {setCenterTitle "トップ賞"}
#else
	if (getRule(RULE_STARTING_POINT) != 2) {
		repeat NUM_OF_ACTUAL_PLAYERS
			if (Rank(cnt) == 1) {
				switch getRule(RULE_STARTING_POINT)
					case 0: PointDelta(cnt) += 200: swbreak
					case 1: PointDelta(cnt) += 120: swbreak
					case 5: PointDelta(cnt) += 400: swbreak
				swend
			}
			if (Rank(cnt) == 4) {
				switch getRule(RULE_STARTING_POINT)
					case 3: PointDelta(cnt) -= 200: swbreak
					case 4: PointDelta(cnt) -= 400: swbreak
				swend
			}
		loop
		if ((getRule(RULE_STARTING_POINT) == 3)||(getRule(RULE_STARTING_POINT) == 4)) {
			setCenterTitle "ラスペナ"
		} else {setCenterTitle "トップ賞"}
#endif
		putdelta PointDelta
		redraw 1: await 3000
		pointcalc GameStat, PointDelta
	}
	
	/* 最終的なスコアを計算する */
	dim FinalScore, NUM_OF_PLAYERS, NUM_OF_DIGIT_GROUPS
	repeat NUM_OF_ACTUAL_PLAYERS
		if (Rank(cnt) > 1) {
			if (isAboveBase(GameStat, cnt)) {
				FinalScore(cnt, 0) = ((getScore(GameStat,cnt, 0)-BasePoint())+5)/10
			} else {
				FinalScore(cnt, 0) = ((getScore(GameStat,cnt, 0)-BasePoint())-5)/10
			}
			switch getRule(RULE_CHIP)
				case 1: FinalScore(cnt, 0) += getChip(GameStat, cnt)/2: swbreak
				case 2: FinalScore(cnt, 0) += getChip(GameStat, cnt): swbreak
				case 3: FinalScore(cnt, 0) += getChip(GameStat, cnt)*2: swbreak
				case 4: FinalScore(cnt, 0) += getChip(GameStat, cnt)*3: swbreak
				case 5: FinalScore(cnt, 0) += getChip(GameStat, cnt)*5: swbreak
				case 6: FinalScore(cnt, 0) += getChip(GameStat, cnt)*10: swbreak
			swend
			tmpcnt = cnt
			repeat NUM_OF_DIGIT_GROUPS-1, 1: FinalScore(tmpcnt, cnt) = getScore(GameStat,tmpcnt, cnt): loop
		}
	loop
	scoreflagfix FinalScore
	dim FinalScoreTmp, NUM_OF_DIGIT_GROUPS
	repeat NUM_OF_DIGIT_GROUPS
#ifdef SANMAT
		FinalScoreTmp(cnt) = FinalScore(0, cnt)+FinalScore(1, cnt)+FinalScore(2, cnt)
#else
		FinalScoreTmp(cnt) = FinalScore(0, cnt)+FinalScore(1, cnt)+FinalScore(2, cnt)+FinalScore(3, cnt)
#endif
	loop
	repeat NUM_OF_ACTUAL_PLAYERS
		if (Rank(cnt) == 1) {
			tmpcnt = cnt
			repeat NUM_OF_DIGIT_GROUPS: FinalScore(tmpcnt, cnt) = 0-FinalScoreTmp(cnt): loop
		}
	loop
	scoreflagfix FinalScore
	repeat NUM_OF_PLAYERS
		tmpcnt=cnt
		repeat NUM_OF_DIGIT_GROUPS-1
			FinalScore(tmpcnt, cnt) += (FinalScore(tmpcnt, cnt+1) \ 1000)*100000
			FinalScore(tmpcnt, cnt+1) /= 1000
		loop
	loop
	deltatxt FinalScore, 1
	repeat NUM_OF_ACTUAL_PLAYERS
		if (getCall(cnt) == "") {setCall cnt, "０"}
	loop
	setCenterTitle "最終成績"
	repeat NUM_OF_PLAYERS
		tmptmprnk = cnt+1
		repeat NUM_OF_PLAYERS
			if (Rank(cnt) == tmptmprnk) {
				tmpscoretxt = bignumtotext(FinalScore, cnt, 1, "+", "-")
				tmptxt += " "+tmpscoretxt+"点"
				chatappend "*** "+tmptmprnk+"位 "+tmpscoretxt+" "+getPlayerName(GameEnv, cnt)+"\n"
			}
		loop
	loop
	dsplay@ SND_YAKULST2: redrscreen
	redraw 1
	if (getGameMode(GameEnv) != GAMEMODE_STANDALONE) {sockclose@ SOCK_GAME+0: sockclose@ SOCK_CHAT+0: sockclose@ SOCK_CHAT+1: sockclose@ SOCK_CHAT+2: sockclose@ SOCK_CHAT+3}
	if (getGameMode(GameEnv) == GAMEMODE_SERVER) {sockclose@ SOCK_GAME+1: sockclose@ SOCK_GAME+2: sockclose@ SOCK_GAME+3}
	if (getGameMode(GameEnv) != GAMEMODE_STANDALONE) {chatappend "*** 切断しました\n"}
	gsel SCR_CHAT: objgray 1, 0: objgray 2, 0: gsel 0
	font fontname, 48, 0: objsize 200, 60
	pos 620, 560: colorbutton gosub "確認", *finishbtn@, -1, 0xffffff: tooltip@ stat-STATBOX, "タイトル画面に戻ります"
	haifusave
	return
#global

*finishbtn
	dsplay@ SND_BUTTON
	closingchat : chatwnd 0
	await 10
	buttonPressed = 1
	return
