/*=============================
 * HSP麻雀クライアントMiHaJong
 *    [七対子判定ルーチン]
 *=============================
 */

/* 指定の対子があるか数える */
#module
#defcfunc countPairs array tileCount, array targetTiles
/*
		val=countPairs(p1, p2)
		指定した種類の対子を数える

		p1 : 手牌にある種類別の牌数の配列
		p2 : 探したい牌の配列

		指定した種類のうち何種類の対子があるかを返します。
*/
	trace "対子の種類を調べます。"
	yakuflagcount = 0
	repeat length(targetTiles)
		if (tileCount(targetTiles(cnt)) >= 2) {yakuflagcount++}
	loop
	return yakuflagcount
#global

/* 数字の合計を数える(七対子版) */
#module
#include "const.as"
#defcfunc countTileNumerals array tileCount
/*
		val=countTileNumerals(p1)
		数字の合計を数える

		p1 : 手牌にある種類別の牌数の配列

		手牌にある数牌の数字の合計を返します。
*/
	Cifr = 0
	repeat TILE_NUMERAL_MAX-1, 1
		Cifr += tileCount(cnt) * (cnt \ TILE_SUIT_STEP)
	loop
	return Cifr
#global

/* 七対子 */
#module
#include "const.as"
#deffunc qiduiyaku var GameStat, var tmpYakuInfo, int targetPlayer
	trace "七対子の処理に入ります。"
	if (getRule(RULE_SEVEN_PAIRS) == 1) {
		addyaku tmpYakuInfo, "七対子", 1: setYakuInfo tmpYakuInfo, YAKUINF_FU, 50
	} else {
		addyaku tmpYakuInfo, "七対子", 2: setYakuInfo tmpYakuInfo, YAKUINF_FU, 25
	}
	countTilesInHand tileCount, GameStat, targetPlayer
	dim tmpTileList, 1: repeat 9:tmpTileList(cnt) = TILE_SUIT_CHARACTERS+cnt+1:loop
	WanzDui = countPairs(tileCount, tmpTileList)
	dim tmpTileList, 1: repeat 9:tmpTileList(cnt) = TILE_SUIT_CIRCLES+cnt+1:loop
	PinzDui = countPairs(tileCount, tmpTileList)
	dim tmpTileList, 1: repeat 9:tmpTileList(cnt) = TILE_SUIT_BAMBOOS+cnt+1:loop
	SouzDui = countPairs(tileCount, tmpTileList)
#ifdef SANMAS
	ZiDui = 0
	dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE,TILE_CHARACTER_NINE,TILE_CIRCLE_ONE,TILE_CIRCLE_NINE,TILE_BAMBOO_ONE,TILE_BAMBOO_NINE
	YaojiuDui = countPairs(tileCount, tmpTileList)
#else
	dim tmpTileList, 1: tmpTileList = TILE_EAST_WIND,TILE_SOUTH_WIND,TILE_WEST_WIND,TILE_NORTH_WIND,TILE_WHITE_DRAGON,TILE_GREEN_DRAGON,TILE_RED_DRAGON
	ZiDui = countPairs(tileCount, tmpTileList)
	dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE,TILE_CHARACTER_NINE,TILE_CIRCLE_ONE,TILE_CIRCLE_NINE,TILE_BAMBOO_ONE,TILE_BAMBOO_NINE,TILE_EAST_WIND,TILE_SOUTH_WIND,TILE_WEST_WIND,TILE_NORTH_WIND,TILE_WHITE_DRAGON,TILE_GREEN_DRAGON,TILE_RED_DRAGON
	YaojiuDui = countPairs(tileCount, tmpTileList)
#endif
	Cifr = countTileNumerals(tileCount)
	/* 一色系 */
	if (((WanzDui+ZiDui) == 7)||((PinzDui+ZiDui) == 7)||((SouzDui+ZiDui) == 7)) {
#ifndef SANMAS
		if (ZiDui == 0) {
#endif
			if (getRule(RULE_CHINIISOH) != 0) {
				addyaku tmpYakuInfo, "清一色", 3+getMenzen(GameStat, targetPlayer)
			} else {
				addyaku tmpYakuInfo, "清一色", 5+getMenzen(GameStat, targetPlayer)
			}
			/* 大車輪は面子手用のファイルで処理 */
#ifndef SANMAX
			/* 水戸三十五万石 */
			if (getRule(RULE_MITO350K) != 0) {
				if (WanzDui == 7) {
					if (Cifr <= 35) {
						addyakuman tmpYakuInfo, "水戸三十五万石", 1
						// 成立したら清一色の成立を解除する(青天井ルール用)
						delyaku tmpYakuInfo, "清一色"
					}
				}
			}
#endif
#ifndef SANMAS
		} else {
			addyaku tmpYakuInfo, "混一色", 2+getMenzen(GameStat, targetPlayer)
			/* 東京オリンピック */
			if (getRule(RULE_TOKYO_OLYMPIC_MONOCHROME) != 0) {
				if ((tileCount(TILE_CIRCLE_FIVE) >= 2)&&(tileCount(TILE_EAST_WIND) >= 2)) {
					addyakuman tmpYakuInfo, "東京オリンピック", 1
					delyaku tmpYakuInfo, "混一色"
				}
			}
			/* 翻満乃歩手地 */
			if (getRule(RULE_HONMANOPOTECHI) != 0) {
				if (tileCount(TILE_GREEN_DRAGON) >= 2) {
					addyaku tmpYakuInfo, "翻満乃歩手地", 1
				}
			}
		}
#endif
	} else {
		/* 絶一門 */
		if (getRule(RULE_CHUEIIMEN) != 0) {
			if ((WanzDui == 0)||(PinzDui == 0)||(SouzDui == 0)) {
#ifndef SANMAS
				if ((ZiDui == 0)||(getRule(RULE_CHUEIIMEN) != 2)) {
#endif
					addyaku tmpYakuInfo, "絶一門", 1
#ifndef SANMAS
				}
#endif
			}
		}
	}
	/* 一九字牌系 */
	if (YaojiuDui == 0) {
		if (getMenzen(GameStat, targetPlayer) == 1) { // バグ対策？
			addyaku tmpYakuInfo, "断幺九", 1
		}
	}
#ifndef SANMAS
	if (YaojiuDui == 7) {
		if ((ZiDui == 7)&&(getRule(RULE_SEVEN_HONOR_PAIRS) != 1)) {
			if (getRule(RULE_SEVEN_HONOR_PAIRS) == 0) {
				addyakuman tmpYakuInfo, "字一色", 1
			} else {
				addyakuman tmpYakuInfo, "七福星", 2
			}
			delyaku tmpYakuInfo, "混一色"
		} else {
			addyaku tmpYakuInfo, "混老頭", 2
		}
	}
#endif
	/* 推不倒 */
	if (getRule(RULE_TOIPUUTAO) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CIRCLE_ONE,TILE_CIRCLE_TWO,TILE_CIRCLE_THREE,TILE_CIRCLE_FOUR,TILE_CIRCLE_FIVE,TILE_CIRCLE_EIGHT,TILE_CIRCLE_NINE,TILE_BAMBOO_TWO,TILE_BAMBOO_FOUR,TILE_BAMBOO_FIVE,TILE_BAMBOO_SIX,TILE_BAMBOO_EIGHT,TILE_BAMBOO_NINE,TILE_WHITE_DRAGON
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount == 7) {
			if (getRule(RULE_TOIPUUTAO) == 3) {
				addyakuman tmpYakuInfo, "推不倒", 1
			} else {
				if (getRule(RULE_TOIPUUTAO) == 1) {
					addyaku tmpYakuInfo, "推不倒", 1
				} else {
					addyaku tmpYakuInfo, "推不倒", 4
				}
			}
		}
	}
#ifndef SANMAS
	/* 白有り緑一色 */
	if (getRule(RULE_ALL_GREEN_WITH_WHITE) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR, TILE_BAMBOO_SIX, TILE_BAMBOO_EIGHT, TILE_WHITE_DRAGON
		yakuflagcount = countPairs(tileCount, tmpTileList)
		dim tmpTileList, 1: tmpTileList = TILE_WHITE_DRAGON
		yakuflagcount += countPairs(tileCount, tmpTileList)
		if (yakuflagcount == 8) {
			switch getRule(RULE_ALL_GREEN_WITH_WHITE)
				case 1: addyaku tmpYakuInfo, "清二色", 2: swbreak
				case 2:
					addyakuman tmpYakuInfo, "白有り緑一色", 1
					// 成立したら混一色の成立を解除する(青天井ルール用)
					delyaku tmpYakuInfo, "混一色"
				swbreak
			swend
		}
	}
#endif
	/* 大草原 */
	if (getRule(RULE_DAISOUGEN) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR, TILE_BAMBOO_FIVE, TILE_BAMBOO_SIX, TILE_BAMBOO_SEVEN, TILE_BAMBOO_NINE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount == 7) {
			addyakuman tmpYakuInfo, "大草原", 1
			delyaku tmpYakuInfo, "清一色"
		}
	}
	/* 南大草原 */
	if (getRule(RULE_DAISOUGEN) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR, TILE_BAMBOO_FIVE, TILE_BAMBOO_SIX, TILE_BAMBOO_SEVEN, TILE_BAMBOO_NINE, TILE_SOUTH_WIND
		yakuflagcount = countPairs(tileCount, tmpTileList)
		dim tmpTileList,1: tmpTileList=TILE_SOUTH_WIND
		yakuflagcount2 = countPairs(tileCount, tmpTileList)
		if ((yakuflagcount == 7)&&(yakuflagcount2 == 1)) {
			addyakuman tmpYakuInfo, "南大草原", 1
			delyaku tmpYakuInfo, "混一色"
		}
	}
#ifndef SANMAS
	/* 桃一色 */
	if (getRule(RULE_TAOYISE) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CIRCLE_ONE, TILE_CIRCLE_THREE, TILE_CIRCLE_FIVE, TILE_CIRCLE_SIX, TILE_CIRCLE_SEVEN, TILE_CIRCLE_NINE, TILE_RED_DRAGON
		yakuflagcount = countPairs(tileCount, tmpTileList)
		countRedTilesInHand redCount, GameStat, targetPlayer
		if ((yakuflagcount == 7)&&(redCount(TILE_CIRCLE_FIVE) >= 1)&&(tileCount(TILE_RED_DRAGON) >= 2)) {
			addyakuman tmpYakuInfo, "桃一色", 1
			delyaku tmpYakuInfo, "混一色"
		}
	}
	/* 茶一色 */
	if (getRule(RULE_TAOYISE) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR, TILE_BAMBOO_SIX, TILE_BAMBOO_EIGHT, TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND, TILE_WHITE_DRAGON, TILE_GREEN_DRAGON
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount == 7) {
			addyakuman tmpYakuInfo, "茶一色", 1
		}
	}
	/* 黒一色 */
	if (getRule(RULE_HEIIISOH) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CIRCLE_TWO,TILE_CIRCLE_FOUR,TILE_CIRCLE_EIGHT,TILE_EAST_WIND,TILE_SOUTH_WIND,TILE_WEST_WIND,TILE_NORTH_WIND
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount == 7) {
			addyakuman tmpYakuInfo, "黒一色", 1
			delyaku tmpYakuInfo, "混一色"
		}
	}
#endif
	/* 断紅和 */
	if (getRule(RULE_NO_RED) != 0) {
#ifdef SANMAS
		dim tmpTileList, 1: tmpTileList = TILE_CIRCLE_TWO, TILE_CIRCLE_FOUR, TILE_CIRCLE_EIGHT, TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR, TILE_BAMBOO_SIX, TILE_BAMBOO_EIGHT
		yakuflagcount = countPairs(tileCount, tmpTileList)
		yakuflagcount2 = 0
#else
		dim tmpTileList, 1: tmpTileList = TILE_CIRCLE_TWO, TILE_CIRCLE_FOUR, TILE_CIRCLE_EIGHT, TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR, TILE_BAMBOO_SIX, TILE_BAMBOO_EIGHT, TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND, TILE_WHITE_DRAGON, TILE_GREEN_DRAGON
		yakuflagcount = countPairs(tileCount, tmpTileList)
		dim tmpTileList, 1: tmpTileList = TILE_EAST_WIND,TILE_SOUTH_WIND,TILE_WEST_WIND,TILE_NORTH_WIND,TILE_WHITE_DRAGON,TILE_GREEN_DRAGON
		yakuflagcount2 = countPairs(tileCount, tmpTileList)
#endif
		tmpyakuCensored = searchyaku(tmpYakuInfo, "黒一色")
		if ((yakuflagcount == 7)&&(tmpyakuCensored == 0)) {
			if ((yakuflagcount2 == 0)&&(getRule(RULE_NO_RED) == 2)) {
				/* 清断紅 */
#ifdef SANMAS
				addyaku tmpYakuInfo, "断紅和", 6
#else
				addyaku tmpYakuInfo, "清断紅", 6
#endif
			} else {
				/* 混断紅 */
#ifndef SANMAS
				if (getRule(RULE_NO_RED) == 2) {
					addyaku tmpYakuInfo, "混断紅", 3
				} else {
#endif
					addyaku tmpYakuInfo, "断紅和", 3
#ifndef SANMAS
				}
#endif
			}
		}
	}
	/* 花一色 */
	if (getRule(RULE_HUAIISOH) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE, TILE_CHARACTER_TWO, TILE_CHARACTER_THREE, TILE_CHARACTER_FOUR, TILE_CHARACTER_FIVE, TILE_CHARACTER_SIX, TILE_CHARACTER_SEVEN, TILE_CHARACTER_EIGHT, TILE_CHARACTER_NINE, TILE_RED_DRAGON, TILE_CIRCLE_ONE, TILE_CIRCLE_THREE, TILE_CIRCLE_FIVE, TILE_CIRCLE_SIX, TILE_CIRCLE_SEVEN, TILE_CIRCLE_NINE, TILE_BAMBOO_ONE, TILE_BAMBOO_FIVE, TILE_BAMBOO_SEVEN, TILE_BAMBOO_NINE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE, TILE_CHARACTER_TWO, TILE_CHARACTER_THREE, TILE_CHARACTER_FOUR, TILE_CHARACTER_FIVE, TILE_CHARACTER_SIX, TILE_CHARACTER_SEVEN, TILE_CHARACTER_EIGHT, TILE_CHARACTER_NINE, TILE_RED_DRAGON
		yakuflagcount2 = countPairs(tileCount, tmpTileList)
		if ((yakuflagcount == 7)&&(yakuflagcount2 < 7)) {
			addyaku tmpYakuInfo, "花一色", 1
		}
	}
	/* 赤一色 */
	if (getRule(RULE_AKAIISOH) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CIRCLE_ONE, TILE_CIRCLE_THREE, TILE_CIRCLE_FIVE, TILE_CIRCLE_SIX, TILE_CIRCLE_SEVEN, TILE_CIRCLE_NINE, TILE_BAMBOO_ONE, TILE_BAMBOO_FIVE, TILE_BAMBOO_SEVEN, TILE_BAMBOO_NINE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount == 7) {
			addyaku tmpYakuInfo, "赤一色", 2
			delyaku tmpYakuInfo, "花一色"
			delyaku tmpYakuInfo, "絶一門"
		}
	}
#ifndef SANMAS
	/* 五門斎 */
	if (getRule(RULE_UUMENCHII) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_EAST_WIND,TILE_SOUTH_WIND,TILE_WEST_WIND,TILE_NORTH_WIND
		WindDui = countPairs(tileCount, tmpTileList)
		dim tmpTileList, 1: tmpTileList = TILE_WHITE_DRAGON,TILE_GREEN_DRAGON,TILE_RED_DRAGON
		DragonDui = countPairs(tileCount, tmpTileList)
		if ((WanzDui >= 1)&&(PinzDui >= 1)&&(SouzDui >= 1)&&(WindDui >= 1)&&(DragonDui >= 1)) {
			addyaku tmpYakuInfo, "五門斎", 2
		}
	}
	/* 三元七対子 */
	if (getRule(RULE_DRAGON_PAIRS) != 0) {
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "字一色")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "七福星")
		if ((tileCount(TILE_WHITE_DRAGON) >= 2)&&(tileCount(TILE_GREEN_DRAGON) >= 2)&&(tileCount(TILE_RED_DRAGON) >= 2)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "三元七対子", 1
		}
	}
	/* 四喜七対子 */
	if (getRule(RULE_WIND_PAIRS) != 0) {
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "字一色")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "七福星")
		if ((tileCount(TILE_EAST_WIND) >= 2)&&(tileCount(TILE_SOUTH_WIND) >= 2)&&(tileCount(TILE_WEST_WIND) >= 2)&&(tileCount(TILE_NORTH_WIND) >= 2)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "四喜七対子", 1
		}
	}
#endif
	/* 奇数七対子 */
	if (getRule(RULE_ODD_PAIRS) != 0) {
		dim tmpTileList, 1: repeat TILE_NUMERAL_COLORS: tmpTileList(cnt*5) = cnt*TILE_SUIT_STEP+1,cnt*TILE_SUIT_STEP+3,cnt*TILE_SUIT_STEP+5,cnt*TILE_SUIT_STEP+7,cnt*TILE_SUIT_STEP+9: loop
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuFlagCount == 7) {
			addyaku tmpYakuInfo, "奇数七対子", 1
		}
	}
	/* 偶数七対子 */
	if (getRule(RULE_EVEN_PAIRS) != 0) {
		dim tmpTileList, 1: repeat TILE_NUMERAL_COLORS: tmpTileList(cnt*4) = cnt*TILE_SUIT_STEP+2,cnt*TILE_SUIT_STEP+4,cnt*TILE_SUIT_STEP+6,cnt*TILE_SUIT_STEP+8: loop
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuFlagCount == 7) {
			addyaku tmpYakuInfo, "偶数七対子", 2
			if ((getRule(RULE_KUITAN) == 2)&&(getMenzen(GameStat, targetPlayer) == 0)) {addYakuInfo tmpYakuInfo, YAKUINF_HAN_BASE, 1: addYakuInfo tmpYakuInfo, YAKUINF_HAN_BONUS, -1}
			delyaku tmpYakuInfo, "断幺九"
		}
	}
#ifndef SANMAS
	/* 紅白七対 */
	if (getRule(RULE_KOUHAKU_CHIITOI) != 0) {
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "字一色")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "七福星")
		if ((tileCount(TILE_WHITE_DRAGON) >= 2)&&(tileCount(TILE_RED_DRAGON) >= 2)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "紅白七対", 2
		}
	}
	/* 北の大地 */
	if (getRule(RULE_KITA_NO_DAICHI) != 0) {
		if ((tileCount(TILE_BAMBOO_TWO) >= 2)&&(tileCount(TILE_BAMBOO_THREE) >= 2)&&(tileCount(TILE_BAMBOO_FOUR) >= 2)&&(tileCount(TILE_BAMBOO_SIX) >= 2)&&(tileCount(TILE_BAMBOO_EIGHT) >= 2)&&(tileCount(TILE_NORTH_WIND) >= 2)&&(tileCount(TILE_WHITE_DRAGON) >= 2)) {
			switch TsumoTile(GameStat, targetPlayer) /* 何で和了ったかで分岐 */
				case TILE_NORTH_WIND: /* 北で和了るとダブル役満 */
					addyakuman tmpYakuInfo, "北の大地", 2
					swbreak
				case TILE_WHITE_DRAGON: /* 白で和了るとトリプル役満 */
					addyakuman tmpYakuInfo, "北の大地", 3
					swbreak
				default:
					addyakuman tmpYakuInfo, "北の大地", 1
			swend
			delyaku tmpYakuInfo, "混一色"
		}
	}
	/* 紅白饅頭 */
	if (getRule(RULE_KOHAKU_MANJU) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE, TILE_CHARACTER_TWO, TILE_CHARACTER_THREE, TILE_CHARACTER_FOUR, TILE_CHARACTER_FIVE, TILE_CHARACTER_SIX, TILE_CHARACTER_SEVEN, TILE_CHARACTER_EIGHT, TILE_CHARACTER_NINE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if ((tileCount(TILE_WHITE_DRAGON) >= 2)&&(tileCount(TILE_RED_DRAGON) >= 2)&&(yakuflagcount >= 5)) {
			switch getRule(RULE_KOHAKU_MANJU)
				case 1: addyaku tmpYakuInfo, "紅白饅頭", 1: swbreak
				case 2: addyaku tmpYakuInfo, "紅白饅頭", 2: swbreak
			swend
			delyaku tmpYakuInfo, "紅白七対"
		}
	}
	/* 世界一 */
	if (getRule(RULE_SEKAIICHI) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE, TILE_CIRCLE_ONE, TILE_BAMBOO_ONE, TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyakuman tmpYakuInfo, "世界一", 1
			delyaku tmpYakuInfo, "四喜七対子"
		}
	}
#endif
	/* 素数役満 */
	if (getRule(RULE_PRIME_NUMBER) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_TWO, TILE_CHARACTER_THREE, TILE_CHARACTER_FIVE, TILE_CHARACTER_SEVEN, TILE_CIRCLE_TWO, TILE_CIRCLE_THREE, TILE_CIRCLE_FIVE, TILE_CIRCLE_SEVEN, TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FIVE, TILE_BAMBOO_SEVEN
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyakuman tmpYakuInfo, "素数役満", 1
		}
	}
	/* 小於五 */
	if (getRule(RULE_XIAOYUWU) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE, TILE_CHARACTER_TWO, TILE_CHARACTER_THREE, TILE_CHARACTER_FOUR, TILE_CIRCLE_ONE, TILE_CIRCLE_TWO, TILE_CIRCLE_THREE, TILE_CIRCLE_FOUR, TILE_BAMBOO_ONE, TILE_BAMBOO_TWO, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyaku tmpYakuInfo, "小於五", 6
		}
	}
	/* 大於五 */
	if (getRule(RULE_DAYUWU) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_SIX, TILE_CHARACTER_SEVEN, TILE_CHARACTER_EIGHT, TILE_CHARACTER_NINE, TILE_CIRCLE_SIX, TILE_CIRCLE_SEVEN, TILE_CIRCLE_EIGHT, TILE_CIRCLE_NINE, TILE_BAMBOO_SIX, TILE_BAMBOO_SEVEN, TILE_BAMBOO_EIGHT, TILE_BAMBOO_NINE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyaku tmpYakuInfo, "大於五", 6
		}
	}
	/* 全小 */
	if (getRule(RULE_ALL_SMALL) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE, TILE_CHARACTER_TWO, TILE_CHARACTER_THREE, TILE_CIRCLE_ONE, TILE_CIRCLE_TWO, TILE_CIRCLE_THREE, TILE_BAMBOO_ONE, TILE_BAMBOO_TWO, TILE_BAMBOO_THREE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyaku tmpYakuInfo, "全小", 6
			delyaku tmpYakuInfo, "小於五"
		}
	}
	/* 全中 */
	if (getRule(RULE_ALL_MIDDLE) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_FOUR, TILE_CHARACTER_FIVE, TILE_CHARACTER_SIX, TILE_CIRCLE_FOUR, TILE_CIRCLE_FIVE, TILE_CIRCLE_SIX, TILE_BAMBOO_FOUR, TILE_BAMBOO_FIVE, TILE_BAMBOO_SIX
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyaku tmpYakuInfo, "全中", 6
			delyaku tmpYakuInfo, "断幺九"
		}
	}
	/* 全大 */
	if (getRule(RULE_ALL_LARGE) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_SEVEN, TILE_CHARACTER_EIGHT, TILE_CHARACTER_NINE, TILE_CIRCLE_SEVEN, TILE_CIRCLE_EIGHT, TILE_CIRCLE_NINE, TILE_BAMBOO_SEVEN, TILE_BAMBOO_EIGHT, TILE_BAMBOO_NINE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyaku tmpYakuInfo, "全大", 6
			delyaku tmpYakuInfo, "大於五"
		}
	}
	/* 橙一色 */
	/*
		七対子の橙一色は４枚使いを認めない限りは２２３３４４５５６６７７８８の形のみ
		これは大数隣と同じ形であり、橙一色のみ採用の場合でも面子手として扱われる
	*/
	/* 上級断幺九 */
	if (getRule(RULE_HIGH_TANYAO) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_THREE, TILE_CHARACTER_FOUR, TILE_CHARACTER_FIVE, TILE_CHARACTER_SIX, TILE_CHARACTER_SEVEN, TILE_CIRCLE_THREE, TILE_CIRCLE_FOUR, TILE_CIRCLE_FIVE, TILE_CIRCLE_SIX, TILE_CIRCLE_SEVEN, TILE_BAMBOO_THREE, TILE_BAMBOO_FOUR, TILE_BAMBOO_FIVE, TILE_BAMBOO_SIX, TILE_BAMBOO_SEVEN
		yakuflagcount = countPairs(tileCount, tmpTileList)
		if (yakuflagcount >= 7) {
			addyaku tmpYakuInfo, "上級断幺九", 2
			delyaku tmpYakuInfo, "断幺九"
		}
	}
	/* 金一色 */
	if (getRule(RULE_ALLMONEY) != 0) {
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE, TILE_CIRCLE_ONE, TILE_CIRCLE_TWO, TILE_CIRCLE_THREE, TILE_CIRCLE_FOUR, TILE_CIRCLE_FIVE, TILE_CIRCLE_SIX, TILE_CIRCLE_SEVEN, TILE_CIRCLE_EIGHT, TILE_CIRCLE_NINE
		yakuflagcount = countPairs(tileCount, tmpTileList)
		dim tmpTileList, 1: tmpTileList = TILE_CHARACTER_ONE
		yakuflagcount2 = countPairs(tileCount, tmpTileList)
		if ((yakuflagcount >= 7)&&(yakuflagcount2 == 1)) {
			addyaku tmpYakuInfo, "金一色", 4
			delyaku tmpYakuInfo, "絶一門"
		}
	}
	/* 三色同対 */
	if (getRule(RULE_SANSHOKU_DOUTOI) != 0) {
		yakuFlag = 0
		repeat 9, 1
			if ((tileCount(TILE_SUIT_CHARACTERS+cnt) >= 2)&&(tileCount(TILE_SUIT_CIRCLES+cnt) >= 2)&&(tileCount(TILE_SUIT_BAMBOOS+cnt) >= 2)) {
				yakuFlag = 1
			}
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "三色同対", 1
		}
	}
#ifndef SANMAS
	/* 逆車輪 */
	if (getRule(RULE_SANSHOKU_DOUTOI) != 0) {
		yakuflagcount = 0
		repeat (TILE_DRAGON_MAX-TILE_DRAGON_MIN+1)+(TILE_WIND_MAX-TILE_WIND_MIN+1), TILE_WIND_MIN
			if (tileCount(cnt) >= 2) {yakuflagcount += 1}
		loop
		if ((tileCount(TILE_CIRCLE_ONE) >= 2)&&(tileCount(TILE_CIRCLE_NINE) >= 2)&&(yakuflagcount == 5)) {
			addyaku tmpYakuInfo, "逆車輪", 1
		}
	}
#endif
return
#global
