/*=============================
 * HSP麻雀クライアントMiHaJong
 *    [刻子役判定ルーチン]
 *=============================
 */

#module
#include "const.hsp"
#deffunc countKez array KezCount, array haiMianziDat
/*
		countKez p1, p2
		刻子の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する刻子または槓子を数え、種類別に格納します。
		また、statには見つかった刻子・槓子の数が格納されます。
*/
	trace "刻子・槓子の種類を調べます。"
	dim KezCount, TILE_NONFLOWER_MAX: Kezi = 0
	repeat 5
		if (haiMianziDat(cnt) >= MELD_TRIPLET*MELD_TYPE_STEP) {
			KezCount(haiMianziDat(cnt) \ TILE_CODE_MAXIMUM)++
			Kezi++
		}
	loop
	return Kezi
#global

#module
#include "const.hsp"
#deffunc countDuiz array DuiCount, array haiMianziDat
/*
		countDuiz p1, p2
		対子・刻子・槓子の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する対子、刻子または槓子を数え、種類別に格納します。
*/
	trace "対子・刻子・槓子の種類を調べます。"
	dim DuiCount, TILE_NONFLOWER_MAX
	countKez DuiCount, haiMianziDat
	DuiCount(haiMianziDat(0) \ TILE_CODE_MAXIMUM)++
	return
#global

/* 刻子系の役 */
#module
#include "const.hsp"
#deffunc tripletyaku var GameStat, var tmpYakuInfo, array haiMianziDat, int targetPlayer, int haiFuriten, int TsumoAgari, int MachiType
	trace "刻子役の処理に入ります。"
	countKez KezCount, haiMianziDat: Kezi = stat
	countDuiz DuiCount, haiMianziDat
	WindCnt = 0: DragonCnt = 0: AnKe = 0
	repeat 5
		if (((haiMianziDat(cnt) \ TILE_CODE_MAXIMUM) >= TILE_WIND_MIN)&&((haiMianziDat(cnt) \ TILE_CODE_MAXIMUM) <= TILE_WIND_MAX)) {
			if (cnt == 0) {WindCnt += 1} else {WindCnt += 2}
		}
		if (((haiMianziDat(cnt) \ TILE_CODE_MAXIMUM) >= TILE_DRAGON_MIN)&&((haiMianziDat(cnt) \ TILE_CODE_MAXIMUM) <= TILE_DRAGON_MAX)) {
			if (cnt == 0) {DragonCnt += 1} else {DragonCnt += 2}
		}
		if (((haiMianziDat(cnt) / MELD_TYPE_STEP) == MELD_TRIPLET_CONCEALED)||((haiMianziDat(cnt) / MELD_TYPE_STEP) == MELD_QUAD_CONCEALED)) {
			AnKe++
		}
	loop
	if ((MachiType == MACHI_SHANPON)&&(TsumoAgari == 0)) {AnKe-- /* 双ポン待ちの出和了りでできた刻子は明刻 */}
	/* 四暗刻 */
	if (AnKe == 4) {
		if ((getHeavenHandFlag(GameStat))&&(getRule(RULE_DOUBLE_YAKUMAN) == "0")) {
			addyakuman tmpYakuInfo, "四暗刻", 2
		} else {
			// 振聴の時は単騎待ちの扱いとしないように変更
			// 九連や国士と違って四単の振聴は待ちを変えれば大丈夫なので
			// あんまり意味無いかも
			if ((MachiType == MACHI_TANKI)&&(haiFuriten == 0)&&(getRule(RULE_DOUBLE_YAKUMAN) == "0")) {
				addyakuman tmpYakuInfo, "四暗刻単騎", 2
			} else {
				addyakuman tmpYakuInfo, "四暗刻", 1
			}
		}
	}
	/* 三暗刻 */
	if (AnKe == 3) {
		addyaku tmpYakuInfo, "三暗刻", 2
	}
	/* 五連刻 */
	SiLianKeFlag = 0
	if (getRule(RULE_GOLDEN_LADDER) != "0") {
		yakuFlag = 0
		repeat TILE_NUMERAL_MAX-1,1
			if ((KezCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)&&(KezCount(cnt+2) >= 1)&&(KezCount(cnt+3) >= 1)&&(DuiCount(cnt+4) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)&&(KezCount(cnt+2) >= 1)&&(KezCount(cnt+3) >= 1)&&(DuiCount(cnt-1) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			SiLianKeFlag = 1
			addyakuman tmpYakuInfo, "金梯", 2
			delyaku tmpYakuInfo, "清一色"
		}
	}
	/* 四連刻 */
	SiLianKeFlag = 0
	if (getRule(RULE_SUURENKOH) != "0") {
		yakuFlag = 0
		repeat TILE_NUMERAL_MAX
			if ((KezCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)&&(KezCount(cnt+2) >= 1)&&(KezCount(cnt+3) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "金梯")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			SiLianKeFlag = 1
			if (getRule(RULE_SUURENKOH) != "0") {
				addyakuman tmpYakuInfo, "四連刻", 1
			} else {
				addyaku tmpYakuInfo, "四連刻", 8
			}
		}
	}
	/* 三連刻 */
	if (getRule(RULE_SANRENKOH) != "0") {
		yakuFlag = 0
		repeat TILE_NUMERAL_MAX
			if ((KezCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)&&(KezCount(cnt+2) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "四連刻")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "金梯")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			if (getRule(RULE_SANRENKOH) == "2") {
				addyaku tmpYakuInfo, "三連刻", 3
			} else {
				addyaku tmpYakuInfo, "三連刻", 2
			}
		}
	}
	/* 三色同刻 */
	yakuFlag = 0
	repeat TILE_NUMERAL_MAX/TILE_NUMERAL_COLORS
		if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
	loop
	if (yakuFlag) {
		if (getRule(RULE_SANSHOKU_DOUKOH) != "0") {
			addyaku tmpYakuInfo, "三色同刻", 3
		} else {
			addyaku tmpYakuInfo, "三色同刻", 2
		}
	}
	/* 小三元 */
	if (DragonCnt == 5) {
		addyaku tmpYakuInfo, "小三元", 2
	}
	/* 大三元 */
	if (DragonCnt == 6) {
		addyakuman tmpYakuInfo, "大三元", 1
	}
	/* 小四喜 */
	if (WindCnt == 7) {
		addyakuman tmpYakuInfo, "小四喜", 1
		// 四喜和が成立したら混一色の成立を解除する(青天井ルール用)
		// 字一色は複合してよい
		delyaku tmpYakuInfo, "混一色"
	}
	/* 大四喜 */
	if (WindCnt == 8) {
		if (getRule(RULE_DOUBLE_YAKUMAN) == "0") {
			addyakuman tmpYakuInfo, "大四喜", 2
		} else {
			addyakuman tmpYakuInfo, "大四喜", 1
		}
		// 四喜和が成立したら混一色の成立を解除する(青天井ルール用)
		// 字一色は複合してよい
		delyaku tmpYakuInfo, "混一色"
	}
	/* 対々和 */
	if ((Kezi == 4)&&(WindCnt < 8)&&(AnKe < 4)&&(SiLianKeFlag == 0)) { // 大四喜や四暗刻とは複合させないようにする
		tmpyakuCensored = 0 // 清老頭が成立してたら飛ばす 混老頭とは複合させる
		tmpyakuCensored += searchyaku(tmpYakuInfo, "清老頭")
		if (tmpyakuCensored == 0) {
			addyaku tmpYakuInfo, "対々和", 2
		}
	}
	/* 役牌 */
	nmflag1 = 0: nmflag2 = 0: nmflag3 = 0
	jikazeflag = 0: bakazeflag = 0
	repeat TILE_WIND_MAX-TILE_WIND_MIN+1
		// 大四喜の時は風牌でさらに役がつかないようにする
		if ((KezCount(cnt+TILE_WIND_MIN) >= 1)&&(WindCnt < 8)) {
			if ((getRound(GameStat)/4) == cnt) {
				addyaku tmpYakuInfo, "役牌・場風", 1
				if (cnt == 1) {nmflag1 = 1}
				if (cnt == 2) {nmflag2 = 1}
				bakazeflag = cnt+TILE_WIND_MIN
			}
			if (playerWind(targetPlayer, getRound(GameStat)) == cnt) {
				addyaku tmpYakuInfo, "役牌・自風", 1
				if (cnt == 1) {nmflag1 = 1}
				if (cnt == 2) {nmflag2 = 1}
				jikazeflag = cnt+TILE_WIND_MIN
			}
			if (getRule(RULE_KAIMENKAZE) != "0") {
				if (playerWind(getWareme(GameStat), getRound(GameStat)) == cnt) {
					addyaku tmpYakuInfo, "役牌・開門風", 1
				}
			}
#ifndef SANMA
			if (getRule(RULE_URAKAZE) != "0") {
				if (playerWind(targetPlayer+2, getRound(GameStat)) == cnt) {
					addyaku tmpYakuInfo, "役牌・裏風", 1
				}
			}
#endif
		}
	loop
	// 大三元が成立している場合、三元牌でさらに役がつかないようにする
	if ((KezCount(TILE_WHITE_DRAGON) >= 1)&&(DragonCnt < 6)) {
		addyaku tmpYakuInfo, "役牌・白", 1
	}
	if ((KezCount(TILE_GREEN_DRAGON) >= 1)&&(DragonCnt < 6)) {
		addyaku tmpYakuInfo, "役牌・發", 1
	}
	if ((KezCount(TILE_RED_DRAGON) >= 1)&&(DragonCnt < 6)) {
		addyaku tmpYakuInfo, "役牌・中", 1
		nmflag3 = 1
	}
	/* 三風刻 */
	if (getRule(RULE_THREE_WINDS) != "0") {
		if (WindCnt == 6) {
			addyaku tmpYakuInfo, "三風刻", 2
		}
	}
	/* 客風三刻 */
	if (getRule(RULE_THREE_OTAKAZE) != "0") {
		tmpyakuCensored = 0 // メチャメチャ強引な実装…
		tmpyakuCensored += searchyaku(tmpYakuInfo, "役牌・場風")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "役牌・自風")
		if ((WindCnt == 6)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "客風三刻", 3
			delyaku tmpYakuInfo, "三風刻"
		}
	}
	/* 西中島南方 */
	if (getRule(RULE_NISHINAKAJIMA) != "0") {
		if ((nmflag1+nmflag2+nmflag3) == 3) {
			addyakuman tmpYakuInfo, "西中島南方", 1
		}
	}
	/* 風花雪月 */
	if (getRule(RULE_FUUKA_SETSUGETSU) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_WHITE_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&((jikazeflag > 0)||(bakazeflag > 0))) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "風花雪月", 1
			delyaku tmpYakuInfo, "対々和"
			delyaku tmpYakuInfo, "役牌・場風"
			delyaku tmpYakuInfo, "役牌・自風"
		}
	}
	/* 花鳥風月 */
	if (getRule(RULE_KACHOU_FUUGETSU) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)&&((jikazeflag > 0)||(bakazeflag > 0))) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "花鳥風月", 1
			delyaku tmpYakuInfo, "対々和"
			delyaku tmpYakuInfo, "役牌・場風"
			delyaku tmpYakuInfo, "役牌・自風"
		}
	}
	/* 東北自動車道 */
	if (getRule(RULE_TOHOKU_EXPWY) != "0") {
		yakuFlag = 0
		if ((DuiCount(TILE_SUIT_CIRCLES+2) >= 1)&&(DuiCount(TILE_SUIT_CIRCLES+4) >= 1)&&(DuiCount(TILE_SUIT_CIRCLES+6) >= 1)&&(DuiCount(TILE_EAST_WIND) >= 1)&&(DuiCount(TILE_NORTH_WIND) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "東北自動車道", 1
			delyaku tmpYakuInfo, "対々和"
			delyaku tmpYakuInfo, "混一色"
		}
	}
	/* 中国系のおめでたい役 */
	if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_EAST_WIND) >= 1)&&(KezCount(TILE_WHITE_DRAGON) >= 1)&&(KezCount(TILE_RED_DRAGON) >= 1)&&(haiMianziDat(0) == TILE_SUIT_CIRCLES+1)&&(getRule(RULE_KOUKEI_HOUGYOU) != "0")) {
		addyakuman tmpYakuInfo, "紅鶏報暁", 1
		delyaku tmpYakuInfo, "対々和"
		delyaku tmpYakuInfo, "絶一門"
		delyaku tmpYakuInfo, "役牌・白"
		delyaku tmpYakuInfo, "役牌・中"
	} else {
		if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_EAST_WIND) >= 1)&&(KezCount(TILE_RED_DRAGON) >= 1)&&(getRule(RULE_KINKEI_HOUGYOU) != "0")) {
			addyaku tmpYakuInfo, "金鶏報暁", 3
			delyaku tmpYakuInfo, "役牌・中"
		} else {
			if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_EAST_WIND) >= 1)&&(KezCount(TILE_WHITE_DRAGON) >= 1)&&((getFlowerFlag(GameStat, targetPlayer)&FLOWER_SPRING) == FLOWER_SPRING)&&(getRule(RULE_GINKEI_HOUSHUN) != "0")) {
				addyaku tmpYakuInfo, "銀鶏報春", 5
				delyaku tmpYakuInfo, "役牌・白"
			}
		}
	}
	/* 四跳牌刻 */
	if (getRule(RULE_SUUCHAOPAIKOH) != "0") {
		yakuFlag = 0
		repeat TILE_NUMERAL_COLORS
			if ((KezCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+2) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+6) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+8) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+7) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+9) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "四跳牌刻", 1
			delyaku tmpYakuInfo, "対々和"
		}
	}
	/* 跳牌刻 */
	if (getRule(RULE_CHAOPAIKOH) != "0") {
		yakuFlag = 0
		repeat TILE_NUMERAL_COLORS
			if ((KezCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+2) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+6) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+6) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+8) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+7) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+9) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "四跳牌刻")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "跳牌刻", 2
		}
	}
	/* 筋牌刻 */
	if (getRule(RULE_CHINPAIKOH) != "0") {
		yakuFlag = 0
		repeat TILE_NUMERAL_COLORS
			if ((KezCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+2) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+8) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+6) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+9) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "筋牌刻", 2
		}
	}
	/* 頂三刻 */
	if (getRule(RULE_TINSANKOH) != "0") {
		yakuFlag = 0
		repeat TILE_NUMERAL_COLORS
			if ((KezCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+9) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "頂三刻", 2
		}
	}
	/* 二色五連刻 */
	if (getRule(RULE_SILVER_LADDER) != "0") {
		yakuFlag = 0
		sdim parsedat, 8, 90
		parsedat( 0) = "00001","00002","00010","00011","00020","00022","00100","00101","00110","00111","00200","00202","00220","00222","01000","01001","01010","01011","01100","01101","01110","01111","02000","02002","02020","02022","02200","02202","02220","02222"
		parsedat(30) = "10000","10001","10010","10011","10100","10101","10110","10111","11000","11001","11010","11011","11100","11101","11110","11112","11121","11122","11211","11212","11221","11222","12111","12112","12121","12122","12211","12212","12221","12222"
		parsedat(60) = "20000","20002","20020","20022","20200","20202","20220","20222","21111","21112","21121","21122","21211","21212","21221","21222","22000","22002","22020","22022","22111","22112","22121","22122","22200","22202","22211","22212","22220","22221"
		repeat 90
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			tmpsuit5 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 6,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+1) >= 1)&&(KezCount(cnt+tmpsuit3+2) >= 1)&&(KezCount(cnt+tmpsuit4+3) >= 1)&&(DuiCount(cnt+tmpsuit5+4) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+1) >= 1)&&(KezCount(cnt+tmpsuit3+2) >= 1)&&(KezCount(cnt+tmpsuit4+3) >= 1)&&(DuiCount(cnt+tmpsuit5-1) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "銀梯", 4
		}
	}
	/* 二色四連刻 */
	if (getRule(RULE_NISHOKU_SUURENKOH) != "0") {
		yakuFlag = 0
		sdim parsedat, 8, 42
		parsedat = "0001","0002","0010","0011","0020","0022","0100","0101","0110","0111","0200","0202","0220","0222","1000","1001","1010","1011","1100","1101","1110","1112","1121","1122","1211","1212","1221","1222","2000","2002","2020","2022","2111","2112","2121","2122","2200","2202","2211","2212","2220","2221"
		repeat 42
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 6,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+1) >= 1)&&(KezCount(cnt+tmpsuit3+2) >= 1)&&(KezCount(cnt+tmpsuit4+3) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "二色四連刻", 3
			delyaku tmpYakuInfo, "対々和"
		}
	}
	/* 二色三連刻 */
	if (getRule(RULE_NISHOKU_SANRENKOH) != "0") {
		yakuFlag = 0
		repeat 6
			switch cnt
				case 0: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
				case 1: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 2: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 3: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 4: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 5: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
			swend
			repeat 7,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit1+1) >= 1)&&(KezCount(cnt+tmpsuit2+2) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+1) >= 1)&&(KezCount(cnt+tmpsuit1+2) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit2) >= 1)&&(KezCount(cnt+tmpsuit1+1) >= 1)&&(KezCount(cnt+tmpsuit1+2) >= 1)) {yakuFlag = 1}
			loop
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "二色四連刻")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "二色三連刻", 1
		}
	}
	/* 二色四跳牌刻 */
	if (getRule(RULE_NISHOKU_SUUCHAOPAIKOH) != "0") {
		yakuFlag = 0
		sdim parsedat, 8, 42
		parsedat = "0001","0002","0010","0011","0020","0022","0100","0101","0110","0111","0200","0202","0220","0222","1000","1001","1010","1011","1100","1101","1110","1112","1121","1122","1211","1212","1221","1222","2000","2002","2020","2022","2111","2112","2121","2122","2200","2202","2211","2212","2220","2221"
		repeat 42
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 3,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+2) >= 1)&&(KezCount(cnt+tmpsuit3+4) >= 1)&&(KezCount(cnt+tmpsuit4+6) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "二色跳牌刻", 3
			delyaku tmpYakuInfo, "対々和"
		}
	}
	/* 二色跳牌刻 */
	if (getRule(RULE_NISHOKU_CHAOPAIKOH) != "0") {
		yakuFlag = 0
		repeat 6
			switch cnt
				case 0: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
				case 1: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 2: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 3: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 4: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 5: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
			swend
			repeat 5,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit1+2) >= 1)&&(KezCount(cnt+tmpsuit2+4) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+2) >= 1)&&(KezCount(cnt+tmpsuit1+4) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit2) >= 1)&&(KezCount(cnt+tmpsuit1+2) >= 1)&&(KezCount(cnt+tmpsuit1+4) >= 1)) {yakuFlag = 1}
			loop
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "二色四跳牌刻")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "二色跳牌刻", 2
		}
	}
	/* 二色筋牌刻 */
	if (getRule(RULE_NISHOKU_CHINPAIKOH) != "0") {
		yakuFlag = 0
		repeat 6
			switch cnt
				case 0: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
				case 1: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 2: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 3: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 4: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 5: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
			swend
			repeat 3,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit1+3) >= 1)&&(KezCount(cnt+tmpsuit2+6) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+3) >= 1)&&(KezCount(cnt+tmpsuit1+6) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit2) >= 1)&&(KezCount(cnt+tmpsuit1+3) >= 1)&&(KezCount(cnt+tmpsuit1+6) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "二色筋牌刻", 2
		}
	}
	/* 二色頂三刻 */
	if (getRule(RULE_NISHOKU_TINSANKOH) != "0") {
		yakuFlag = 0
		repeat 6
			switch cnt
				case 0: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
				case 1: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 2: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 3: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 4: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 5: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
			swend
			if ((KezCount(1+tmpsuit1) >= 1)&&(KezCount(5+tmpsuit1) >= 1)&&(KezCount(9+tmpsuit2) >= 1)) {yakuFlag = 1}
			if ((KezCount(1+tmpsuit1) >= 1)&&(KezCount(5+tmpsuit2) >= 1)&&(KezCount(9+tmpsuit1) >= 1)) {yakuFlag = 1}
			if ((KezCount(1+tmpsuit2) >= 1)&&(KezCount(5+tmpsuit1) >= 1)&&(KezCount(9+tmpsuit1) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "二色頂三刻", 2
		}
	}
	/* 三色五連刻 */
	if (getRule(RULE_SKY_LADDER) != "0") {
		yakuFlag = 0
		sdim parsedat, 8, 150
		parsedat(  0) = "00012","00021","00102","00112","00120","00121","00122","00201","00210","00211","00212","00221","01002","01012","01020","01021","01022","01102","01112","01120","01121","01122","01200","01201","01202","01210","01211","01212","01220","01221","01222","02001","02010","02011","02012","02021","02100","02101","02102","02110","02111","02112","02120","02121","02122","02201","02210","02211","02212","02221"
		parsedat( 50) = "10002","10012","10020","10021","10022","10102","10112","10120","10121","10122","10200","10201","10202","10210","10211","10212","10220","10221","10222","11002","11012","11020","11021","11022","11102","11120","11200","11201","11202","11210","11220","12000","12001","12002","12010","12011","12012","12020","12021","12022","12100","12101","12102","12110","12120","12200","12201","12202","12210","12220"
		parsedat(100) = "20001","20010","20011","20012","20021","20100","20101","20102","20110","20111","20112","20120","20121","20122","20201","20210","20211","20212","20221","21000","21001","21002","21010","21011","21012","21020","21021","21022","21100","21101","21102","21110","21120","21200","21201","21202","21210","21220","22001","22010","22011","22012","22021","22100","22101","22102","22110","22120","22201","22210"
		repeat 150
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			tmpsuit5 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 6,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+1) >= 1)&&(KezCount(cnt+tmpsuit3+2) >= 1)&&(KezCount(cnt+tmpsuit4+3) >= 1)&&(DuiCount(cnt+tmpsuit5+4) >= 1)) {yakuFlag = 1}
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+1) >= 1)&&(KezCount(cnt+tmpsuit3+2) >= 1)&&(KezCount(cnt+tmpsuit4+3) >= 1)&&(DuiCount(cnt+tmpsuit5-1) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "雲梯", 4
		}
	}
	/* 三色四連刻 */
	if (getRule(RULE_SANSHOKU_SUURENKOH) != "0") {
		yakuFlag = 0
		sdim parsedat, 8, 36
		parsedat = "0012","0021","0102","0112","0120","0121","0122","0201","0210","0211","0212","0221","1002","1012","1020","1021","1022","1102","1120","1200","1201","1202","1210","1220","2001","2010","2011","2012","2021","2100","2101","2102","2110","2120","2201","2210"
		repeat 36
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 6,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+1) >= 1)&&(KezCount(cnt+tmpsuit3+2) >= 1)&&(KezCount(cnt+tmpsuit4+3) >= 1)) {yakuFlag = 1}
			loop
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "雲梯")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "三色四連刻", 3
		}
	}
	/* 三色連刻 */
	if (getRule(RULE_SANSHOKU_RENKOH) != "0") {
		yakuFlag = 0
		repeat 7, 1
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+1) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+1) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "雲梯")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "三色四連刻")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "三色連刻", 2
		}
	}
	/* 三色四跳牌刻 */
	if (getRule(RULE_SANSHOKU_SUUCHAOPAIKOH) != "0") {
		yakuFlag = 0
		sdim parsedat, 8, 36
		parsedat = "0012","0021","0102","0112","0120","0121","0122","0201","0210","0211","0212","0221","1002","1012","1020","1021","1022","1102","1120","1200","1201","1202","1210","1220","2001","2010","2011","2012","2021","2100","2101","2102","2110","2120","2201","2210"
		repeat 36
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 3,1
				if ((KezCount(cnt+tmpsuit1) >= 1)&&(KezCount(cnt+tmpsuit2+2) >= 1)&&(KezCount(cnt+tmpsuit3+4) >= 1)&&(KezCount(cnt+tmpsuit4+6) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "三色四跳牌刻", 3
		}
	}
	/* 三色跳牌刻 */
	if (getRule(RULE_SANSHOKU_CHAOPAIKOH) != "0") {
		yakuFlag = 0
		repeat 5,1
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+4) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+4) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+4) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+4) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+4) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+4) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = searchyaku(tmpYakuInfo, "三色四跳牌刻")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "三色跳牌刻", 2
		}
	}
	/* 三色筋牌刻 */
	if (getRule(RULE_SANSHOKU_CHINPAIKOH) != "0") {
		yakuFlag = 0
		repeat 3,1
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+3) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+6) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+3) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+6) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+3) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+6) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS+3) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+6) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+3) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+6) >= 1)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES+3) >= 1)&&(KezCount(cnt+TILE_SUIT_CHARACTERS+6) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "三色筋牌刻", 2
		}
	}
	/* 三色頂三刻 */
	if (getRule(RULE_SANSHOKU_TINSANKOH) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+5) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+5) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+5) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+9) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "三色頂三刻", 2
		}
	}
	/* 二同刻 */
	if (getRule(RULE_RYANDOUKOH) != "0") {
		yakuFlag = 0
		yakuTmpFlag = 0
		repeat 9, 1
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)) {yakuTmpFlag++}
		loop: if (yakuTmpFlag == 2) {yakuFlag = 1}
		yakuTmpFlag = 0
		repeat 9, 1
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuTmpFlag++}
		loop: if (yakuTmpFlag == 2) {yakuFlag = 1}
		yakuTmpFlag = 0
		repeat 9, 1
			if ((KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuTmpFlag++}
		loop: if (yakuTmpFlag == 2) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "二同刻", 2
		}
	}
	/* 茴香ポン */
	if (getRule(RULE_UIKYOU_TOITOI) != "0") {
		yakuFlag = 0
		repeat 9, 1
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {
				if (KezCount(cnt+TILE_SUIT_CHARACTERS-1) >= 1) {yakuFlag = 1}
				if (KezCount(cnt+TILE_SUIT_CIRCLES-1) >= 1) {yakuFlag = 1}
				if (KezCount(cnt+TILE_SUIT_BAMBOOS-1) >= 1) {yakuFlag = 1}
				if (KezCount(cnt+TILE_SUIT_CHARACTERS+1) >= 1) {yakuFlag = 1}
				if (KezCount(cnt+TILE_SUIT_CIRCLES+1) >= 1) {yakuFlag = 1}
				if (KezCount(cnt+TILE_SUIT_BAMBOOS+1) >= 1) {yakuFlag = 1}
			}
		loop
		if (yakuFlag) {
			if (getRule(RULE_UIKYOU_TOITOI) == "2") {
				addyaku tmpYakuInfo, "茴香ポン", 5
			} else {
				addyaku tmpYakuInfo, "茴香ポン", 6
			}
		}
	}
#ifndef SANMAX
	if ((KezCount(TILE_SUIT_CHARACTERS+8) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+8) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+8) >= 1)) {
		/* 八仙乗風過海 */
		if (getRule(RULE_BAXIAN_CHENGFENG) != "0") {
			yakuFlag = 0
			if ((KezCount(TILE_WHITE_DRAGON) >= 1)&&((DuiCount(TILE_EAST_WIND) >= 1)||(DuiCount(TILE_SOUTH_WIND) >= 1))) {yakuFlag = 1}
			if (yakuFlag) {
				addyakuman tmpYakuInfo, "八仙乗風過海", 1
				delyaku tmpYakuInfo, "対々和"
				delyaku tmpYakuInfo, "三色同刻"
				delyaku tmpYakuInfo, "役牌・白"
			}
		}
		/* 八仙戯鳳 */
		if (getRule(RULE_BAXIAN_XIFENG) != "0") {
			yakuFlag = 0
			if ((KezCount(TILE_WEST_WIND) >= 1)&&(DuiCount(TILE_SUIT_BAMBOOS+1) >= 1)) {yakuFlag = 1}
			if (yakuFlag) {
				addyakuman tmpYakuInfo, "八仙戯鳳", 1
				delyaku tmpYakuInfo, "対々和"
				delyaku tmpYakuInfo, "三色同刻"
			}
		}
		/* 八仙戯四鳳 */
		if (getRule(RULE_BAXIAN_XISIFENG) != "0") {
			yakuFlag = 0
			if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(DuiCount(TILE_WEST_WIND) >= 1)) {yakuFlag += 1}
			countKangz KangzCount, haiMianziDat
			if (KangzCount(TILE_SUIT_BAMBOOS+1) >= 1) {yakuFlag += 1}
			if (yakuFlag == 2) {
				addyakuman tmpYakuInfo, "八仙戯四鳳", 1
				delyaku tmpYakuInfo, "対々和"
				delyaku tmpYakuInfo, "三色同刻"
			}
		}
		/* 八仙戯金亀 */
		if (getRule(RULE_BAXIAN_XIJINGUI) != "0") {
			yakuFlag = 0
			if ((KezCount(TILE_WEST_WIND) >= 1)&&(DuiCount(TILE_SUIT_BAMBOOS+7) >= 1)) {yakuFlag = 1}
			if (yakuFlag) {
				addyakuman tmpYakuInfo, "八仙戯金亀", 1
				delyaku tmpYakuInfo, "対々和"
				delyaku tmpYakuInfo, "三色同刻"
			}
		}
		/* 八仙闘北斗 */
		if (getRule(RULE_BAXIAN_DOU_BEIDOU) != "0") {
			yakuFlag = 0
			if ((KezCount(TILE_SUIT_CIRCLES+7) >= 1)) {yakuFlag = 1}
			if (yakuFlag) {
				addyakuman tmpYakuInfo, "八仙闘北斗", 1
				delyaku tmpYakuInfo, "対々和"
				delyaku tmpYakuInfo, "三色同刻"
				delyaku tmpYakuInfo, "茴香ポン"
			}
		}
		/* 八仙聚首 */
		if (getRule(RULE_BAXIAN_JUSHOU) != "0") {
			yakuFlag = 0
			if ((getFlowerFlag(GameStat, targetPlayer)&FLOWER_ALL) == FLOWER_ALL) {yakuFlag = 1}
			if (yakuFlag) {
				addyakuman tmpYakuInfo, "八仙聚首", 1
				delyaku tmpYakuInfo, "対々和"
				delyaku tmpYakuInfo, "三色同刻"
			}
		}
	}
#endif
	/* 奇数対々和 */
	if (getRule(RULE_ODD_TOITOI) != "0") {
		yakuFlagCount = 0
		repeat TILE_NUMERAL_MAX/2
			if (DuiCount(cnt*2+1) >= 1) {yakuFlagCount++}
		loop
		if (yakuFlagCount == 5) {
			addyaku tmpYakuInfo, "奇数対々和", 4
			delyaku tmpYakuInfo, "対々和"
		}
	}
	/* 偶数対々和 */
	if (getRule(RULE_EVEN_TOITOI) != "0") {
		yakuFlagCount = 0
		repeat TILE_NUMERAL_MAX/2
			if (DuiCount(cnt*2) >= 1) {yakuFlagCount++}
		loop
		if (yakuFlagCount == 5) {
			addyaku tmpYakuInfo, "偶数対々和", 5
			delyaku tmpYakuInfo, "対々和"
			if ((getRule(RULE_KUITAN) == "2")&&(getMenzen(GameStat, targetPlayer) == 0)) {addYakuInfo tmpYakuInfo, YAKUINF_HAN_BASE, 1: addYakuInfo tmpYakuInfo, YAKUINF_HAN_BONUS, -1}
			delyaku tmpYakuInfo, "断幺九"
		}
	}
	/* 中原逐鹿 */
	if (getRule(RULE_CHUUGEN_CHIKUROKU) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_GREEN_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+7) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+6) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "中原逐鹿", 1
			delyaku tmpYakuInfo, "対々和"
			delyaku tmpYakuInfo, "役牌・發"
			delyaku tmpYakuInfo, "役牌・中"
			delyaku tmpYakuInfo, "絶一門"
		}
	}
	/* 金亀孵蛋 */
	if (getRule(RULE_TURTLE_EGG) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "金亀孵蛋", 2
		}
	}
	/* 旭日東昇 */
	if (getRule(RULE_KYOKUJITSU_TOUSHOU) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(TILE_EAST_WIND) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "旭日東昇", 1
		}
	}
	/* 日落西山 */
	if (getRule(RULE_RIIRAO_SHIISHAN) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(TILE_WEST_WIND) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "日落西山", 1
		}
	}
	/* 揺望北斗 */
	if (getRule(RULE_YOUBOU_HOKUTO) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_CIRCLES+2) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+7) >= 1)&&(KezCount(TILE_NORTH_WIND) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "揺望北斗", 2
		}
	}
	/* 北斗七星 */
	if (getRule(RULE_BIG_DIPPER) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+17) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(KezCount(TILE_NORTH_WIND) >= 1)&&(DuiCount(TILE_WHITE_DRAGON) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "北斗七星", 1
		}
	}
	/* 西廂待月 */
	if (getRule(RULE_SEISOU_TAIGETSU) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_WEST_WIND) >= 1)&&(KezCount(TILE_WHITE_DRAGON) >= 1)&&(DuiCount(TILE_SUIT_CIRCLES+1) >= 1)&&(getHand(GameStat, HAND_TILECODE, 13, targetPlayer) == TILE_SUIT_CIRCLES+1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "西廂待月", 2
		}
	}
	/* 南無八幡 */
	if (getRule(RULE_NAMU_HACHIMAN) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SOUTH_WIND) >= 1)&&(KezCount(TILE_WHITE_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+8) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "南無八幡", 1
			delyaku tmpYakuInfo, "役牌・白"
		}
	}
	/* 西湖十景 */
	if (getRule(RULE_SEIKO_JIKKEI) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_WEST_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_WEST_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+2) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+8) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_WEST_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+3) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+7) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_WEST_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+4) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+6) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "西湖十景", 1
		}
	}
	/* 火山爆発 */
	if (getRule(RULE_VOLCANIC_ERRUPTION) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_BAMBOOS+3) >= 1)&&(KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_GREEN_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+3) >= 1)&&(KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_GREEN_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+3) >= 1)&&(KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_GREEN_DRAGON) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+9) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "火山爆発", 1
			delyaku tmpYakuInfo, "対々和"
			delyaku tmpYakuInfo, "役牌・發"
			delyaku tmpYakuInfo, "役牌・中"
		}
	}
	/* 丹鳳朝陽 */
	if (getRule(RULE_TANFON_CHOUYOU) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "丹鳳朝陽", 1
			delyaku tmpYakuInfo, "役牌・中"
		}
	}
	/* 覇王鞭 */
	if (getRule(RULE_HAOUBEN) != "0") {
		countKangz KangzCount, haiMianziDat
		yakuFlag = 0
		repeat TILE_NUMERAL_COLORS
			if ((getRule(RULE_HAOUBEN) == "2")&&(cnt != TILE_SUIT_BAMBOOS)) {continue}
			if ((KangzCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(KangzCount(cnt*TILE_SUIT_STEP+4) == 0)&&(haiMianziDat(0) == cnt*TILE_SUIT_STEP+7)) {yakuFlag = 1}
			if ((KangzCount(cnt*TILE_SUIT_STEP+2) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KangzCount(cnt*TILE_SUIT_STEP+5) == 0)&&(haiMianziDat(0) == cnt*TILE_SUIT_STEP+8)) {yakuFlag = 1}
			if ((KangzCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+6) >= 1)&&(KangzCount(cnt*TILE_SUIT_STEP+6) == 0)&&(haiMianziDat(0) == cnt*TILE_SUIT_STEP+9)) {yakuFlag = 1}
			if ((KangzCount(cnt*TILE_SUIT_STEP+7) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(KangzCount(cnt*TILE_SUIT_STEP+4) == 0)&&(haiMianziDat(0) == cnt*TILE_SUIT_STEP+1)) {yakuFlag = 1}
			if ((KangzCount(cnt*TILE_SUIT_STEP+8) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(KangzCount(cnt*TILE_SUIT_STEP+5) == 0)&&(haiMianziDat(0) == cnt*TILE_SUIT_STEP+2)) {yakuFlag = 1}
			if ((KangzCount(cnt*TILE_SUIT_STEP+9) >= 1)&&(KezCount(cnt*TILE_SUIT_STEP+6) >= 1)&&(KangzCount(cnt*TILE_SUIT_STEP+6) == 0)&&(haiMianziDat(0) == cnt*TILE_SUIT_STEP+3)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "覇王鞭", 6
		}
	}
	/* 劉関張 */
	if (getRule(RULE_RYUUKANCHAN) != "0") {
		countKangz KangzCount, haiMianziDat
		yakuFlag = 0
		repeat 7,1
			tmpcnt = cnt
			repeat TILE_NUMERAL_COLORS
				if ((KangzCount(tmpcnt+cnt*TILE_SUIT_STEP) >= 1)&&(KezCount(tmpcnt+1+cnt*TILE_SUIT_STEP) >= 1)&&(KangzCount(tmpcnt+1+cnt*TILE_SUIT_STEP) == 0)&&(haiMianziDat(0) == tmpcnt+2+cnt*TILE_SUIT_STEP)) {yakuFlag = 1}
				if ((KangzCount(tmpcnt+2+cnt*TILE_SUIT_STEP) >= 1)&&(KezCount(tmpcnt+1+cnt*TILE_SUIT_STEP) >= 1)&&(KangzCount(tmpcnt+1+cnt*TILE_SUIT_STEP) == 0)&&(haiMianziDat(0) == tmpcnt+cnt*TILE_SUIT_STEP)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "劉関張", 6
		}
	}
	/* 鵲橋会 */
	if (getRule(RULE_QUEQIAOHUI) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+7) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(KezCount(27) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+7) >= 1)&&(KezCount(27) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "鵲橋会", 2
		}
	}
	/* 一竿風月 */
	if (getRule(RULE_HUAIISOH) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_BAMBOOS+2) >= 1)&&(KezCount(TILE_EAST_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+2) >= 1)&&(KezCount(TILE_SOUTH_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+2) >= 1)&&(KezCount(TILE_WEST_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_SUIT_BAMBOOS+2) >= 1)&&(KezCount(TILE_NORTH_WIND) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+1) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "一竿風月", 2
		}
	}
	/* 南の朱雀 */
	if (getRule(RULE_VERMILION_BIRD) != "0") {
		countKangz KangzCount, haiMianziDat
		yakuFlag = 0
		if ((KangzCount(TILE_SOUTH_WIND) >= 1)&&(KangzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+5) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(haiMianziDat(0) == TILE_RED_DRAGON)) {yakuFlag = 1}
		if ((KangzCount(TILE_SOUTH_WIND) >= 1)&&(KangzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+5) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+9) >= 1)&&(haiMianziDat(0) == TILE_RED_DRAGON)) {yakuFlag = 1}
		if ((KangzCount(TILE_SOUTH_WIND) >= 1)&&(KangzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+9) >= 1)&&(haiMianziDat(0) == TILE_RED_DRAGON)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "南の朱雀", 1
			delyaku tmpYakuInfo, "対々和"
			delyaku tmpYakuInfo, "混一色"
		}
	}
	/* 西の白虎 */
	if (getRule(RULE_WHITE_TIGER) != "0") {
		countKangz KangzCount, haiMianziDat
		yakuFlag = 0
		if ((KangzCount(TILE_WEST_WIND) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+3) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+3) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+3) >= 1)&&(haiMianziDat(0) == TILE_WHITE_DRAGON)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "西の白虎", 1
			delyaku tmpYakuInfo, "対々和"
			delyaku tmpYakuInfo, "三色同刻"
		}
	}
	/* 三杯美酒敬梅花 */
	if (getRule(RULE_SANBEI_MEIJIU) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&(KezCount(TILE_SUIT_CHARACTERS+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+9) >= 1)) {yakuFlag = 1}
		if ((KezCount(TILE_RED_DRAGON) >= 1)&&(KezCount(TILE_SUIT_CIRCLES+5) >= 1)&&(KezCount(TILE_SUIT_BAMBOOS+9) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "三杯美酒敬梅花", 1
			delyaku tmpYakuInfo, "役牌・中"
		}
	}
	/* 東風圧倒西風 */
	if (getRule(RULE_DONGFENG_YADAO) != "0") {
		countKangz KangzCount, haiMianziDat
		if ((KangzCount(TILE_EAST_WIND) == 0)&&(KezCount(TILE_EAST_WIND) >= 1)&&(haiMianziDat(0) == TILE_WEST_WIND)) {
			addyaku tmpYakuInfo, "東風圧倒西風", 2
		}
		if ((KangzCount(TILE_EAST_WIND) >= 1)&&(KezCount(TILE_WEST_WIND) >= 1)&&(KangzCount(TILE_WEST_WIND) == 0)) {
			addyaku tmpYakuInfo, "東風圧倒西風", 3
		}
	}
	/* 太公釣金亀 */
	if (getRule(RULE_TAIKOU_TURTLE) != "0") {
		yakuFlag = 0
		if ((KezCount(TILE_SUIT_BAMBOOS+2) >= 1)&&(getHand(GameStat, HAND_TILECODE, 13, targetPlayer) == TILE_SUIT_BAMBOOS+3)&&(MachiType == MACHI_TANKI)) {
			addyaku tmpYakuInfo, "太公釣金亀", 1
		}
	}
return
#global
