/*=============================
 * HSP麻雀クライアントMiHaJong
 *    [槓子役判定ルーチン]
 *=============================
 */

#module
#include "const.as"
#deffunc countKangz array KangzCount, array haiMianziDat
/*
		countKangz p1, p2
		槓子の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する槓子を数え、種類別に格納します。
		また、statには見つかった槓子の数が格納されます。
*/
	trace "槓子の種類を調べます。"
	Kangzi = 0
	dim KangzCount, TILE_NONFLOWER_MAX
	repeat SIZE_OF_MELD_BUFFER
		if (haiMianziDat(cnt) >= MELD_QUAD*MELD_TYPE_STEP) {KangzCount(haiMianziDat(cnt) \ TILE_CODE_MAXIMUM)++}
	loop
	return Kangzi
#global

/* 槓子系の役 */
#module
#include "const.as"
#deffunc quadyaku var GameStat, var tmpYakuInfo, array haiMianziDat
	trace "槓子役の処理に入ります。"
	countKangz KangzCount, haiMianziDat
	Kangzi = stat: AnKan = 0
	repeat SIZE_OF_MELD_BUFFER
		if ((haiMianziDat(cnt) / MELD_TYPE_STEP) == MELD_QUAD) {
			AnKan++
		}
	loop
	/* 四槓子 */
	if (Kangzi == 4) {
		if (getRule(RULE_SUUKANTSU) != "0") {
			addyakuman tmpYakuInfo, "四槓子", 2
		} else {
			addyakuman tmpYakuInfo, "四槓子", 1
		}
		// 四槓子が成立したら対々和の成立を解除する(青天井ルール用)
		delyaku tmpYakuInfo, "対々和"
	}
	/* 三暗槓 */
	if (getRule(RULE_SANANKAN) != "0") {
		if (AnKan == 3) {
			addyakuman tmpYakuInfo, "三暗槓", 1
		}
	}
	/* 大峡谷 */
	if (getRule(RULE_GRAND_CANYON) != "0") {
		yakuFlag = 0
		countKez KezCount, haiMianziDat
		if ((KezCount(TILE_SUIT_CHARACTERS+8) >= 1)||(KezCount(TILE_SUIT_CIRCLES+8) >= 1)||(KezCount(TILE_SUIT_BAMBOOS+8) >= 1)) {yakuFlag = 1}
		if ((KangzCount(TILE_SUIT_CHARACTERS+9) >= 1)&&(KangzCount(TILE_SUIT_CIRCLES+9) >= 1)&&(KangzCount(TILE_SUIT_BAMBOOS+9) >= 1)&&(yakuFlag == 1)&&(haiMianziDat(0) \ TILE_SUIT_STEP == 8)) {
			addyakuman tmpYakuInfo, "大峡谷", 1
		}
	}
	/* 雪化粧 */
	if (getRule(RULE_YUKIGESHOU) != "0") {
		if ((KangzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(KangzCount(TILE_SUIT_CIRCLES+9) >= 1)&&(KangzCount(TILE_WHITE_DRAGON) >= 1)) {
			addyakuman tmpYakuInfo, "雪化粧", 1
			delyaku tmpYakuInfo, "役牌・白"
		}
	}
	/* 三槓子…下手な役満よりも難しいのに何で２飜しかないの */
	tmpyakuCensored = 0 // 三暗槓が成立してたら飛ばす
	tmpyakuCensored += searchyaku(tmpYakuInfo, "三暗槓")
	tmpyakuCensored += searchyaku(tmpYakuInfo, "大峡谷")
	tmpyakuCensored += searchyaku(tmpYakuInfo, "雪化粧")
	if ((Kangzi == 3)&&(tmpyakuCensored == 0)) {
		if (getRule(RULE_SANKANTSU) == "2") {
			addyakuman tmpYakuInfo, "三槓子", 1
		} else {
			if (getRule(RULE_SANKANTSU) != "0") {
				addyaku tmpYakuInfo, "三槓子", 3
			} else {
				addyaku tmpYakuInfo, "三槓子", 2
			}
		}
	}
	/* 二暗槓 */
	if (getRule(RULE_RYANANKAN) != "0") {
		if (AnKan == 2) {
			addyaku tmpYakuInfo, "二暗槓", 1
		}
	}
	/* 声東撃西 */
	if (getRule(RULE_SHENGDONG_JIXI) != "0") {
		if ((KangzCount(TILE_EAST_WIND) >= 1)&&(KangzCount(TILE_WEST_WIND) >= 1)) {
			addyaku tmpYakuInfo, "声東撃西", 6
		}
	}
	/* 走南闖北 */
	if (getRule(RULE_ZAONAN_CHUANGBEI) != "0") {
		if ((KangzCount(TILE_SOUTH_WIND) >= 1)&&(KangzCount(TILE_NORTH_WIND) >= 1)) {
			addyaku tmpYakuInfo, "走南闖北", 6
		}
	}
return
#global
