/*=============================
 * HSP麻雀クライアントMiHaJong
 *     [摸打処理ルーチン]
 *=============================
 */

#module
#include "const.as"
#deffunc getdahai var GameStat, var GameEnv
	/* COMが「カンニング」しないように処理 */
	makesandbox Sandbox, GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)
	/* 打牌を取得する */
	if (getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE) == getPlayer(GameStat)) {
		if (getAgariHouki(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1) {
			DiscardTileIndex = DAHAI_TYPE_NORMAL+TSUMOHAI_INDEX
		} else {
			if (GetWatchModeFlag(GameEnv) == 1) {
				compdahai Sandbox: DiscardTileIndex = stat
			} else {
				playerdahai GameStat, GameEnv: DiscardTileIndex = stat
			}
		}
	} else {
		if ((IsRemotePlayer(GameEnv, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == -1)||(getAgariHouki(GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) == 1)) {
			DiscardTileIndex = DAHAI_TYPE_NORMAL+TSUMOHAI_INDEX
		} else {
			/* ネット対戦時の処理 */
			if (IsRemotePlayer(GameEnv, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE)) > 0) {
				remoteDahai GameStat, GameEnv: DiscardTileIndex = stat
				if (getGameMode(GameEnv) == GAMEMODE_CLIENT) {
					if (ReceivedMsg == 1023) {
						setDisconnectFlag GameStat, getCurrentPlayer(GameStat, CURRENTPLAYER_ACTIVE), 1
						setCenterTitle "回線切断": redrscreen
						gosub *reconnection@
						dialog "再接続しました\nこの回は和了り放棄になります\n次の局から参加可能です", 0, "回線切断"
						RoundEndType = ENDKYOKU_DISCONNECT
						return DAHAI_TYPE_DISCONNECT
					}
				}
			} else {
				compdahai Sandbox: DiscardTileIndex = stat
				if (getGameMode(GameEnv) == GAMEMODE_SERVER) {
					if (DiscardTileIndex == DAHAI_TYPE_KYUUSHU) {SendingMsg = 0x6e}
					if (DiscardTileIndex == DAHAI_TYPE_AGARI) {SendingMsg = 0x6f}
					if ((DiscardTileIndex >= DAHAI_TYPE_NORMAL)&&(DiscardTileIndex <= DAHAI_TYPE_NORMAL+TSUMOHAI_INDEX)) {SendingMsg = DiscardTileIndex-DAHAI_TYPE_NORMAL+0x60}
					if ((DiscardTileIndex >= DAHAI_TYPE_KAN)&&(DiscardTileIndex <= DAHAI_TYPE_KAN+TSUMOHAI_INDEX)) {SendingMsg = DiscardTileIndex-DAHAI_TYPE_KAN+0x70}
					if ((DiscardTileIndex >= DAHAI_TYPE_KAKAN)&&(DiscardTileIndex <= DAHAI_TYPE_KAKAN+TSUMOHAI_INDEX)) {SendingMsg = DiscardTileIndex-DAHAI_TYPE_KAKAN+0x80}
					if ((DiscardTileIndex >= DAHAI_TYPE_RIICHI)&&(DiscardTileIndex <= DAHAI_TYPE_RIICHI+TSUMOHAI_INDEX)) {SendingMsg = DiscardTileIndex-DAHAI_TYPE_RIICHI+0x90}
					if ((DiscardTileIndex >= DAHAI_TYPE_OPENRIICHI)&&(DiscardTileIndex <= DAHAI_TYPE_OPENRIICHI+TSUMOHAI_INDEX)) {SendingMsg = DiscardTileIndex-DAHAI_TYPE_OPENRIICHI+0xC0}
					serversend SendingMsg, GameEnv
				}
			}
		}
	}
	return DiscardTileIndex
#global
