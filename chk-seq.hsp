/*=============================
 * HSP麻雀クライアントMiHaJong
 *    [順子役判定ルーチン]
 *=============================
 */

#module
#include "const.as"
#deffunc countShunz array ShunzCount, array MianziDat
/*
		countShunz p1, p2
		順子の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する順子を数え、種類別に格納します。
*/
	trace "順子の種類を調べます。"
	dim ShunzCount, TILE_NONFLOWER_MAX
	repeat SIZE_OF_MELD_BUFFER-1, 1
		if (MianziDat(cnt) < MELD_TRIPLET*MELD_TYPE_STEP) {
			ShunzCount(MianziDat(cnt) \ TILE_CODE_MAXIMUM)++
		}
	loop
	return
#global

#module
#include "const.as"
#deffunc countAnShunz array ShunzCount, array MianziDat
/*
		countAnShunz p1, p2
		暗順子の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する暗順子を数え、種類別に格納します。
		また、statには見つかった暗順子の数が格納されます。
*/
	trace "暗順子の種類を調べます。"
	dim ShunzCount, TILE_NONFLOWER_MAX
	repeat SIZE_OF_MELD_BUFFER-1, 1
		if ((MianziDat(cnt) / MELD_TYPE_STEP) == MELD_SEQUENCE_CONCEALED) {
			ShunzCount(MianziDat(cnt) \ TILE_CODE_MAXIMUM)++
		}
	loop
	return
#global

/* 順子系の役 */
#module
#include "const.as"
#deffunc sequenceyaku var GameStat, var tmpYakuInfo, array MianziDat, int targetPlayer, int MachiType
	trace "順子役の処理に入ります。"
	countShunz ShunzCount, MianziDat
	countAnShunz AnShunzCount, MianziDat
	DragonW = 0: DragonG = 0: DragonR = 0: Wind = 0
	EastWind = 0: SouthWind = 0: WestWind = 0: NorthWind = 0
#ifndef SANMAS
	repeat SIZE_OF_MELD_BUFFER-1, 1
		switch (MianziDat(cnt) \ TILE_CODE_MAXIMUM)
			case TILE_EAST_WIND: EastWind++: Wind++: swbreak
			case TILE_SOUTH_WIND: SouthWind++: Wind++: swbreak
			case TILE_WEST_WIND: WestWind++: Wind++: swbreak
			case TILE_NORTH_WIND: NorthWind++: Wind++: swbreak
			case TILE_WHITE_DRAGON: DragonW++: swbreak
			case TILE_GREEN_DRAGON: DragonG++: swbreak
			case TILE_RED_DRAGON: DragonR++: swbreak
		swend
	loop
#endif
	/* 一色双龍会 */
	if (getRule(RULE_ISSHOKU_SOURYUU) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 2)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 2)&&(MianziDat(0) == TILE_SUIT_CHARACTERS+5)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_CIRCLES+1) >= 2)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 2)&&(MianziDat(0) == TILE_SUIT_CIRCLES+5)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_BAMBOOS+1) >= 2)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 2)&&(MianziDat(0) == TILE_SUIT_BAMBOOS+5)) {yakuFlag = 1}
		if (yakuFlag) {
			if ((getRule(RULE_ISSHOKU_SOURYUU) != 2)||(getMenzen(GameStat, targetPlayer) == 1)) {
				addyakuman tmpYakuInfo, "一色双龍会", 1
				delyaku tmpYakuInfo, "清一色"
			}
		}
	}
	/* 一色四順 */
	if (getRule(RULE_ISSHOKU_SUUJUN) != 0) {
		yakuFlag = 0
		repeat TILE_NUMERAL_MAX
			if (ShunzCount(cnt) == 4) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "一色四順", 1
		}
	}
	/* 一色三順 */
	if (getRule(RULE_ISSHOKU_SANJUN) != 0) {
		yakuFlag = 0
		repeat TILE_NUMERAL_MAX
			if (ShunzCount(cnt) == 3) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			switch getRule(RULE_ISSHOKU_SANJUN)
				case 1:
					if (getMenzen(GameStat, targetPlayer) == 1) {
						addyaku tmpYakuInfo, "一色三順", 2
					}
					swbreak
				case 2:
					addyaku tmpYakuInfo, "一色三順", 1+getMenzen(GameStat, targetPlayer)
					swbreak
				case 3:
					if (getMenzen(GameStat, targetPlayer) == 1) {
						addyaku tmpYakuInfo, "一色三順", 3
					}
					swbreak
				case 4:
					addyaku tmpYakuInfo, "一色三順", 2+getMenzen(GameStat, targetPlayer)
					swbreak
			swend
		}
	}
	/* 一盃口 */
	yakuFlag = 0
	repeat TILE_NUMERAL_MAX
		if (ShunzCount(cnt) == 2) {yakuFlag++}
	loop
	if ((yakuFlag == 1)&&(getMenzen(GameStat, targetPlayer) == 1)) {
		addyaku tmpYakuInfo, "一盃口", 1
	}
	/* 二盃口 */
	if ((yakuFlag == 2)&&(getMenzen(GameStat, targetPlayer) == 1)) {
		tmpyakuCensored = 0 // 大車輪が成立してたら飛ばす
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大車輪")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大竹林")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大数隣")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "一色双龍会")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "清海湖")
		if (tmpyakuCensored == 0) {
			if (getRule(RULE_RYANPEIKOU) != 0) {
				addyaku tmpYakuInfo, "二盃口", 2
			} else {
				addyaku tmpYakuInfo, "二盃口", 3
			}
			/* 大鏡 */
			if (getRule(RULE_OHKAGAMI) != 0) {
				if (MianziDat(0) == TILE_SUIT_CIRCLES+1) {
					addyaku tmpYakuInfo, "大鏡", 1
				}
			}
		}
	}
	/* 清盃口 */
	if (getRule(RULE_CHINPEIKOU) != 0) {
		yakuFlag2 = 0
		repeat 7,1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) == 2)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES) == 2)) {yakuFlag2++}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) == 2)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) == 2)) {yakuFlag2++}
			if ((ShunzCount(cnt+TILE_SUIT_CIRCLES) == 2)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) == 2)) {yakuFlag2++}
		loop
		if ((yakuFlag == 2)&&(yakuFlag2 == 1)&&(getMenzen(GameStat, targetPlayer) == 1)) {
			switch getRule(RULE_CHINPEIKOU)
				case 1: addyaku tmpYakuInfo, "清盃口", 6: swbreak
				case 2: addyaku tmpYakuInfo, "清盃口", 5: swbreak
				case 3: addyaku tmpYakuInfo, "清盃口", 4: swbreak
			swend
			delyaku tmpYakuInfo, "二盃口"
		}
	}
	/* 鳴き二盃口 */
	if (getRule(RULE_EXPOSED_RYANPEIKOU) != 0) {
		if ((yakuFlag == 2)&&(getMenzen(GameStat, targetPlayer) == 0)) {
			addyaku tmpYakuInfo, "鳴き二盃口", 2
		}
	}
	/* 三色同順 */
	yakuFlag = 0
	repeat 7,1
		if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
	loop
	if (yakuFlag) {
		addyaku tmpYakuInfo, "三色同順", 1+getMenzen(GameStat, targetPlayer)
	}
	/* 一気通貫 */
	yakuFlag = 0: straightColor = -1
	repeat TILE_NUMERAL_COLORS
		if ((ShunzCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlag = 1: straightColor = cnt}
	loop
	if (yakuFlag) {
		addyaku tmpYakuInfo, "一気通貫", 1+getMenzen(GameStat, targetPlayer)
		/* 一気通貫派生役 */
#ifndef SANMAS
		/* 東北新幹線 */
		if ((getRule(RULE_TOHOKU_SHINKANSEN) != 0)&&(EastWind > 0)&&(NorthWind > 0)) {
			if ((getRule(RULE_TOHOKU_SHINKANSEN) == 1)||(getMenzen(GameStat, targetPlayer) == 1)) {
				addyakuman tmpYakuInfo, "東北新幹線", 1
				delyaku tmpYakuInfo, "一気通貫"
				delyaku tmpYakuInfo, "混一色"
			}
		}
#endif
		/* 一気通貫全帯幺九 */
		if (getRule(RULE_ITTSUU_CHANTA) != 0) {
			gosub *checkIttsuuChanta
			tmpyakuCensored = 0
			tmpyakuCensored += searchyaku(tmpYakuInfo, "混一色")
			tmpyakuCensored += searchyaku(tmpYakuInfo, "清一色")
			if ((tmpyaojiu == 4)&&(tmpyakuCensored == 0)) {
				if (JihaiCount == 0) {
					addyaku tmpYakuInfo, "純一気通貫全帯幺九", 3+getMenzen(GameStat, targetPlayer)
				} else {
					addyaku tmpYakuInfo, "一気通貫全帯幺九", 2+getMenzen(GameStat, targetPlayer)
				}
				delyaku tmpYakuInfo, "一気通貫"
			}
		}
		/* 五心通貫 */
		if (getRule(RULE_UUSHINTONKAN) != 0) {
			if ((MachiType == MACHI_KANCHAN)&&(TsumoTile(GameStat, targetPlayer) == (straightColor*TILE_SUIT_STEP+5))) {
				addyaku tmpYakuInfo, "五心通貫", 1
			}
		}
#ifndef SANMAS
		/* 竜戯珠 */
		if (getRule(RULE_LONGXIZHU) != 0) {
			if ((WestWind > 0)&&(MianziDat(0) == TILE_SUIT_CIRCLES+1)) {
				addyaku tmpYakuInfo, "龍戯珠", 2
			}
		}
		/* 金龍戯北斗 */
		if (getRule(RULE_JINLONG_DOU_BEIDOU) != 0) {
			countKez KezCount, MianziDat
			if ((MianziDat(0) == TILE_NORTH_WIND)&&(KezCount(TILE_SUIT_CIRCLES+7) >= 1)) {
				addyaku tmpYakuInfo, "金龍戯北斗", 2
			}
		}
		/* 西湖十景系列の役 */
		tmpRuleCodeList = RULE_SHUANGFENG_ZHAYUN, RULE_NANBING_WANZHONG, RULE_LEIFENG_XIZHAO, RULE_LIULANG_WENYING, RULE_DUANQIAO_CANXUE, RULE_SUDI_CHUNXIAO, RULE_HUAGANG_GUANYU, RULE_SANTAN_YINYUE, RULE_QUYUAN_FENGHE, RULE_PINGHU_QIUYUE
		tmpTileCodeList1 = TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND, TILE_WHITE_DRAGON, TILE_GREEN_DRAGON, TILE_RED_DRAGON, TILE_WHITE_DRAGON, TILE_GREEN_DRAGON, TILE_RED_DRAGON
		tmpTileCodeList2 = TILE_SUIT_BAMBOOS+8, TILE_RED_DRAGON, TILE_SUIT_CIRCLES+8, TILE_SUIT_CHARACTERS+8, TILE_SUIT_BAMBOOS+2, TILE_SUIT_CHARACTERS+2, TILE_SUIT_CIRCLES+2, TILE_SUIT_CHARACTERS+1, TILE_SUIT_CIRCLES+1, TILE_SUIT_BAMBOOS+1
		tmpStraightColorList = TILE_SUIT_CHARACTERS, TILE_SUIT_CIRCLES, TILE_SUIT_BAMBOOS, TILE_SUIT_CIRCLES, TILE_SUIT_CHARACTERS, TILE_SUIT_CIRCLES, TILE_SUIT_BAMBOOS, TILE_SUIT_CHARACTERS, TILE_SUIT_CIRCLES, TILE_SUIT_BAMBOOS
		sdim tmpYakuNameList, 16, length(tmpRuleCodeList)
		tmpYakuNameList = "双峰挿雲", "南屏晩鍾", "雷峰夕照", "柳浪聞鶯", "断橋残雪", "蘇堤春暁", "花港観魚", "三譚印月", "曲院風荷", "平湖秋月"
		countDuiz DuiCount, MianziDat
		repeat length(tmpRuleCodeList)
			if (getRule(tmpRuleCodeList(cnt)) != 0) {
				if ((DuiCount(tmpTileCodeList1(cnt)) >= 1)&&(DuiCount(tmpTileCodeList2(cnt)) >= 1)&&(straightColor == tmpStraightColorList(cnt)/TILE_SUIT_STEP)) {
					addyaku tmpYakuInfo, tmpYakuNameList(cnt), 1+getMenzen(GameStat, targetPlayer)
				}
			}
		loop
		/* 東の青龍 */
		if (getRule(RULE_AZURE_DRAGON) != 0) {
			countKez KezCount, MianziDat
			if ((MianziDat(0) == TILE_WHITE_DRAGON)&&(KezCount(TILE_EAST_WIND) >= 1)&&(straightColor == TILE_SUIT_CIRCLES/TILE_SUIT_STEP)&&(getMenzen(GameStat, targetPlayer) == 1)) {
				addyakuman tmpYakuInfo, "東の青龍", 1
				delyaku tmpYakuInfo, "一気通貫"
				delyaku tmpYakuInfo, "混一色"
			}
		}
		/* 北の玄武 */
		if (getRule(RULE_BLACK_TORTOISE) != 0) {
			if ((MianziDat(0) == TILE_SUIT_BAMBOOS+7)&&(KezCount(TILE_NORTH_WIND) >= 1)&&(straightColor == TILE_SUIT_CIRCLES/TILE_SUIT_STEP)&&(getMenzen(GameStat, targetPlayer) == 1)) {
				addyakuman tmpYakuInfo, "北の玄武", 1
				delyaku tmpYakuInfo, "一気通貫"
			}
		}
		/* 秋田新幹線 */
		if (getRule(RULE_AKITA_SHINKANSEN) != 0) {
			if (KezCount(TILE_NORTH_WIND) >= 1) {
				if (searchyaku(tmpYakuInfo, "混一色")) {
					if (TsumoTile(GameStat, targetPlayer) == straightColor*TILE_SUIT_STEP+5) {
						addyakuman tmpYakuInfo, "秋田新幹線", 1
						delyaku tmpYakuInfo, "一気通貫"
						delyaku tmpYakuInfo, "混一色"
					}
				}
			}
		}
		/* 上越新幹線とき */
		if (getRule(RULE_JOETSU_TOKI) != 0) {
			if ((KezCount(TILE_NORTH_WIND) >= 1)||(MianziDat(0) == TILE_NORTH_WIND)) {
				if (searchyaku(tmpYakuInfo, "混一色")) {
					addyaku tmpYakuInfo, "上越新幹線とき", 2
				}
			}
		}
		/* 上越新幹線あさひ */
		if (getRule(RULE_JOETSU_ASAHI) != 0) {
			if (((MianziDat(0) == TILE_WHITE_DRAGON)&&(KezCount(TILE_NORTH_WIND) >= 1))||((MianziDat(0) == TILE_NORTH_WIND)&&(KezCount(TILE_WHITE_DRAGON) >= 1))) {
				addyakuman tmpYakuInfo, "上越新幹線あさひ", 1
				delyaku tmpYakuInfo, "上越新幹線とき"
				delyaku tmpYakuInfo, "一気通貫"
				delyaku tmpYakuInfo, "混一色"
			}
		}
		/* 青函連絡船 */
		if (getRule(RULE_SEIKAN_SHIP) != 0) {
			countAnKangz AnKangzCount, MianziDat
			if ((AnKangzCount(TILE_GREEN_DRAGON) >= 1)&&(MianziDat(0) == TILE_NORTH_WIND)) {
				addyakuman tmpYakuInfo, "青函連絡船", 1
				delyaku tmpYakuInfo, "一気通貫"
				delyaku tmpYakuInfo, "混一色"
			}
		}
		/* ゴールドラッシュ */
		if (getRule(RULE_GOLDRUSH) != 0) {
			countDuiz DuiCount, MianziDat
			if ((DuiCount(TILE_GREEN_DRAGON) >= 1)&&(straightColor == TILE_SUIT_CIRCLES/TILE_SUIT_STEP)&&(searchyaku(tmpYakuInfo, "混一色") != 0)) {
				if ((getRule(RULE_GOLDRUSH) == 1)||(getRule(RULE_GOLDRUSH) == 3)||(DuiCount(TILE_WEST_WIND) >= 1)) {
					switch (getRule(RULE_GOLDRUSH))
						case 1: case 2:
							addyaku tmpYakuInfo, "ゴールドラッシュ", 2
							swbreak
						case 3: case 4:
							addyakuman tmpYakuInfo, "ゴールドラッシュ", 1
							delyaku tmpYakuInfo, "一気通貫"
							delyaku tmpYakuInfo, "混一色"
							swbreak
					swend
				}
			}
		}
#endif
		/* ルート２４６ */
		if (getRule(RULE_ROUTE_246) != 0) {
			if ((MachiType == MACHI_KANCHAN)&&((TsumoTile(GameStat, targetPlayer) == (straightColor*TILE_SUIT_STEP+2))||(TsumoTile(GameStat, targetPlayer) == (straightColor*TILE_SUIT_STEP+4))||(TsumoTile(GameStat, targetPlayer) == (straightColor*TILE_SUIT_STEP+6)))) {
				addyaku tmpYakuInfo, "ルート２４６", 1
			}
		}
#ifndef ALLSANMA
		/* 九龍城落地 */
		if (getRule(RULE_GAULUNGSING_LOKDEI) != 0) {
			if (((ShunzCount(straightColor*TILE_SUIT_STEP+1)-AnShunzCount(straightColor*TILE_SUIT_STEP+1)) >= 1)&&((ShunzCount(straightColor*TILE_SUIT_STEP+4)-AnShunzCount(straightColor*TILE_SUIT_STEP+4)) >= 1)&&((ShunzCount(straightColor*TILE_SUIT_STEP+7)-AnShunzCount(straightColor*TILE_SUIT_STEP+7)) >= 1)) {
				addyaku tmpYakuInfo, "九龍城落地", 2
			}
		}
#endif
#ifndef SANMAS
		/* 北海道新幹線 */
		if (getRule(RULE_HOKKAIDO_SHINKANSEN) != 0) {
			countAnKangz AnKangzCount, MianziDat
			if ((AnKangzCount(TILE_WHITE_DRAGON) >= 1)&&(MianziDat(0) == TILE_NORTH_WIND)) {
				addyakuman tmpYakuInfo, "北海道新幹線", 1
				delyaku tmpYakuInfo, "一気通貫"
				delyaku tmpYakuInfo, "混一色"
			}
		}
#endif
	}
	/* 金門橋 */
	if (getRule(RULE_GOLDEN_GATE_BRIDGE) != 0) {
		yakuFlag = 0
		repeat TILE_NUMERAL_COLORS
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "金門橋", 1
		}
	}
	/* 凱旋門 */
	if (getRule(RULE_GAISENMON) != 0) {
		repeat TILE_NUMERAL_COLORS
			yakuFlagCount = 0
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) >= 2)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlagCount += 2}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) == 0)) {yakuFlagCount += 1}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) == 0)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlagCount += 1}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+2) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+5) >= 1)) {yakuFlagCount += 1}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+6) >= 1)) {yakuFlagCount += 1}
			if ((yakuFlagCount == 2)&&((MianziDat(0) / TILE_SUIT_STEP) == cnt)) {
				switch getRule(RULE_GAISENMON)
					case 1:
						addyakuman tmpYakuInfo, "凱旋門", 1
					swbreak
					case 2:
						addyakuman tmpYakuInfo, "凱旋門", 2
					swbreak
				swend
				delyaku tmpYakuInfo, "清一色"
			}
		loop
	}
	/* 長城 */
	if (getRule(RULE_CHOUJOU) != 0) {
		repeat 3
			yakuFlagCount = 0
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) >= 2)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlagCount += 2}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) >= 2)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) >= 1)) {yakuFlagCount += 2}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) >= 2)) {yakuFlagCount += 2}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+1) == 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+4) == 1)&&(ShunzCount(cnt*TILE_SUIT_STEP+7) == 1)) {yakuFlagCount += 1}
			if ((ShunzCount(cnt*TILE_SUIT_STEP+2) >= 1)||(ShunzCount(cnt*TILE_SUIT_STEP+3) >= 1)||(ShunzCount(cnt*TILE_SUIT_STEP+5) >= 1)||(ShunzCount(cnt*TILE_SUIT_STEP+6) >= 1)) {yakuFlagCount += 1}
			if ((yakuFlagCount == 2)&&((MianziDat(0) == cnt*TILE_SUIT_STEP+2)||(MianziDat(0) == cnt*TILE_SUIT_STEP+5)||(MianziDat(0) == cnt*TILE_SUIT_STEP+8))) {
				addyakuman tmpYakuInfo, "長城", 1
				delyaku tmpYakuInfo, "一気通貫"
				delyaku tmpYakuInfo, "清一色"
			}
		loop
	}
	/* 青龍在天 */
	if (getRule(RULE_QINGLONG_ZAITIAN) != 0) {
		if (getMenzen(GameStat, targetPlayer) == 1) {
			yakuFlagCount = 0 /* 他力で判定 */
			yakuFlagCount += searchyaku(tmpYakuInfo, "清一色")
			yakuFlagCount += searchyaku(tmpYakuInfo, "一気通貫")
			yakuFlagCount += searchyaku(tmpYakuInfo, "一盃口")
			if (yakuFlagCount == 3) {
				addyakuman tmpYakuInfo, "青龍在天", 1
				delyaku tmpYakuInfo, "一気通貫"
				delyaku tmpYakuInfo, "清一色"
				delyaku tmpYakuInfo, "一盃口"
			}
		}
	}
	/* 鏡同和 */
	if (getRule(RULE_KYANTONHOH) != 0) {
		yakuFlag = 0
		yakuTmpFlag = 0
		repeat 7, 1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)) {yakuTmpFlag++}
		loop: if (yakuTmpFlag == 2) {yakuFlag = 1}
		yakuTmpFlag = 0
		repeat 7, 1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuTmpFlag++}
		loop: if (yakuTmpFlag == 2) {yakuFlag = 1}
		yakuTmpFlag = 0
		repeat 7, 1
			if ((ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuTmpFlag++}
		loop: if (yakuTmpFlag == 2) {yakuFlag = 1}
		if ((getRule(RULE_KYANTONHOH) == 2)||(getMenzen(GameStat, targetPlayer) == 1)) {
			if (yakuFlag) {
				addyaku tmpYakuInfo, "鏡同和", 1+getMenzen(GameStat, targetPlayer)
			}
		}
	}
	/* 四歩高 */
	if (getRule(RULE_OKASUUJUN) != 0) {
		yakuFlag = 0
		repeat 4,1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+1+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+3+TILE_SUIT_CHARACTERS) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+1+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+3+TILE_SUIT_CIRCLES) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+1+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+3+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			switch getRule(RULE_OKASUUJUN)
				case 1:
					addyakuman tmpYakuInfo, "四歩高", 1
				swbreak
				case 2:
					addyakuman tmpYakuInfo, "四歩高", 2
				swbreak
			swend
		}
	}
	/* 三歩高 */
	if (getRule(RULE_OKASANJUN) != 0) {
		yakuFlag = 0
		repeat 5,1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+1+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_CHARACTERS) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+1+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_CIRCLES) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+1+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "四歩高")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			switch getRule(RULE_OKASANJUN)
				case 1:
					addyaku tmpYakuInfo, "三歩高", 1
				swbreak
				case 2:
					if (getMenzen(GameStat, targetPlayer) == 1) {
						addyaku tmpYakuInfo, "三歩高", 1
					}
				swbreak
				case 3:
					addyaku tmpYakuInfo, "三歩高", 1+getMenzen(GameStat, targetPlayer)
				swbreak
			swend
		}
	}
	/* 山三順 */
	if (getRule(RULE_YAMASANJUN) != 0) {
		yakuFlag = 0
		repeat 3,1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+4+TILE_SUIT_CHARACTERS) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+4+TILE_SUIT_CIRCLES) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+2+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+4+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "金門橋")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			switch getRule(RULE_YAMASANJUN)
				case 1:
					addyaku tmpYakuInfo, "清連環套", 1
				swbreak
				case 2:
					if (getMenzen(GameStat, targetPlayer) == 1) {
						addyaku tmpYakuInfo, "清連環套", 1
					}
				swbreak
				case 3:
					addyaku tmpYakuInfo, "清連環套", 1+getMenzen(GameStat, targetPlayer)
				swbreak
			swend
		}
	}
	/* 二色四歩高 */
	if (getRule(RULE_NISHOKU_OKASUUJUN) != 0) {
		yakuFlag = 0
		sdim parsedat, 8, 42
		parsedat = "0001","0002","0010","0011","0020","0022","0100","0101","0110","0111","0200","0202","0220","0222","1000","1001","1010","1011","1100","1101","1110","1112","1121","1122","1211","1212","1221","1222","2000","2002","2020","2022","2111","2112","2121","2122","2200","2202","2211","2212","2220","2221"
		repeat 42
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 4,1
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+tmpsuit2+1) >= 1)&&(ShunzCount(cnt+tmpsuit3+2) >= 1)&&(ShunzCount(cnt+tmpsuit4+3) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "二色四歩高", 2+getMenzen(GameStat, targetPlayer)
		}
	}
	/* 二色三歩高 */
	if (getRule(RULE_NISHOKU_OKASANJUN) != 0) {
		yakuFlag = 0
		repeat 6
			switch cnt
				case 0: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
				case 1: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 2: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 3: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 4: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 5: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
			swend
			repeat 5,1
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+1+tmpsuit1) >= 1)&&(ShunzCount(cnt+2+tmpsuit2) >= 1)) {yakuFlag = 1}
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+1+tmpsuit2) >= 1)&&(ShunzCount(cnt+2+tmpsuit1) >= 1)) {yakuFlag = 1}
				if ((ShunzCount(cnt+tmpsuit2) >= 1)&&(ShunzCount(cnt+1+tmpsuit1) >= 1)&&(ShunzCount(cnt+2+tmpsuit1) >= 1)) {yakuFlag = 1}
			loop
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "二色四歩高")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			if (getMenzen(GameStat, targetPlayer) == 1) {
				addyaku tmpYakuInfo, "二色三歩高", 1
			}
		}
	}
	/* 二色山四順 */
	if (getRule(RULE_NISHOKU_YAMASUUJUN) != 0) {
		yakuFlag = 0
		sdim parsedat, 8, 42
		parsedat = "0001","0002","0010","0011","0020","0022","0100","0101","0110","0111","0200","0202","0220","0222","1000","1001","1010","1011","1100","1101","1110","1112","1121","1122","1211","1212","1221","1222","2000","2002","2020","2022","2111","2112","2121","2122","2200","2202","2211","2212","2220","2221"
		repeat 42
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 1,1
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+tmpsuit2+2) >= 1)&&(ShunzCount(cnt+tmpsuit3+4) >= 1)&&(ShunzCount(cnt+tmpsuit4+6) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			if (getRule(RULE_NISHOKU_YAMASUUJUN) == 2) {
				addyakuman tmpYakuInfo, "大雑連套", 1
			} else {
				addyaku tmpYakuInfo, "大雑連套", 5+getMenzen(GameStat, targetPlayer)
			}
		}
	}
	/* 二色山三順 */
	if (getRule(RULE_NISHOKU_YAMASANJUN) != 0) {
		yakuFlag = 0
		repeat 6
			switch cnt
				case 0: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
				case 1: tmpsuit1 = TILE_SUIT_CHARACTERS: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 2: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 3: tmpsuit1 = TILE_SUIT_CIRCLES: tmpsuit2 = TILE_SUIT_BAMBOOS: swbreak
				case 4: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CHARACTERS: swbreak
				case 5: tmpsuit1 = TILE_SUIT_BAMBOOS: tmpsuit2 = TILE_SUIT_CIRCLES: swbreak
			swend
			repeat 3,1
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+2+tmpsuit1) >= 1)&&(ShunzCount(cnt+4+tmpsuit2) >= 1)) {yakuFlag = 1}
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+2+tmpsuit2) >= 1)&&(ShunzCount(cnt+4+tmpsuit1) >= 1)) {yakuFlag = 1}
				if ((ShunzCount(cnt+tmpsuit2) >= 1)&&(ShunzCount(cnt+2+tmpsuit1) >= 1)&&(ShunzCount(cnt+4+tmpsuit1) >= 1)) {yakuFlag = 1}
			loop
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大雑連套")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "雑連環套", 1+getMenzen(GameStat, targetPlayer)
		}
	}
#ifndef SANMAX
	/* 三色四歩高 */
	if (getRule(RULE_SANSHOKU_OKASUUJUN) != 0) {
		yakuFlag = 0
		sdim parsedat, 8, 36
		parsedat = "0012","0021","0102","0112","0120","0121","0122","0201","0210","0211","0212","0221","1002","1012","1020","1021","1022","1102","1120","1200","1201","1202","1210","1220","2001","2010","2011","2012","2021","2100","2101","2102","2110","2120","2201","2210"
		repeat 36
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 6,1
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+tmpsuit2+1) >= 1)&&(ShunzCount(cnt+tmpsuit3+2) >= 1)&&(ShunzCount(cnt+tmpsuit4+3) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			addyaku tmpYakuInfo, "三色四歩高", 2+getMenzen(GameStat, targetPlayer)
		}
	}
	/* 三色三歩高 */
	if (getRule(RULE_SANSHOKU_OKASANJUN) != 0) {
		yakuFlag = 0
		repeat 5,1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+1) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+1) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "三色四歩高")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			switch getRule(RULE_SANSHOKU_OKASANJUN)
				case 1:
					addyaku tmpYakuInfo, "三色三歩高", 1
				swbreak
				case 2:
					if (getMenzen(GameStat, targetPlayer) == 1) {
						addyaku tmpYakuInfo, "三色三歩高", 1
					}
				swbreak
				case 3:
					addyaku tmpYakuInfo, "三色三歩高", 1+getMenzen(GameStat, targetPlayer)
				swbreak
			swend
		}
	}
	/* 三色山四順 */
	if (getRule(RULE_SANSHOKU_YAMASUUJUN) != 0) {
		yakuFlag = 0
		sdim parsedat, 8, 36
		parsedat = "0012","0021","0102","0112","0120","0121","0122","0201","0210","0211","0212","0221","1002","1012","1020","1021","1022","1102","1120","1200","1201","1202","1210","1220","2001","2010","2011","2012","2021","2100","2101","2102","2110","2120","2201","2210"
		repeat 36
			tmpsuit1 = int(strmid(parsedat(cnt),0,1))*TILE_SUIT_STEP
			tmpsuit2 = int(strmid(parsedat(cnt),1,1))*TILE_SUIT_STEP
			tmpsuit3 = int(strmid(parsedat(cnt),2,1))*TILE_SUIT_STEP
			tmpsuit4 = int(strmid(parsedat(cnt),3,1))*TILE_SUIT_STEP
			repeat 1,1
				if ((ShunzCount(cnt+tmpsuit1) >= 1)&&(ShunzCount(cnt+tmpsuit2+2) >= 1)&&(ShunzCount(cnt+tmpsuit3+4) >= 1)&&(ShunzCount(cnt+tmpsuit4+6) >= 1)) {yakuFlag = 1}
			loop
		loop
		if (yakuFlag) {
			if (getRule(RULE_SANSHOKU_YAMASUUJUN) == 2) {
				addyakuman tmpYakuInfo, "大混連套", 1
			} else {
				addyaku tmpYakuInfo, "大混連套", 5+getMenzen(GameStat, targetPlayer)
			}
		}
	}
	/* 三色山三順 */
	if (getRule(RULE_SANSHOKU_YAMASANJUN) != 0) {
		yakuFlag = 0
		repeat 3,1
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+4) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+4) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+4) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+4) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+4) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+2) >= 1)) {yakuFlag = 1}
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+4) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+2) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大混連套")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			switch getRule(RULE_SANSHOKU_YAMASANJUN)
				case 1:
					addyaku tmpYakuInfo, "混連環套", 1
				swbreak
				case 2:
					if (getMenzen(GameStat, targetPlayer) == 1) {
						addyaku tmpYakuInfo, "混連環套", 1
					}
				swbreak
				case 3:
					addyaku tmpYakuInfo, "混連環套", 1+getMenzen(GameStat, targetPlayer)
				swbreak
			swend
		}
	}
	/* 双龍争珠：萬子の順子を使うので三麻では不可能 */
	if (getRule(RULE_SHANRON_CHONCHUU) != 0) {
		yakuFlag = 0
		repeat 4
			if ((ShunzCount(cnt+TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CHARACTERS+4) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+4) >= 1)) {
				if ((MianziDat(0) / TILE_SUIT_STEP) == (TILE_SUIT_CIRCLES/TILE_SUIT_STEP)) {
					yakuFlag = 1
				}
			}
		loop
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "双龍争珠", 1
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
	/* 花龍：萬子の順子を使うので三麻では不可能 */
	if (getRule(RULE_HUALONG) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+4) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+4) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+4) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+4) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+4) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+4) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)) {yakuFlag = 1}
		if (yakuFlag) {
			if ((getMenzen(GameStat, targetPlayer) == 1)&&(MachiType == MACHI_TANKI)&&(TsumoTile(GameStat, targetPlayer) != TILE_WHITE_DRAGON)&&(TsumoTile(GameStat, targetPlayer) != TILE_GREEN_DRAGON)&&(getRule(RULE_TANRON) != 0)) {
				/* 単龍(門前で単騎待ち) */
				addyaku tmpYakuInfo, "単龍", 6
			} else {
				/* 普通の花龍 */
				addyaku tmpYakuInfo, "三色通貫", 1+getMenzen(GameStat, targetPlayer)
			}
			/* 三色通貫全帯幺九 */
			if (getRule(RULE_HUALONG_CHANTA) != 0) {
				gosub *checkIttsuuChanta
				if (tmpyaojiu == 4) {
					if (JihaiCount == 0) {
						addyaku tmpYakuInfo, "純三色通貫全帯幺九", 3+getMenzen(GameStat, targetPlayer)
					} else {
						addyaku tmpYakuInfo, "三色通貫全帯幺九", 2+getMenzen(GameStat, targetPlayer)
					}
					delyaku tmpYakuInfo, "三色通貫"
				}
			}
		}
	}
	/* 双龍双亀：萬子の順子を使うので三麻では不可能 */
	if (getRule(RULE_SHANRON_SHANKUI) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)) {
			if (MianziDat(0) == TILE_SUIT_BAMBOOS+3) {
				yakuFlag = 1
			}
		}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "双龍双亀", 4
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
	/* 双龍争珠(老少副タイプ)：萬子の順子を使うので三麻では不可能 */
	if (getRule(RULE_SHANRON_CHONCHU_LSF) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)) {
			if (MianziDat(0) == TILE_SUIT_CIRCLES+5) {
				yakuFlag = 1
			}
		}
		if (yakuFlag) {
			addyakuman tmpYakuInfo, "双龍争珠 ", 1
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
	/* 三色双龍会 */
	if (getRule(RULE_SANSHOKU_SOURYUU) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(MianziDat(0) == TILE_SUIT_CIRCLES+5)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)&&(MianziDat(0) == TILE_SUIT_BAMBOOS+5)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(MianziDat(0) == TILE_SUIT_BAMBOOS+5)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(MianziDat(0) == TILE_SUIT_CHARACTERS+5)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(MianziDat(0) == TILE_SUIT_CIRCLES+5)) {yakuFlag = 1}
		if ((ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)&&(MianziDat(0) == TILE_SUIT_CHARACTERS+5)) {yakuFlag = 1}
		tmpyakuCensored = searchyaku(tmpYakuInfo, "双龍争珠 ")
		if ((yakuFlag)&&(tmpyakuCensored == 0)) {
			switch getRule(RULE_SANSHOKU_SOURYUU)
				case 1:
					addyaku tmpYakuInfo, "三色双龍会", 1+getMenzen(GameStat, targetPlayer)
					swbreak
				case 2:
					addyaku tmpYakuInfo, "三色双龍会", 5+getMenzen(GameStat, targetPlayer)
					swbreak
				case 3:
					addyaku tmpYakuInfo, "三色双龍会", 6+getMenzen(GameStat, targetPlayer)
					swbreak
				case 4:
					addyakuman tmpYakuInfo, "三色双龍会", 1
					swbreak
			swend
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
	/* 双龍戯鳳：萬子の順子を使うので三麻では不可能 */
	if (getRule(RULE_SHANRON_SHIIFON) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)) {
			if (MianziDat(0) == TILE_SUIT_BAMBOOS+1) {
				yakuFlag = 1
			}
		}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "双龍戯鳳", 4
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
	/* 双龍包柱：萬子の順子を使うので三麻では不可能 */
	if (getRule(RULE_SHANRON_PAOCHUU) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)) {
			if ((MianziDat(0) == TILE_SUIT_BAMBOOS+2)||(MianziDat(0) == TILE_SUIT_BAMBOOS+4)||(MianziDat(0) == TILE_SUIT_BAMBOOS+6)) {
				yakuFlag = 1
			}
		}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "双龍包柱", 4
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
	/* 双龍戯亀：萬子の順子を使うので三麻では不可能 */
	if (getRule(RULE_SHANRON_SHIIKUI) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CHARACTERS+1) >= 1)&&(ShunzCount(TILE_SUIT_CHARACTERS+7) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)) {
			if (MianziDat(0) == TILE_SUIT_BAMBOOS+7) {
				yakuFlag = 1
			}
		}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "双龍戯亀", 4
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
#endif
	/* 双龍闘蛇：これは三麻でも可能 */
	if (getRule(RULE_SHANRON_TOUJA) != 0) {
		yakuFlag = 0
		if ((ShunzCount(TILE_SUIT_CIRCLES+1) >= 1)&&(ShunzCount(TILE_SUIT_CIRCLES+7) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+1) >= 1)&&(ShunzCount(TILE_SUIT_BAMBOOS+7) >= 1)) {
			if (MianziDat(0) == TILE_SUIT_CHARACTERS+1) {
				yakuFlag = 1
			}
		}
		if (yakuFlag) {
			addyaku tmpYakuInfo, "双龍闘蛇", 4
			delyaku tmpYakuInfo, "鏡同和"
		}
	}
#ifndef ALLSANMA
	/* 吃三嵌：三麻ではチーできないから不可 */
	yakuFlagCount = 0
	repeat SIZE_OF_MELD_BUFFER-1, 1
		if (MianziDat(cnt)/MELD_TYPE_STEP == MELD_SEQUENCE_EXPOSED_MIDDLE) {yakuFlagCount++}
	loop
	if (getRule(RULE_CHII_SANKAN) != 0)&&(yakuFlagCount == 3) {
		addyaku tmpYakuInfo, "吃三嵌", 1
	}
	if (getRule(RULE_CHII_SUUKAN) != 0)&&(yakuFlagCount == 4) {
		addyaku tmpYakuInfo, "吃四嵌", 2
	}
	/* 吃三辺：三麻ではチーできないから不可 */
	yakuFlagCount = 0
	repeat SIZE_OF_MELD_BUFFER-1, 1
		if (MianziDat(cnt) == MELD_SEQUENCE_EXPOSED_LOWER*MELD_TYPE_STEP+TILE_SUIT_CHARACTERS+7) {yakuFlagCount++}
		if (MianziDat(cnt) == MELD_SEQUENCE_EXPOSED_LOWER*MELD_TYPE_STEP+TILE_SUIT_CIRCLES+7) {yakuFlagCount++}
		if (MianziDat(cnt) == MELD_SEQUENCE_EXPOSED_LOWER*MELD_TYPE_STEP+TILE_SUIT_BAMBOOS+7) {yakuFlagCount++}
		if (MianziDat(cnt) == MELD_SEQUENCE_EXPOSED_UPPER*MELD_TYPE_STEP+TILE_SUIT_CHARACTERS+1) {yakuFlagCount++}
		if (MianziDat(cnt) == MELD_SEQUENCE_EXPOSED_UPPER*MELD_TYPE_STEP+TILE_SUIT_CIRCLES+1) {yakuFlagCount++}
		if (MianziDat(cnt) == MELD_SEQUENCE_EXPOSED_UPPER*MELD_TYPE_STEP+TILE_SUIT_BAMBOOS+1) {yakuFlagCount++}
	loop
	if (getRule(RULE_CHII_SANPEN) != 0)&&(yakuFlagCount == 3) {
		addyaku tmpYakuInfo, "吃三辺", 1
	}
	if (getRule(RULE_CHII_SUUPEN) != 0)&&(yakuFlagCount == 4) {
		addyaku tmpYakuInfo, "吃四辺", 2
	}
	/* 吃小順：三麻ではチーできないから不可 */
		yakuFlagCount = 0
		repeat SIZE_OF_MELD_BUFFER-1, 1
			if ((MianziDat(cnt)/MELD_TYPE_STEP == MELD_SEQUENCE_EXPOSED_LOWER)&&((MianziDat(cnt) \ TILE_SUIT_STEP) != 7)) {yakuFlagCount++}
		loop
	if (getRule(RULE_CHII_SHAOSHUN) != 0)&&(yakuFlagCount == 3) {
		addyaku tmpYakuInfo, "吃小順", 1
	}
	if (getRule(RULE_CHII_SHAOSUUSHUN) != 0)&&(yakuFlagCount == 4) {
		addyaku tmpYakuInfo, "吃小四順", 2
	}
	/* 吃大順：三麻ではチーできないから不可 */
	yakuFlagCount = 0
	repeat SIZE_OF_MELD_BUFFER-1, 1
		if ((MianziDat(cnt)/MELD_TYPE_STEP == MELD_SEQUENCE_EXPOSED_UPPER)&&((MianziDat(cnt) \ TILE_SUIT_STEP) != 1)) {yakuFlagCount++}
	loop
	if (getRule(RULE_CHII_TAASHUN) != 0)&&(yakuFlagCount == 3) {
		addyaku tmpYakuInfo, "吃大順", 1
	}
	if (getRule(RULE_CHII_TAASUUSHUN) != 0)&&(yakuFlagCount == 4) {
		addyaku tmpYakuInfo, "吃大四順", 2
	}
#endif
return

*checkIttsuuChanta
	tmpyaojiu = 0: JihaiCount = 0
	repeat SIZE_OF_MELD_BUFFER
		switch (MianziDat(cnt) \ TILE_CODE_MAXIMUM)
#ifndef SANMAS
			case TILE_EAST_WIND: case TILE_SOUTH_WIND: case TILE_WEST_WIND: case TILE_NORTH_WIND:
			case TILE_WHITE_DRAGON: case TILE_GREEN_DRAGON: case TILE_RED_DRAGON:
				JihaiCount++
				/* FALLTHRU */
#endif
			case TILE_SUIT_CHARACTERS+1: case TILE_SUIT_CHARACTERS+9:
			case TILE_SUIT_CIRCLES+1: case TILE_SUIT_CIRCLES+9:
			case TILE_SUIT_BAMBOOS+1: case TILE_SUIT_BAMBOOS+9:
				tmpyaojiu++
			swbreak
		swend
		if ((MianziDat(cnt) < MELD_TRIPLET*MELD_TYPE_STEP)&&((MianziDat(cnt) \ TILE_SUIT_STEP) == 7)&&(cnt > 0)) {
			tmpyaojiu++
		}
	loop
return
#global
