/*=============================
 * HSP麻雀クライアントMiHaJong
 *  [その他の役判定ルーチン]
 *=============================
 */

/* 指定の刻子・順子があるか数える */
#module
#defcfunc countSpecMentz array haiMianziDat, array targetKez, array targetShunz, int Mode
/*
		val=countSpecMentz(p1, p2, p3, p4)
		指定した種類の面子を数える

		p1 : 面子構成を格納した配列
		p2 : 探したい刻子の配列
		p3 : 探したい順子の配列
		p4 : モード(0なら雀頭も数える)

		指定した種類のうち何種類の刻子・順子があるかを返します。
*/
	if (Mode == 0) {countDuiz DuiCount, haiMianziDat} else {countKez DuiCount, haiMianziDat}
	countShunz ShunzCount, haiMianziDat
	yakuflagcount = 0
	repeat length(targetKez)
		if (targetKez(cnt)==0) {continue}
		if (DuiCount(targetKez(cnt)) >= 1) {yakuflagcount++}
	loop
	repeat length(targetShunz)
		if (targetShunz(cnt)==0) {continue}
		if (ShunzCount(targetShunz(cnt)) >= 1) {yakuflagcount++}
	loop
	return yakuflagcount
#global

/* 数字の合計を数える */
#module
#include "const.as"
#defcfunc countMentzNumerals array haiMianziDat
/*
		val=countMentzNumerals(p1)
		数字の合計を数える

		p1 : 面子構成を格納した配列

		面子にある数牌の数字の合計を返します。
*/
	Cifr = 0
	repeat SIZE_OF_MELD_BUFFER
		if (haiMianziDat(cnt) \ TILE_CODE_MAXIMUM < TILE_NUMERAL_MAX) {
			if (cnt == 0) {
				Cifr += (haiMianziDat(0) \ TILE_SUIT_STEP) * 2
			} else {
				switch (haiMianziDat(cnt) / MELD_TYPE_STEP)
					case MELD_SEQUENCE_CONCEALED: case MELD_SEQUENCE_EXPOSED_LOWER: case MELD_SEQUENCE_EXPOSED_MIDDLE: case MELD_SEQUENCE_EXPOSED_UPPER:
						Cifr += (((haiMianziDat(cnt) \ TILE_SUIT_STEP)+1) * 3)
					swbreak
					case MELD_TRIPLET_CONCEALED: case MELD_TRIPLET_EXPOSED_LEFT: case MELD_TRIPLET_EXPOSED_CENTER: case MELD_TRIPLET_EXPOSED_RIGHT:
						Cifr += ((haiMianziDat(cnt) \ TILE_SUIT_STEP) * 3)
					swbreak
					default:
						Cifr += ((haiMianziDat(cnt) \ TILE_SUIT_STEP) * 4)
					swbreak
				swend
			}
		}
	loop
	return Cifr
#global

/* その他の役 */
#module
#include "const.as"
#deffunc miscyaku var GameStat, var tmpYakuInfo, array haiMianziDat, int targetPlayer, int TsumoAgari
	/* 緑一色 */
#ifdef SANMAS
	dim targetKez,1: targetKez=TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8
	dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2
	yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
	verdflag = 0
#else
	dim targetKez,1: targetKez=TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8, TILE_GREEN_DRAGON
	dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2
	yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
	dim targetKez,1: targetKez=TILE_GREEN_DRAGON
	dim targetShunz,1: targetShunz=0
	verdflag = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
#endif
	if (yakuflagcount == SIZE_OF_MELD_BUFFER) {
#ifndef SANMAS
		if (verdflag == 0) {
#endif
			switch getRuleInt(RULE_DRAGONLESS_ALL_GREEN)
				case 0:
					/* 発無しも普通に役満とするルール
					   この場合青天井で清一色が複合*/
					addyakuman tmpYakuInfo, "緑一色", 1
#ifdef SANMAS
					delyaku tmpYakuInfo, "清一色"
#endif
					swbreak
				case 2:
					/* 発無しはダブル役満というルール */
#ifdef SANMAS
					addyakuman tmpYakuInfo, "緑一色", 2
#else
					addyakuman tmpYakuInfo, "清緑一色", 2
#endif
					// 緑一色が成立したら清一色の成立を解除する(青天井ルール用)
					delyaku tmpYakuInfo, "清一色"
					swbreak
			swend
#ifndef SANMAS
		} else {
			addyakuman tmpYakuInfo, "緑一色", 1
			// 緑一色が成立したら混一色の成立を解除する(青天井ルール用)
			delyaku tmpYakuInfo, "混一色"
		}
#endif
	}
	/* 裸単騎１飜というローカル役 */
	yakuflagcount = 0
	repeat SIZE_OF_MELD_BUFFER-1, 1
		if (((haiMianziDat(cnt)/MELD_TYPE_STEP) != MELD_SEQUENCE_CONCEALED)&&((haiMianziDat(cnt)/MELD_TYPE_STEP) != MELD_TRIPLET_CONCEALED)&&((haiMianziDat(cnt)/MELD_TYPE_STEP) != MELD_QUAD_CONCEALED)) {
			yakuflagcount++ // 副露面子の数 暗槓があってはならない
		}
	loop
	if (yakuflagcount == 4) {
		if (getRuleInt(RULE_SHIIARU_RAOTAI) != 0) {
			if ((getRuleInt(RULE_SHIIARU_RAOTAI) == 1)||(TsumoAgari == 0)) {
				if (TsumoAgari == 1) {addyaku tmpYakuInfo, "全倒鋪", 1}
				else {addyaku tmpYakuInfo, "全求人", 1}
			}
		}
		// 金鶏独立…一索待ち
		if (getRuleInt(RULE_KINKEI_DOKURITSU) != 0) {
			if (getHand(GameStat, HAND_TILECODE, TSUMOHAI_INDEX, targetPlayer) == TILE_SUIT_BAMBOOS+1) {
				addyakuman tmpYakuInfo, "金鶏独立", 1
				delyaku tmpYakuInfo, "全倒鋪"
				delyaku tmpYakuInfo, "全求人"
			}
		}
#ifndef SANMAS
		// 独釣寒江雪…白待ち
		if (getRuleInt(RULE_KINKEI_DOKURITSU) != 0) {
			if (getHand(GameStat, HAND_TILECODE, TSUMOHAI_INDEX, targetPlayer) == TILE_WHITE_DRAGON) {
				addyakuman tmpYakuInfo, "独釣寒江雪", 1
				delyaku tmpYakuInfo, "全倒鋪"
				delyaku tmpYakuInfo, "全求人"
			}
		}
#endif
	}
	/* 推不倒 */
	if (getRuleInt(RULE_TOIPUUTAO) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+1, TILE_SUIT_CIRCLES+2, TILE_SUIT_CIRCLES+3, TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+5, TILE_SUIT_CIRCLES+8, TILE_SUIT_CIRCLES+9, TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+5, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8, TILE_SUIT_BAMBOOS+9, TILE_WHITE_DRAGON
		dim targetShunz,1: targetShunz=TILE_SUIT_CIRCLES+1, TILE_SUIT_CIRCLES+2, TILE_SUIT_CIRCLES+3, TILE_SUIT_BAMBOOS+4
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if (yakuflagcount == SIZE_OF_MELD_BUFFER) {
			switch getRuleInt(RULE_TOIPUUTAO)
				case 1:
					addyaku tmpYakuInfo, "推不倒", 1
					swbreak
				case 2:
					addyaku tmpYakuInfo, "推不倒", 4
					swbreak
				case 3:
					addyakuman tmpYakuInfo, "推不倒", 1
					swbreak
			swend
		}
	}
#ifndef SANMAS
	/* 紅孔雀 */
	if (getRuleInt(RULE_BENIKUJAKU) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_BAMBOOS+1, TILE_SUIT_BAMBOOS+5, TILE_SUIT_BAMBOOS+7, TILE_SUIT_BAMBOOS+9, TILE_RED_DRAGON
		dim targetShunz,1: targetShunz=0
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		dim targetKez,1: targetKez=TILE_RED_DRAGON
		yakuflagcount += countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if (yakuflagcount == SIZE_OF_MELD_BUFFER) {
			addyakuman tmpYakuInfo, "紅孔雀", 1
			// 成立したら混一色の成立を解除する(青天井ルール用)
			delyaku tmpYakuInfo, "混一色"
			delyaku tmpYakuInfo, "対々和"
		}
	}
	/* 萬緑叢中一点紅 */
	if (getRuleInt(RULE_KOUITTEN) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8, TILE_RED_DRAGON
		dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		dim targetKez,1: targetKez=TILE_RED_DRAGON
		dim targetShunz,1: targetShunz=0
		yakuflagcount += countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if (yakuflagcount == 6) {
			addyakuman tmpYakuInfo, "紅一点", 1
			// 成立したら混一色の成立を解除する(青天井ルール用)
			delyaku tmpYakuInfo, "混一色"
		}
	}
#endif
	/* 緑一色輪 */
	if (getRuleInt(RULE_RYUUIISOHRIN) != 0) {
		yakuflagcount = 0
		if (haiMianziDat(0) == TILE_SUIT_BAMBOOS+5) {yakuflagcount = 2}
		dim targetKez,1: targetKez=TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8
		dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2
		yakuflagcount += countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if (yakuflagcount == 6) {
			addyakuman tmpYakuInfo, "緑一色輪", 1
			// 成立したら混一色の成立を解除する(青天井ルール用) 清一色は複合させる
#ifdef SANMAS
			delyaku tmpYakuInfo, "清一色"
#else
			delyaku tmpYakuInfo, "混一色"
#endif
		}
	}
	/* 大草原 */
	if (getRuleInt(RULE_DAISOUGEN) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+5, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+7, TILE_SUIT_BAMBOOS+9
		dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+5
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if (yakuflagcount == 5) {
			addyakuman tmpYakuInfo, "大草原", 1
			delyaku tmpYakuInfo, "清一色"
		}
	}
#ifndef SANMAS
	/* 桃一色 */
	if (getRuleInt(RULE_TAOYISE) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+1, TILE_SUIT_CIRCLES+3, TILE_SUIT_CIRCLES+5, TILE_SUIT_CIRCLES+6, TILE_SUIT_CIRCLES+7, TILE_SUIT_CIRCLES+9, TILE_RED_DRAGON
		dim targetShunz,1: targetShunz=TILE_SUIT_CIRCLES+5
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		countDuiz DuiCount, haiMianziDat
		countRedTilesInHand redCount, GameStat, targetPlayer
		if ((yakuflagcount == SIZE_OF_MELD_BUFFER)&&(redCount(TILE_SUIT_CIRCLES+5) >= 1)&&(DuiCount(TILE_RED_DRAGON) >= 1)) {
			addyakuman tmpYakuInfo, "桃一色", 1
			delyaku tmpYakuInfo, "混一色"
		}
	}
	/* 茶一色 */
	if (getRuleInt(RULE_CHAYISE) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8, TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND, TILE_WHITE_DRAGON, TILE_GREEN_DRAGON
		dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "緑一色")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "字一色")
		if ((yakuflagcount == SIZE_OF_MELD_BUFFER)&&(tmpyakuCensored == 0)) {
			addyakuman tmpYakuInfo, "茶一色", 1
		}
	}
	/* 黒一色 */
	if (getRuleInt(RULE_HEIIISOH) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+2, TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+8, TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND
		dim targetShunz,1: targetShunz=0
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if (yakuflagcount == SIZE_OF_MELD_BUFFER) {
			addyakuman tmpYakuInfo, "黒一色", 1
			delyaku tmpYakuInfo, "混一色"
			delyaku tmpYakuInfo, "対々和"
		}
	}
	/* 青洞門 */
	if (getRuleInt(RULE_AO_NO_DOMON) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+2, TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+8, TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND, TILE_GREEN_DRAGON
		dim targetShunz,1: targetShunz=0
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		dim targetKez,1: targetKez=TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND
		windflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if ((yakuflagcount == SIZE_OF_MELD_BUFFER)&&(windflagcount == 1)) {
			addyakuman tmpYakuInfo, "青洞門", 1
			delyaku tmpYakuInfo, "混一色"
			delyaku tmpYakuInfo, "対々和"
		}
	}
#endif
	/* 断紅和 */
	if (getRuleInt(RULE_NO_RED) != 0) {
#ifdef SANMAS
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+2, TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+8, TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8
		dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		yakuflagcount2 = 0
		tmpyakuCensored = 0
#else
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+2, TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+8, TILE_SUIT_BAMBOOS+2, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+6, TILE_SUIT_BAMBOOS+8, TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND, TILE_WHITE_DRAGON, TILE_GREEN_DRAGON
		dim targetShunz,1: targetShunz=TILE_SUIT_BAMBOOS+2
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		dim targetKez,1: targetKez=TILE_EAST_WIND, TILE_SOUTH_WIND, TILE_WEST_WIND, TILE_NORTH_WIND, TILE_WHITE_DRAGON, TILE_GREEN_DRAGON
		dim targetShunz,1: targetShunz=0
		yakuflagcount2 = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "緑一色")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "黒一色")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "青洞門")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "雪の銀閣")
#endif
		if ((yakuflagcount == SIZE_OF_MELD_BUFFER)&&(tmpyakuCensored == 0)) {
			if ((yakuflagcount2 == 0)&&(getRuleInt(RULE_NO_RED) == 2)) {
				/* 清断紅 */
#ifdef SANMAS
				addyaku tmpYakuInfo, "断紅和", 5+getMenzen(GameStat, targetPlayer)
#else
				addyaku tmpYakuInfo, "清断紅", 5+getMenzen(GameStat, targetPlayer)
#endif
			} else {
				/* 混断紅 */
#ifndef SANMAS
				if (getRuleInt(RULE_NO_RED) == 2) {addyaku tmpYakuInfo, "混断紅", 2+getMenzen(GameStat, targetPlayer)}
				else {
#endif
					addyaku tmpYakuInfo, "断紅和", 2+getMenzen(GameStat, targetPlayer)
#ifndef SANMAS
				}
#endif
			}
		}
	}
#ifndef SANMAS
	/* 白一点 */
	if (getRuleInt(RULE_HAKUITTEN) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+1, TILE_SUIT_CIRCLES+3, TILE_SUIT_CIRCLES+5, TILE_SUIT_CIRCLES+6, TILE_SUIT_CIRCLES+7, TILE_SUIT_CIRCLES+9, TILE_WHITE_DRAGON
		dim targetShunz,1: targetShunz=TILE_SUIT_CIRCLES+5
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		dim targetKez,1: targetKez=TILE_WHITE_DRAGON
		dim targetShunz,1: targetShunz=0
		dragonflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if ((yakuflagcount == SIZE_OF_MELD_BUFFER)&&(dragonflagcount == 1)) {
			addyakuman tmpYakuInfo, "白一点", 4+getMenzen(GameStat, targetPlayer)
			delyaku tmpYakuInfo, "混一色"
		}
	}
	/* 紅白饅頭 */
	if (getRuleInt(RULE_KOHAKU_MANJU) != 0) {
		yakuFlag = 0
		countDuiz DuiCount, haiMianziDat
		if ((DuiCount(TILE_RED_DRAGON) >= 1)&&(DuiCount(TILE_WHITE_DRAGON) >= 1)) {yakuFlag = 1}
		dim targetKez,1: targetKez=TILE_SUIT_CHARACTERS+1, TILE_SUIT_CHARACTERS+2, TILE_SUIT_CHARACTERS+3, TILE_SUIT_CHARACTERS+4, TILE_SUIT_CHARACTERS+5, TILE_SUIT_CHARACTERS+6, TILE_SUIT_CHARACTERS+7, TILE_SUIT_CHARACTERS+8, TILE_SUIT_CHARACTERS+9
		dim targetShunz,1: targetShunz=TILE_SUIT_CHARACTERS+1, TILE_SUIT_CHARACTERS+2, TILE_SUIT_CHARACTERS+3, TILE_SUIT_CHARACTERS+4, TILE_SUIT_CHARACTERS+5, TILE_SUIT_CHARACTERS+6, TILE_SUIT_CHARACTERS+7
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if ((yakuFlag == 1)&&(yakuflagcount >= 3)) {
			switch getRuleInt(RULE_KOHAKU_MANJU)
				case 1: addyaku tmpYakuInfo, "紅白饅頭", 1: swbreak
				case 2: addyaku tmpYakuInfo, "紅白饅頭", 2: swbreak
			swend
		}
	}
#endif
	/* 大四帰四 */
	if (getRuleInt(RULE_GREAT_FOUR_INTO_FOUR) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX /* 順子と雀頭で4枚使い３つ */
			// 123 123 123 123: 牌式44440
			// 123 123 123 234: 牌式34410
			// 123 123 123 345: 牌式33411
			// 123 123 234 234: 牌式24420
			if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+1) >= 2)) {yakuflagcount++}
			if ((haiMianziDat(0) == cnt+3)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+1) >= 2)) {yakuflagcount++}
			// 123 123 234 345: 牌式23421
			if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+1) >= 1)&&(ShunzCount(cnt+2) >= 1)) {yakuflagcount++}
			if ((haiMianziDat(0) == cnt+3)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+1) >= 1)&&(ShunzCount(cnt+2) >= 1)) {yakuflagcount++}
			// 123 123 345 345: 牌式22422
			if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			if ((haiMianziDat(0) == cnt+1)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			if ((haiMianziDat(0) == cnt+3)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			if ((haiMianziDat(0) == cnt+4)&&(ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			// 123 234 234 234: 牌式14430
			// 123 234 234 345: 牌式13431
			// 123 234 345 345: 牌式12432
			if ((haiMianziDat(0) == cnt+1)&&(ShunzCount(cnt) >= 1)&&(ShunzCount(cnt+1) >= 1)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			if ((haiMianziDat(0) == cnt+4)&&(ShunzCount(cnt) >= 1)&&(ShunzCount(cnt+1) >= 1)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			// 123 345 345 345: 牌式11433
		loop
		if (yakuflagcount == 1) {
			addyakuman tmpYakuInfo, "大四帰四", 1
		}
	}
	/* 重四帰四 */
	if (getRuleInt(RULE_DOUBLE_FOUR_INTO_FOUR) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX /* 順子で4枚使い */
			// 123 123 123 234
			if ((ShunzCount(cnt) >= 3)&&(ShunzCount(cnt+1) >= 1)) {yakuflagcount++}
			// 123 123 234 234
			if ((ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+1) >= 2)) {yakuflagcount++}
			// 123 234 234 234
			if ((ShunzCount(cnt) >= 1)&&(ShunzCount(cnt+1) >= 3)) {yakuflagcount++}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "一色四順")
		if ((yakuflagcount == 1)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "重四帰四", 8
		}
	}
	/* 四帰四 */
	if (getRuleInt(RULE_FOUR_INTO_FOUR) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX /* 順子で4枚使い */
			// 123 123 123 345
			if ((ShunzCount(cnt) >= 3)&&(ShunzCount(cnt+2) >= 1)) {yakuflagcount++}
			// 123 123 234 345
			if ((ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+1) >= 1)&&(ShunzCount(cnt+2) >= 1)) {yakuflagcount++}
			// 123 123 345 345
			if ((ShunzCount(cnt) >= 2)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			// 123 234 234 345
			if ((ShunzCount(cnt) >= 1)&&(ShunzCount(cnt+1) >= 2)&&(ShunzCount(cnt+2) >= 1)) {yakuflagcount++}
			// 123 234 345 345
			if ((ShunzCount(cnt) >= 1)&&(ShunzCount(cnt+1) >= 1)&&(ShunzCount(cnt+2) >= 2)) {yakuflagcount++}
			// 123 345 345 345
			if ((ShunzCount(cnt) >= 1)&&(ShunzCount(cnt+2) >= 3)) {yakuflagcount++}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "重四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "一色四順")
		if ((yakuflagcount == 1)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "四帰四", 6
		}
	}
	/* 四帰三一 */
	if (getRuleInt(RULE_FOUR_INTO_THREE_ONE) != 0) {
		yakuflagcount = 0: yakuflagcount2 = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX
			if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt) >= 2)) {yakuflagcount++}
			if (cnt > 0) {
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-1) >= 1)&&(ShunzCount(cnt) >= 1)) {yakuflagcount++}
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-1) >= 2)) {yakuflagcount++}
			}
			if (cnt > 1) {
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-2) >= 1)&&(ShunzCount(cnt) >= 1)) {yakuflagcount++}
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-2) >= 1)&&(ShunzCount(cnt-1) >= 1)) {yakuflagcount++}
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-2) >= 2)) {yakuflagcount++}
			}
		loop
		repeat TILE_NUMERAL_MAX /* 刻子と順子で4枚使い */
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt) >= 1)) {yakuflagcount2++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)) {yakuflagcount2++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt+2) >= 1)) {yakuflagcount2++}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "重四帰四")
		if ((yakuflagcount == 1)&&(yakuflagcount2 == 1)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "四帰三一", 4
		}
	}
	/* 四帰三 */
	if (getRuleInt(RULE_FOUR_INTO_THREE) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX /* 順子二つと雀頭で4枚使い */
			if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt) >= 2)) {yakuflagcount++}
			if (cnt > 0) {
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-1) >= 1)&&(ShunzCount(cnt) >= 1)) {yakuflagcount++}
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-1) >= 2)) {yakuflagcount++}
			}
			if (cnt > 1) {
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-2) >= 1)&&(ShunzCount(cnt) >= 1)) {yakuflagcount++}
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-2) >= 1)&&(ShunzCount(cnt-1) >= 1)) {yakuflagcount++}
				if ((haiMianziDat(0) == cnt)&&(ShunzCount(cnt-2) >= 2)) {yakuflagcount++}
			}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "四帰三一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "重四帰四")
		if ((yakuflagcount == 1)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "四帰三", 3
		}
	}
	/* 龍四帰一 */
	if (getRuleInt(RULE_DRAGON_FOUR_INTO_ONE) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)&&(KezCount(cnt+2) >= 1)) {yakuflagcount++}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "重四帰四")
		if ((yakuflagcount == 1)&&(tmpyakuCensored == 0)) {
			addyakuman tmpYakuInfo, "龍四帰一", 1
			delyaku tmpYakuInfo, "三連刻"
		}
	}
	/* 虎四帰一 */
	if (getRuleInt(RULE_TIGER_FOUR_INTO_ONE) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)) {yakuflagcount++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt) >= 1)&&(KezCount(cnt+2) >= 1)) {yakuflagcount++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)&&(KezCount(cnt+2) >= 1)) {yakuflagcount++}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "龍四帰一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "重四帰四")
		if ((yakuflagcount == 1)&&(tmpyakuCensored == 0)) {
			switch getRuleInt(RULE_TIGER_FOUR_INTO_ONE)
				case 1: addyaku tmpYakuInfo, "虎四帰一", 2: swbreak
				case 2: addyaku tmpYakuInfo, "虎四帰一", 3: swbreak
			swend
		}
	}
	/* 両四帰一 */
	if (getRuleInt(RULE_DOUBLE_FOUR_INTO_ONE) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX /* 刻子と順子で4枚使い×2組 */
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt) >= 1)) {yakuflagcount++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)) {yakuflagcount++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt+2) >= 1)) {yakuflagcount++}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "龍四帰一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "虎四帰一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "重四帰四")
		if ((yakuflagcount == 2)&&(tmpyakuCensored == 0)) {
			switch getRuleInt(RULE_DOUBLE_FOUR_INTO_ONE)
				case 1: addyaku tmpYakuInfo, "両四帰一", 2: swbreak
				case 2: addyaku tmpYakuInfo, "両四帰一", 3: swbreak
			swend
		}
	}
	/* 四帰一 */
	if (getRuleInt(RULE_FOUR_INTO_ONE) != 0) {
		yakuflagcount = 0
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		repeat TILE_NUMERAL_MAX /* 刻子と順子で4枚使い */
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt) >= 1)) {yakuflagcount++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt+1) >= 1)) {yakuflagcount++}
			if ((ShunzCount(cnt) >= 1)&&(KezCount(cnt+2) >= 1)) {yakuflagcount++}
		loop
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "龍四帰一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "虎四帰一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "両四帰一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "四帰三")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "四帰三一")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "大四帰四")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "重四帰四")
		if ((yakuflagcount == 1)&&(tmpyakuCensored == 0)) {
			addyaku tmpYakuInfo, "四帰一", 1
		}
	}
	/* 九蓮花燈 */
	if (getRuleInt(RULE_CHUUREN_HWATON) != 0) {
		countTilesInHand haiCount, GameStat, targetPlayer
		yakuflagcount = 0
		if ((haiCount(TILE_SUIT_CHARACTERS+1)+haiCount(TILE_SUIT_CIRCLES+1)+haiCount(TILE_SUIT_BAMBOOS+1)) >= 3) {yakuflagcount++}
		repeat 7,2
			if ((haiCount(TILE_SUIT_CHARACTERS+cnt)+haiCount(TILE_SUIT_CIRCLES+cnt)+haiCount(TILE_SUIT_BAMBOOS+cnt)) >= 1) {yakuflagcount++}
		loop
		if ((haiCount(TILE_SUIT_CHARACTERS+9)+haiCount(TILE_SUIT_CIRCLES+9)+haiCount(TILE_SUIT_BAMBOOS+9)) >= 3) {yakuflagcount++}
		tmpyakuCensored = 0
		tmpyakuCensored += searchyaku(tmpYakuInfo, "純正九蓮宝燈")
		tmpyakuCensored += searchyaku(tmpYakuInfo, "九蓮宝燈")
		if ((yakuflagcount == 9)&&(tmpyakuCensored == 0)) {
			if (getMenzen(GameStat, targetPlayer) == 1) {
				switch getRuleInt(RULE_CHUUREN_HWATON)
					case 1:
						addyaku tmpYakuInfo, "九蓮花燈", 4
						swbreak
					case 2:
						addyaku tmpYakuInfo, "九蓮花燈", 2
						swbreak
				swend
			}
		}
	}
	/* 鳴き九連宝燈 */
	if (getRuleInt(RULE_EXPOSED_CHUUREN) != 0) {
		countTilesInHand haiCount, GameStat, targetPlayer
		// 鳴き面子も計算 槓子は無視
		repeat SIZE_OF_MELD_BUFFER-1,1
			switch (haiMianziDat(cnt) / MELD_TYPE_STEP)
				case MELD_SEQUENCE_EXPOSED_LOWER: case MELD_SEQUENCE_EXPOSED_MIDDLE: case MELD_SEQUENCE_EXPOSED_UPPER
					haiCount(haiMianziDat(cnt) \ TILE_CODE_MAXIMUM)++
					haiCount(haiMianziDat(cnt) \ TILE_CODE_MAXIMUM + 1)++
					haiCount(haiMianziDat(cnt) \ TILE_CODE_MAXIMUM + 2)++
					swbreak
				case MELD_TRIPLET_EXPOSED_LEFT: case MELD_TRIPLET_EXPOSED_CENTER: case MELD_TRIPLET_EXPOSED_RIGHT
					haiCount(haiMianziDat(cnt) \ TILE_CODE_MAXIMUM) += 3
					swbreak
			swend
		loop
		yakuflag = 0
		repeat 3
			if ((haiCount(cnt*TILE_SUIT_STEP+1) >= 3)&&(haiCount(cnt*TILE_SUIT_STEP+2) >= 1)&&(haiCount(cnt*TILE_SUIT_STEP+3) >= 1)&&(haiCount(cnt*TILE_SUIT_STEP+4) >= 1)&&(haiCount(cnt*TILE_SUIT_STEP+5) >= 1)&&(haiCount(cnt*TILE_SUIT_STEP+6) >= 1)&&(haiCount(cnt*TILE_SUIT_STEP+7) >= 1)&&(haiCount(cnt*TILE_SUIT_STEP+8) >= 1)&&(haiCount(cnt*TILE_SUIT_STEP+9) >= 3)) {yakuFlag = 1}
		loop
		if ((yakuflag == 1)&&(getMenzen(GameStat, targetPlayer) == 0)) {
			addyaku tmpYakuInfo, "九蓮碧燈", 3
		}
	}
	/* 茴香ポン(非対々) */
	if (getRuleInt(RULE_UIKYOU_SHUNTSU) != 0) {
		countKez KezCount, haiMianziDat
		countShunz ShunzCount, haiMianziDat
		yakuFlag = 0
		repeat 5, 1
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CHARACTERS+1) >= 1)&&(haiMianziDat(0) == cnt+TILE_SUIT_CHARACTERS+4)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES+1) >= 1)&&(haiMianziDat(0) == cnt+TILE_SUIT_CIRCLES+4)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS+1) >= 1)&&(haiMianziDat(0) == cnt+TILE_SUIT_BAMBOOS+4)) {yakuFlag = 1}
		loop
		repeat 5, 5
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CHARACTERS-3) >= 1)&&(haiMianziDat(0) == cnt+TILE_SUIT_CHARACTERS-4)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_CIRCLES-3) >= 1)&&(haiMianziDat(0) == cnt+TILE_SUIT_CIRCLES-4)) {yakuFlag = 1}
			if ((KezCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KezCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KezCount(cnt+TILE_SUIT_BAMBOOS) >= 1)&&(ShunzCount(cnt+TILE_SUIT_BAMBOOS-3) >= 1)&&(haiMianziDat(0) == cnt+TILE_SUIT_BAMBOOS-4)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			// 対々形と区別するために後ろにスペースを入れる
			if (getRuleInt(RULE_UIKYOU_SHUNTSU) == 2) {
				addyaku tmpYakuInfo, "茴香ポン ", 3
			} else {
				addyaku tmpYakuInfo, "茴香ポン ", 4
			}
		}
	}
	/* 武太郎売焼餅 */
	if (getRuleInt(RULE_UUTAARAN) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CHARACTERS+5, TILE_SUIT_CIRCLES+5, TILE_SUIT_BAMBOOS+5
		dim targetShunz,1: targetShunz=TILE_SUIT_CHARACTERS+3, TILE_SUIT_CHARACTERS+4, TILE_SUIT_CHARACTERS+5, TILE_SUIT_CIRCLES+3, TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+5, TILE_SUIT_BAMBOOS+3, TILE_SUIT_BAMBOOS+4, TILE_SUIT_BAMBOOS+5
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 1)
		if ((yakuflagcount == 4)&&(haiMianziDat(0) == TILE_SUIT_CIRCLES+1)) {
			addyaku tmpYakuInfo, "武太郎売焼餅", 2+getMenzen(GameStat, targetPlayer)
		}
	}
	/* 愚濫怒苦露酢 */
	if (getRuleInt(RULE_GRAND_CROSS) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+6
		dim targetShunz,1: targetShunz=TILE_SUIT_CIRCLES+1, TILE_SUIT_CIRCLES+7
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 1)
		if ((yakuflagcount == 4)&&(haiMianziDat(0) == TILE_SUIT_CIRCLES+5)&&(getMenzen(GameStat, targetPlayer) == 1)) {
			if (getHand(GameStat, HAND_TILECODE, TSUMOHAI_INDEX, targetPlayer) == TILE_SUIT_CIRCLES+5) {
				addyakuman tmpYakuInfo, "愚濫怒苦露酢", 2
			} else {
				addyakuman tmpYakuInfo, "愚濫怒苦露酢", 1
			}
			delyaku tmpYakuInfo, "清一色"
		}
	}
	/* 喜鵲閙梅 */
	if (getRuleInt(RULE_XIQUE_NAOMEI) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CIRCLES+5, TILE_SUIT_BAMBOOS+1
		dim targetShunz,1: targetShunz=TILE_SUIT_CHARACTERS+1, TILE_SUIT_CIRCLES+1
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 1)
		if ((yakuflagcount == 4)&&(haiMianziDat(0) == TILE_SUIT_BAMBOOS+5)) {
			addyaku tmpYakuInfo, "喜鵲閙梅", 6
		}
	}
	/* 花一色 */
	if (getRuleInt(RULE_HUAIISOH) != 0) {
		dim targetKez,1: targetKez=TILE_SUIT_CHARACTERS+1, TILE_SUIT_CHARACTERS+2, TILE_SUIT_CHARACTERS+3, TILE_SUIT_CHARACTERS+4, TILE_SUIT_CHARACTERS+5, TILE_SUIT_CHARACTERS+6, TILE_SUIT_CHARACTERS+7, TILE_SUIT_CHARACTERS+8, TILE_SUIT_CHARACTERS+9, TILE_RED_DRAGON, TILE_SUIT_CIRCLES+1, TILE_SUIT_CIRCLES+3, TILE_SUIT_CIRCLES+5, TILE_SUIT_CIRCLES+6, TILE_SUIT_CIRCLES+7, TILE_SUIT_CIRCLES+9, TILE_SUIT_BAMBOOS+1, TILE_SUIT_BAMBOOS+5, TILE_SUIT_BAMBOOS+7, TILE_SUIT_BAMBOOS+9
		dim targetShunz,1: targetShunz=TILE_SUIT_CHARACTERS+1, TILE_SUIT_CHARACTERS+2, TILE_SUIT_CHARACTERS+3, TILE_SUIT_CHARACTERS+4, TILE_SUIT_CHARACTERS+5, TILE_SUIT_CHARACTERS+6, TILE_SUIT_CHARACTERS+7, TILE_SUIT_CIRCLES+5
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		dim targetKez,1: targetKez=TILE_SUIT_CHARACTERS+1, TILE_SUIT_CHARACTERS+2, TILE_SUIT_CHARACTERS+3, TILE_SUIT_CHARACTERS+4, TILE_SUIT_CHARACTERS+5, TILE_SUIT_CHARACTERS+6, TILE_SUIT_CHARACTERS+7, TILE_SUIT_CHARACTERS+8, TILE_SUIT_CHARACTERS+9, TILE_RED_DRAGON
		dim targetShunz,1: targetShunz=TILE_SUIT_CHARACTERS+1, TILE_SUIT_CHARACTERS+2, TILE_SUIT_CHARACTERS+3, TILE_SUIT_CHARACTERS+4, TILE_SUIT_CHARACTERS+5, TILE_SUIT_CHARACTERS+6, TILE_SUIT_CHARACTERS+7
		yakuflagcount2 = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if ((yakuflagcount == SIZE_OF_MELD_BUFFER)&&(yakuflagcount2 < SIZE_OF_MELD_BUFFER)) {
			addyaku tmpYakuInfo, "花一色", 1
		}
	}
#ifndef SANMAS
	/* 赤眼青龍 */
	if (getRuleInt(RULE_SEKIGAN_SEIRYUU) != 0) {
		dim targetKez,1: targetKez=TILE_EAST_WIND, TILE_WHITE_DRAGON, TILE_SUIT_CIRCLES+2, TILE_SUIT_CIRCLES+4, TILE_SUIT_CIRCLES+8
		dim targetShunz,1: targetShunz=TILE_SUIT_CIRCLES+2
		yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		dim targetKez,1: targetKez=TILE_EAST_WIND, TILE_WHITE_DRAGON
		dim targetShunz,1: targetShunz=0
		yakuflagcount2 = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
		if ((yakuflagcount == SIZE_OF_MELD_BUFFER)&&(yakuflagcount2 == 1)) {
			addyakuman tmpYakuInfo, "赤眼青龍", 1
		}
	}
#endif
	/* 満園花 */
	tmpRuleCodeList = RULE_ALL_ONE, RULE_ALL_TWO, RULE_ALL_THREE, RULE_ALL_FOUR, RULE_ALL_FIVE, RULE_ALL_SIX, RULE_ALL_SEVEN, RULE_ALL_EIGHT, RULE_ALL_NINE
	sdim tmpYakuNameList, 16, length(tmpRuleCodeList)
	tmpYakuNameList = "全帯一", "全帯二", "全帯三", "全帯四", "全帯五", "全帯六", "全帯七", "全帯八", "全帯九"
	repeat 9, 1
		if (getRuleInt(tmpRuleCodeList(cnt-1)) != 0) {
			dim targetKez,1: targetKez=cnt, cnt+10, cnt+20
			dim targetShunz,1
			if (cnt <= 7) {targetShunz(0)=cnt+TILE_SUIT_CHARACTERS, cnt+TILE_SUIT_CIRCLES, cnt+TILE_SUIT_BAMBOOS}
			if ((cnt >= 2)&&(cnt <= 8)) {targetShunz(3)=cnt+TILE_SUIT_CHARACTERS-1, cnt+TILE_SUIT_CIRCLES-1, cnt+TILE_SUIT_BAMBOOS-1}
			if (cnt >= 3) {targetShunz(6)=cnt+TILE_SUIT_CHARACTERS-2, cnt+TILE_SUIT_CIRCLES-2, cnt+TILE_SUIT_BAMBOOS-2}
			yakuflagcount = countSpecMentz(haiMianziDat, targetKez, targetShunz, 0)
			if (yakuflagcount == SIZE_OF_MELD_BUFFER) {
				if (cnt == 5) {
					switch getRuleInt(tmpRuleCodeList(cnt-1))
						case 1: addyaku tmpYakuInfo, tmpYakuNameList(cnt-1), 2: swbreak
						case 2: addyaku tmpYakuInfo, tmpYakuNameList(cnt-1), 2+getMenzen(GameStat, targetPlayer): swbreak
						case 3: addyaku tmpYakuInfo, tmpYakuNameList(cnt-1), 4: swbreak
						case 4: addyaku tmpYakuInfo, tmpYakuNameList(cnt-1), 5+getMenzen(GameStat, targetPlayer): swbreak
					swend
				} else: if ((cnt == 1)||(cnt == 9)) {
					switch getRuleInt(tmpRuleCodeList(cnt-1))
						case 1: addyaku tmpYakuInfo, tmpYakuNameList(cnt-1), 4: swbreak
						case 2: addyaku tmpYakuInfo, tmpYakuNameList(cnt-1), 5+getMenzen(GameStat, targetPlayer): swbreak
					swend
					delyaku tmpYakuInfo, "純全帯幺九"
				} else {
					switch getRuleInt(tmpRuleCodeList(cnt-1))
						case 1: addyaku tmpYakuInfo, tmpYakuNameList(cnt-1), 4: swbreak
					swend
				}
				if ((cnt >= 4)&&(cnt <= 6)) {
					if ((getRuleInt(RULE_KUITAN) == 2)&&(getMenzen(GameStat, targetPlayer) == 0)) {addYakuInfo tmpYakuInfo, YAKUINF_HAN_BASE, 1: addYakuInfo tmpYakuInfo, YAKUINF_HAN_BONUS, -1}
					delyaku tmpYakuInfo, "断幺九"
				}
			}
		}
	loop
return
	/* 黒衣騎士 */
	if (getRuleInt(RULE_BLACKJACK) != 0) {
		if (countMentzNumerals(haiMianziDat) == 21) {
			addyaku tmpYakuInfo, "黒衣騎士", 2
		}
	}
	/* 白衣騎士 */
	if (getRuleInt(RULE_WHITE_KNIGHT) != 0) {
		countKez KezCount, haiMianziDat
		if (KezCount(TILE_WHITE_DRAGON) >= 1) {
			if (countMentzNumerals(haiMianziDat) == 21) {
				addyakuman tmpYakuInfo, "白衣騎士", 1
			}
		}
	}
#global
