/*=============================
 * HSP麻雀クライアントMiHaJong
 *   [十三不搭判定ルーチン]
 *=============================
 */

#ifdef SANMAS
/* 数牌三麻用のダミー */
#module
#include "const.hsp"
#include "struct.hsp"
#defcfunc chkshisanbuda var GameStat, int targetPlayer
	return 0 // dummy
#global

#module
#include "const.hsp"
#include "struct.hsp"
#defcfunc chkshisibuda var GameStat, int targetPlayer
	return 0 // dummy
#global

#else
/* 十三不塔の判定 */
#module
#include "const.hsp"
#include "struct.hsp"
#defcfunc chkshisanbuda var GameStat, int targetPlayer
	ShisanBuDa = 0
	Ukihai = 0
	Duizi = 0
	repeat NUM_OF_TILES_IN_HAND
		TileInfo = getpaiinfo(GameStat, targetPlayer, cnt, 0)
		if (TileInfo == 0) {Ukihai++}
		if (TileInfo == 4) {Duizi++}
	loop
	if ((Ukihai == 12)&&(Duizi == 2)) {ShisanBuDa = 1}
	return ShisanBuDa
#global

/* 十四不塔の判定 */
#module
#include "const.hsp"
#include "struct.hsp"
#defcfunc chkshisibuda var GameStat, int targetPlayer
	ShisiBuDa = 0
	Ukihai = 0
	repeat NUM_OF_TILES_IN_HAND
		TileInfo = getpaiinfo(GameStat, targetPlayer, cnt, 0)
		if (TileInfo == 0) {Ukihai++}
		if (TileInfo == 4) {Duizi++}
	loop
	if (Ukihai == NUM_OF_TILES_IN_HAND) {ShisiBuDa = 1}
	return ShisiBuDa
#global

/* 十三不塔 */
#module
#include "const.hsp"
#include "struct.hsp"
#deffunc shisanbudayaku var GameStat, var tmpYakuInfo, int targetPlayer, array tmpScore, array ShantenArray
	if (getRule(RULE_SHIISAN_PUUTAA) != 0) {
		ShisanBuDa = chkshisanbuda(GameStat, targetPlayer)
		if ((ShisanBuDa == 1)&&(ShantenArray(SHANTEN_ORPHANS) >= 0)&&(getFirstDrawFlag(GameStat, targetPlayer) == 1)) {
			delmod tmpYakuInfo: newmod tmpYakuInfo, yakuStruct
			setYakuInfo tmpYakuInfo, YAKUINF_FU, 30
			if (getRule(RULE_SHIISAN_PUUTAA) == 2) {
				addmangan tmpYakuInfo, "十三不搭", 2
			} else {
				addyakuman tmpYakuInfo, "十三不搭", 1
			}
			countdora GameStat, tmpYakuInfo, targetPlayer
			tmpScore = 30: calculateScore tmpYakuInfo, tmpScore
		} else {
			// 条件満たしてない場合、国士無双だったら何もしない。国士でないならチョンボ
			Shanten = ShantenArray(SHANTEN_ALL)
			if (Shanten >= 0) {
				tmpScore = 0: setYakuInfo tmpYakuInfo, YAKUINF_HAN_BASE, 0
			}
		}
	} else {
		// 和了ってない和了ってない
		Shanten = ShantenArray(SHANTEN_ALL)
		if (Shanten >= 0) {
			tmpScore = 0: setYakuInfo tmpYakuInfo, YAKUINF_HAN_BASE, 0
		}
	}
return
#global

/* 十四不塔 */
#module
#include "const.hsp"
#include "struct.hsp"
#deffunc shisibudayaku var GameStat, var tmpYakuInfo, int targetPlayer, array tmpScore, array ShantenArray
	if (getRule(RULE_SHIISAN_UUSHII) != 0) {
		ShisiBuDa = chkshisibuda(GameStat, targetPlayer)
		if ((ShisiBuDa == 1)&&(getFirstDrawFlag(GameStat, targetPlayer) == 1)) {
			delmod tmpYakuInfo: newmod tmpYakuInfo, yakuStruct
			setYakuInfo tmpYakuInfo, YAKUINF_FU, 30
			addyakuman tmpYakuInfo, "十三無靠", 1
			countdora GameStat, tmpYakuInfo, targetPlayer
			tmpScore = 30: calculateScore tmpYakuInfo, tmpScore
		} else {
			// 和了ってない和了ってない
			Shanten = ShantenArray(SHANTEN_ALL)
			if (Shanten >= 0) {
				tmpScore = 0: setYakuInfo tmpYakuInfo, YAKUINF_HAN_BASE, 0
			}
		}
	} else {
		// 和了ってない和了ってない
		Shanten = ShantenArray(SHANTEN_ALL)
		if (Shanten >= 0) {
			tmpScore = 0: setYakuInfo tmpYakuInfo, YAKUINF_HAN_BASE, 0
		}
	}
return
#global
#endif
