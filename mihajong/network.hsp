/*=============================
 * HSP麻雀クライアントMiHaJong
 * [ネットワーク処理ルーチン]
 *=============================
 */

/* 接続先の打牌 */
#module
#uselib "mjcore.dll"
#func remotedahai_begin "?remotedahai_begin@RemoteAction@@YAXQAUGameTable@@@Z" int
#cfunc remotedahai_isfinished "?remotedahai_isfinished@RemoteAction@@YAHXZ"
#cfunc remotedahai_getdiscard "?remotedahai_getdiscard@RemoteAction@@YAHXZ"
#func remotedahai_end "?remotedahai_end@RemoteAction@@YAXXZ"
#deffunc remotedahai var GameStat, var GameEnv
	remotedahai_begin GameStat
	repeat
		if (remotedahai_isfinished()) {break}
		await 0
	loop
	DiscardTileIndex = remotedahai_getdiscard()
	remotedahai_end
	return DiscardTileIndex
#global

/* 接続先の鳴き */
#module
#uselib "mjcore.dll"
#func remotenaki_begin"?remotenaki_begin@RemoteAction@@YAXQAUGameTable@@@Z" int
#cfunc remotenaki_isfinished "?remotenaki_isfinished@RemoteAction@@YAHXZ"
#func remotenaki_end "?remotenaki_end@RemoteAction@@YAXXZ"
#deffunc remotenaki var GameStat, var GameEnv
	remotenaki_begin GameStat
	repeat
		if (remotenaki_isfinished()) {break}
		await 0
	loop
	remotenaki_end
	return
#global

/* 落ち戻り用の処理 */
*reconnection
	if (ReconnectionSuccess == 0) {
		dialog "サーバーとの接続が切れました\n再接続を試みます", 1, "回線切断"
	}
	sockclose 0+SOCK_GAME: await 1000
	ReconnectionSuccess = 0
	repeat 10
		sockopen 0+SOCK_GAME, ServerAddress, PORT_GAME+ClientNumber
		if (stat == 0) {ReconnectionSuccess = 1: break}
		await 1000
	loop
	if (ReconnectionSuccess == 0) {
		errordlg "再接続に失敗しました\nゲームは続行できません", "回線切断"
		if (getGameMode(GameEnv) != GAMEMODE_STANDALONE) {sockclose SOCK_GAME+0}
		if (getGameMode(GameEnv) == GAMEMODE_SERVER) {sockclose SOCK_GAME+1: sockclose SOCK_GAME+2: sockclose SOCK_GAME+3}
		font fontname, 48, 0: objsize 200, 60
		pos 620, 560: colorbutton goto "確認", *startgame, -1, 0xffffff
		stop
	}
return

/* チャットバッファ受信 */
#module
#include "const.hsp"
#include "mjcore.hsp"
#deffunc initializechat var GameEnv, var ServerAddress, int ClientNumber
	chatinit: rulewndinit
	if (getGameMode(GameEnv) == GAMEMODE_STANDALONE) {gsel SCR_CHAT: objgray 1, 0: objgray 2, 0: gsel 0}
	if (getGameMode(GameEnv) == GAMEMODE_SERVER) {
		ChatBuf1 = "": ChatBuf2 = "": ChatBuf3 = ""
		repeat NUM_OF_PLAYERS
			if (IsRemotePlayer(GameEnv, cnt) > 1) {tmpClientWaiting(cnt) = 1}
			else {tmpClientWaiting(cnt) = 0}
		loop
		repeat
			repeat NUM_OF_PLAYERS
				if (IsRemotePlayer(GameEnv, cnt) > 1) {
					if (tmpClientWaiting(cnt) == 1) {
						sockwait@ SOCK_CHAT-1+IsRemotePlayer(GameEnv, cnt)
						if (stat == 0) {tmpClientWaiting(cnt) = 0}
					;	title ""+tmpClientWaiting(0)+" "+tmpClientWaiting(1)+" "+tmpClientWaiting(2)+" "+tmpClientWaiting(3)
					}
				}
			loop
			await 0
			if ((tmpClientWaiting(0)+tmpClientWaiting(1)+tmpClientWaiting(2)+tmpClientWaiting(3)) == 0) {
				break
			}
		loop
	}
	if (getGameMode(GameEnv) == GAMEMODE_CLIENT) {
		sockopen@ SOCK_CHAT+0, ServerAddress, PORT_CHAT+ClientNumber
		ChatBuf = ""
	}
	gsel 0, 1
	return

#deffunc chatrecv var GameStat, var GameEnv
	if (getGameMode(GameEnv) == GAMEMODE_SERVER) {
		repeat NUM_OF_PLAYERS
			if (IsRemotePlayer(GameEnv, cnt) >= 2) {
				sockcheck@ SOCK_CHAT-1+IsRemotePlayer(GameEnv, cnt)
				if (stat == 0) {
					sdim chattmp, 65536: sockget@ chattmp, 65536, SOCK_CHAT-1+IsRemotePlayer(GameEnv, cnt)
					switch IsRemotePlayer(GameEnv, cnt)
						case 2: ChatBuf1 += chattmp: notesel ChatBuf1: swbreak
						case 3: ChatBuf2 += chattmp: notesel ChatBuf2: swbreak
						case 4: ChatBuf3 += chattmp: notesel ChatBuf3: swbreak
					swend
					repeat notemax
						if (notemax == 0) {break}
						tmpcht = "": noteget tmpcht, 0: notedel 0
						#ifdef SANMAT
							if ((IsRemotePlayer(GameEnv, 0) == 2)||(IsRemotePlayer(GameEnv, 1) == 2)||(IsRemotePlayer(GameEnv, 2) == 2)) {sockput@ tmpcht+"\n", SOCK_CHAT+1}
							if ((IsRemotePlayer(GameEnv, 0) == 3)||(IsRemotePlayer(GameEnv, 1) == 3)||(IsRemotePlayer(GameEnv, 2) == 3)) {sockput@ tmpcht+"\n", SOCK_CHAT+2}
						#else
							if ((IsRemotePlayer(GameEnv, 0) == 2)||(IsRemotePlayer(GameEnv, 1) == 2)||(IsRemotePlayer(GameEnv, 2) == 2)||(IsRemotePlayer(GameEnv, 3) == 2)) {sockput@ tmpcht+"\n", SOCK_CHAT+1}
							if ((IsRemotePlayer(GameEnv, 0) == 3)||(IsRemotePlayer(GameEnv, 1) == 3)||(IsRemotePlayer(GameEnv, 2) == 3)||(IsRemotePlayer(GameEnv, 3) == 3)) {sockput@ tmpcht+"\n", SOCK_CHAT+2}
							if ((IsRemotePlayer(GameEnv, 0) == 4)||(IsRemotePlayer(GameEnv, 1) == 4)||(IsRemotePlayer(GameEnv, 2) == 4)||(IsRemotePlayer(GameEnv, 3) == 4)) {sockput@ tmpcht+"\n", SOCK_CHAT+3}
						#endif
							if (strmid(tmpcht, 0, 1) == "0") {chatappend getPlayerName(GameEnv, 0)+"("+windName(playerWind(0, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
							if (strmid(tmpcht, 0, 1) == "1") {chatappend getPlayerName(GameEnv, 1)+"("+windName(playerWind(1, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
							if (strmid(tmpcht, 0, 1) == "2") {chatappend getPlayerName(GameEnv, 2)+"("+windName(playerWind(2, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
						#ifndef SANMAT
							if (strmid(tmpcht, 0, 1) == "3") {chatappend getPlayerName(GameEnv, 3)+"("+windName(playerWind(3, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
						#endif
					loop
					noteunsel
				}
			}
		loop
	}
	if (getGameMode(GameEnv) == GAMEMODE_CLIENT) {
		sockcheck@ SOCK_CHAT+0
		if (stat == 0) {
			sdim chattmp, 65536: sockget@ chattmp, 65536, SOCK_CHAT+0
			ChatBuf += chattmp: notesel ChatBuf
			repeat notemax
				if (notemax == 0) {break}
				tmpcht = "": noteget tmpcht, 0: notedel 0
					if (strmid(tmpcht, 0, 1) == "0") {chatappend getPlayerName(GameEnv, 0)+"("+windName(playerWind(0, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
					if (strmid(tmpcht, 0, 1) == "1") {chatappend getPlayerName(GameEnv, 1)+"("+windName(playerWind(1, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
					if (strmid(tmpcht, 0, 1) == "2") {chatappend getPlayerName(GameEnv, 2)+"("+windName(playerWind(2, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
				#ifndef SANMAT
					if (strmid(tmpcht, 0, 1) == "3") {chatappend getPlayerName(GameEnv, 3)+"("+windName(playerWind(3, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
				#endif
			loop
			noteunsel
		}
	}
return

#deffunc chatpost var GameStat, var GameEnv
	CurrentChatTxt = CurrentChatTxt()
	if (getGameMode(GameEnv) == GAMEMODE_SERVER) {
	#ifdef SANMAT
		if ((IsRemotePlayer(GameEnv, 0) == 2)||(IsRemotePlayer(GameEnv, 1) == 2)||(IsRemotePlayer(GameEnv, 2) == 2)) {sockput@ ""+getPlayer(GameStat)+CurrentChatTxt+"\n", SOCK_CHAT+1}
		if ((IsRemotePlayer(GameEnv, 0) == 3)||(IsRemotePlayer(GameEnv, 1) == 3)||(IsRemotePlayer(GameEnv, 2) == 3)) {sockput@ ""+getPlayer(GameStat)+CurrentChatTxt+"\n", SOCK_CHAT+2}
	#else
		if ((IsRemotePlayer(GameEnv, 0) == 2)||(IsRemotePlayer(GameEnv, 1) == 2)||(IsRemotePlayer(GameEnv, 2) == 2)||(IsRemotePlayer(GameEnv, 3) == 2)) {sockput@ ""+getPlayer(GameStat)+CurrentChatTxt+"\n", SOCK_CHAT+1}
		if ((IsRemotePlayer(GameEnv, 0) == 3)||(IsRemotePlayer(GameEnv, 1) == 3)||(IsRemotePlayer(GameEnv, 2) == 3)||(IsRemotePlayer(GameEnv, 3) == 3)) {sockput@ ""+getPlayer(GameStat)+CurrentChatTxt+"\n", SOCK_CHAT+2}
		if ((IsRemotePlayer(GameEnv, 0) == 4)||(IsRemotePlayer(GameEnv, 1) == 4)||(IsRemotePlayer(GameEnv, 2) == 4)||(IsRemotePlayer(GameEnv, 3) == 4)) {sockput@ ""+getPlayer(GameStat)+CurrentChatTxt+"\n", SOCK_CHAT+3}
	#endif
		tmpcht = ""+getPlayer(GameStat)+CurrentChatTxt
			if (strmid(tmpcht, 0, 1) == "0") {chatappend getPlayerName(GameEnv, 0)+"("+windName(playerWind(0, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
			if (strmid(tmpcht, 0, 1) == "1") {chatappend getPlayerName(GameEnv, 1)+"("+windName(playerWind(1, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
			if (strmid(tmpcht, 0, 1) == "2") {chatappend getPlayerName(GameEnv, 2)+"("+windName(playerWind(2, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
		#ifndef SANMAT
			if (strmid(tmpcht, 0, 1) == "3") {chatappend getPlayerName(GameEnv, 3)+"("+windName(playerWind(3, getRound(GameStat)))+") : "+strmid(tmpcht, 1, strlen(tmpcht))+"\n"}
		#endif
	}
	if (getGameMode(GameEnv) == GAMEMODE_CLIENT) {
		sockput@ ""+getPlayer(GameStat)+CurrentChatTxt+"\n", SOCK_CHAT+0
	}
	clearChatTxt
return
#global

/* チャット送信 */
*chatpost_
	chatpost GameStat, GameEnv
return
