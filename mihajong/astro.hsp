/*=============================
 * HSP麻雀クライアントMiHaJong
 *     [天文計算ルーチン]
 *=============================
 */

#include "IMPORTED.hsp"

#uselib "kernel32.dll"
#ifndef GetSystemTime
#func GetSystemTime@ "GetSystemTime" var
#endif
#ifndef getutc
#module
#defcfunc getutc int param
	// UTCで時刻を取得する 使い方はgettimeと同様
	dim nowtime, 4
	GetSystemTime@ nowtime
	return wpeek(nowtime, param*2)
#global
#endif

#ifndef julian
#module
#include "hspmath.as"
#defcfunc julian int year, int month, int day, int hour, int min, int sec, int msec
/*
		val=julian(p1, p2, p3, p4, p5, p6, p7)
		ユリウス日を計算する

		p1 : 年
		p2 : 月
		p3 : 日
		p4 : 時
		p5 : 分
		p6 : 秒
		p7 : ミリ秒

		指定した日時のユリウス日を求めます。
*/
	if (month < 3) {
		JulianDay = floor(365.25*double(year-1))+floor(double(year-1)/400)-floor(double(year-1)/100)+floor(30.59*(double(month)+10))+double(day)-678912+2400000.5
	} else {
		JulianDay = floor(365.25*double(year))+floor(double(year)/400)-floor(double(year)/100)+floor(30.59*(double(month)-2))+double(day)-678912+2400000.5
	}
	JulianDay += double(hour)/24+double(min)/24/60+double(sec)/24/3600+double(msec)/24/3600/1000
	return JulianDay
#global
#endif

#ifndef sun_ecliptic
#module
#include "hspmath.as"
#defcfunc sun_ecliptic double jd
	// 太陽黄経を計算する
	T = (jd - 2451545.0) / 36525.0
	omega = deg2rad(282.9400)
	M = deg2rad(357.5256+35999.049*T)
	lambda = omega+M+(6191.2*T + 6893.0*sin(M) + 72.0*sin(2.0*M))/1296000.0*2.0*M_PI
	return rad2deg(lambda)\360.0
#global
#endif
