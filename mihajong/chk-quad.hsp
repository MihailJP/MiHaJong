/*=============================
 * HSP麻雀クライアントMiHaJong
 *    [槓子役判定ルーチン]
 *=============================
 */

#module
#include "const.hsp"
#deffunc countKangz array KangzCount, array MianziDat
/*
		countKangz p1, p2
		槓子の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する槓子を数え、種類別に格納します。
		また、statには見つかった槓子の数が格納されます。
*/
	trace "槓子の種類を調べます。"
	Kangzi = 0
	dim KangzCount, TILE_NONFLOWER_MAX
	repeat SIZE_OF_MELD_BUFFER
		if (MianziDat(cnt) >= MELD_QUAD*MELD_TYPE_STEP) {
			KangzCount(MianziDat(cnt) \ TILE_CODE_MAXIMUM)++
			Kangzi++
		}
	loop
	return Kangzi
#global

#module
#include "const.hsp"
#deffunc countAnKangz array KangzCount, array MianziDat
/*
		countAnKangz p1, p2
		暗槓子の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する暗槓子を数え、種類別に格納します。
		また、statには見つかった暗槓子の数が格納されます。
*/
	trace "暗槓子の種類を調べます。"
	Kangzi = 0
	dim KangzCount, TILE_NONFLOWER_MAX
	repeat SIZE_OF_MELD_BUFFER
		if ((MianziDat(cnt) / MELD_TYPE_STEP) == MELD_QUAD_CONCEALED) {
			KangzCount(MianziDat(cnt) \ TILE_CODE_MAXIMUM)++
			Kangzi++
		}
	loop
	return Kangzi
#global

#module
#include "const.hsp"
#deffunc countKaKangz array KangzCount, array MianziDat
/*
		countKaKangz p1, p2
		加槓の数を数える

		p1 : 結果を格納する配列
		p2 : 面子構成を格納した配列

		手牌に存在する加槓を数え、種類別に格納します。
*/
	trace "加槓の種類を調べます。"
	Kangzi = 0
	dim KangzCount, TILE_NONFLOWER_MAX
	repeat SIZE_OF_MELD_BUFFER
		if (MianziDat(cnt) >= MELD_QUAD_ADDED_MIN*MELD_TYPE_STEP) {
			KangzCount(MianziDat(cnt) \ TILE_CODE_MAXIMUM)++
			Kangzi++
		}
	loop
	return
#global

#include "astro.hsp"

/* 槓子系の役 */
#module
#include "const.hsp"
#include "mjcore.hsp"
#deffunc quadyaku var GameStat, var tmpYakuInfo, array MianziDat, int targetPlayer
	trace "槓子役の処理に入ります。"
	countKangz KangzCount, MianziDat: Kangzi = stat
	countAnKangz AnKangzCount, MianziDat: AnKan = stat
	countKaKangz KaKangzCount, MianziDat
#ifndef SANMAS
	/* 超四喜 */
	if (getRule(RULE_CHOUSIXI) != 0) {
		if ((KangzCount(TILE_EAST_WIND) >= 1)&&(KangzCount(TILE_SOUTH_WIND) >= 1)&&(KangzCount(TILE_WEST_WIND) >= 1)&&(KangzCount(TILE_NORTH_WIND) >= 1)) {
			addyakuman tmpYakuInfo, "超四喜", 3
			delyaku tmpYakuInfo, "大四喜"
		}
	}
#endif
	/* 四槓子 */
	tmpyakuCensored = 0 // 超四喜が成立してたら飛ばす
	tmpyakuCensored += searchyaku(tmpYakuInfo, "超四喜")
	if ((Kangzi == 4)&&(tmpyakuCensored == 0)) {
		switch getRule(RULE_SUUKANTSU)
			case 0: case 2:
				addyakuman tmpYakuInfo, "四槓子", 1
				swbreak
			case 1: case 3:
				addyakuman tmpYakuInfo, "四槓子", 2
				swbreak
		swend
		// 四槓子が成立したら対々和の成立を解除する(青天井ルール用)
		delyaku tmpYakuInfo, "対々和"
	}
	/* 三暗槓 */
	if (getRule(RULE_SANANKAN) != 0) {
		if (AnKan == 3) {
			addyakuman tmpYakuInfo, "三暗槓", 1
		}
	}
	/* 大峡谷 */
	if (getRule(RULE_GRAND_CANYON) != 0) {
		yakuFlag = 0
		countKez KezCount, MianziDat
		if ((KezCount(TILE_CHARACTER_EIGHT) >= 1)||(KezCount(TILE_CIRCLE_EIGHT) >= 1)||(KezCount(TILE_BAMBOO_EIGHT) >= 1)) {yakuFlag = 1}
		if ((KangzCount(TILE_CHARACTER_NINE) >= 1)&&(KangzCount(TILE_CIRCLE_NINE) >= 1)&&(KangzCount(TILE_BAMBOO_NINE) >= 1)&&(yakuFlag == 1)&&(MianziDat(0) \ TILE_SUIT_STEP == 8)) {
			addyakuman tmpYakuInfo, "大峡谷", 1
		}
	}
#ifndef SANMAS
	/* 雪化粧 */
	if (getRule(RULE_YUKIGESHOU) != 0) {
		if ((KangzCount(TILE_CIRCLE_ONE) >= 1)&&(KangzCount(TILE_CIRCLE_NINE) >= 1)&&(KangzCount(TILE_WHITE_DRAGON) >= 1)) {
			addyakuman tmpYakuInfo, "雪化粧", 1
			delyaku tmpYakuInfo, "役牌・白"
		}
	}
#endif
	/* 三色同槓 */
	if (getRule(RULE_SANSHOKU_DOUKAN) != 0) {
		yakuFlag = 0
		repeat TILE_NUMERAL_MAX/TILE_NUMERAL_COLORS
			if ((KangzCount(cnt+TILE_SUIT_CHARACTERS) >= 1)&&(KangzCount(cnt+TILE_SUIT_CIRCLES) >= 1)&&(KangzCount(cnt+TILE_SUIT_BAMBOOS) >= 1)) {yakuFlag = 1}
		loop
		if (yakuFlag) {
			switch getRule(RULE_SANSHOKU_DOUKAN)
				case 1: addyaku tmpYakuInfo, "三色同槓", 4+getMenzen(GameStat, targetPlayer): swbreak
				case 2: addyakuman tmpYakuInfo, "三色同槓", 1: swbreak
			swend
			delyaku tmpYakuInfo, "三色同刻"
		}
	}
#ifndef SANMAS
	/* 超三元 */
	if (getRule(RULE_CHOUSANGEN) != 0) {
		if ((KangzCount(TILE_WHITE_DRAGON) >= 1)&&(KangzCount(TILE_GREEN_DRAGON) >= 1)&&(KangzCount(TILE_RED_DRAGON) >= 1)) {
			addyakuman tmpYakuInfo, "超三元", 2
			delyaku tmpYakuInfo, "大三元"
		}
	}
#endif
	/* 三槓子…下手な役満よりも難しいのに何で２飜しかないの */
	tmpyakuCensored = 0 // 三暗槓が成立してたら飛ばす
	tmpyakuCensored += searchyaku(tmpYakuInfo, "三暗槓")
	tmpyakuCensored += searchyaku(tmpYakuInfo, "大峡谷")
	tmpyakuCensored += searchyaku(tmpYakuInfo, "雪化粧")
	tmpyakuCensored += searchyaku(tmpYakuInfo, "三色同槓")
	tmpyakuCensored += searchyaku(tmpYakuInfo, "超三元")
	if ((Kangzi == 3)&&(tmpyakuCensored == 0)) {
		switch getRule(RULE_SANKANTSU)
			case 0:
				addyaku tmpYakuInfo, "三槓子", 2
				swbreak
			case 1:
				addyaku tmpYakuInfo, "三槓子", 3
				swbreak
			case 2:
				addyakuman tmpYakuInfo, "三槓子", 1
				swbreak
		swend
	}
	/* 二暗槓 */
	if (getRule(RULE_RYANANKAN) != 0) {
		if (AnKan == 2) {
			addyaku tmpYakuInfo, "二暗槓", 1
		}
	}
#ifndef SANMAS
	/* 声東撃西 */
	if (getRule(RULE_SHENGDONG_JIXI) != 0) {
		if ((KangzCount(TILE_EAST_WIND) >= 1)&&(KangzCount(TILE_WEST_WIND) >= 1)) {
			addyaku tmpYakuInfo, "声東撃西", 6
		}
	}
	/* 走南闖北 */
	if (getRule(RULE_ZAONAN_CHUANGBEI) != 0) {
		if ((KangzCount(TILE_SOUTH_WIND) >= 1)&&(KangzCount(TILE_NORTH_WIND) >= 1)) {
			addyaku tmpYakuInfo, "走南闖北", 6
		}
	}
	/* 暗中模索 */
	if (getRule(RULE_ANCHUMOSAKU) != 0) {
		if ((AnKangzCount(TILE_RED_DRAGON) >= 1)&&(TsumoTile(GameStat, targetPlayer)/TILE_SUIT_STEP*TILE_SUIT_STEP = TILE_SUIT_BAMBOOS)&&(getTsumoAgariFlag(GameStat) == 1)) {
			addyaku tmpYakuInfo, "暗中模索", 1
		}
	}
#endif
	/* 戦車 */
	if (getRule(RULE_TANK) != 0) {
		if (KangzCount(TILE_BAMBOO_SEVEN) >= 1) {
			addyaku tmpYakuInfo, "戦車", 1
		}
	}
	/* 真田六文銭 */
	if (getRule(RULE_SANADACOIN) != 0) {
		if (KangzCount(TILE_CIRCLE_SIX) >= 1) {
			addyaku tmpYakuInfo, "真田六文銭", 1
		}
	}
	/* 三矢の誓い */
	if (getRule(RULE_THREE_ARROWS) != 0) {
		if (KangzCount(TILE_BAMBOO_THREE) >= 1) {
			addyaku tmpYakuInfo, "三矢の誓い", 1
		}
	}
	/* 鬼は外 */
	if (getRule(RULE_SETSUBUN) != 0) {
		if (KangzCount(TILE_CIRCLE_NINE) >= 1) {
			TZ = julian(gettime(0), gettime(1), gettime(3), gettime(4), gettime(5), gettime(6), gettime(7)) - julian(getutc(0), getutc(1), getutc(3), getutc(4), getutc(5), getutc(6), getutc(7))
			/* 翌日が立春であるかを判定する。タイムゾーンは自動認識する */
			if ((sun_ecliptic(julian(gettime(0), gettime(1), gettime(3), 0, 0, 0, 0) + TZ + 1.0) <= 315.0)&&(sun_ecliptic(julian(gettime(0), gettime(1), gettime(3), 0, 0, 0, 0) + TZ + 2.0) > 315.0)) {
				addyaku tmpYakuInfo, "鬼は外", 1
			}
		}
	}
	/* 草加 */
	if (getRule(RULE_SOUKA) != 0) {
		repeat 9, 1
			if (KaKangzCount(TILE_SUIT_BAMBOOS+cnt) >= 1) {
				addyaku tmpYakuInfo, "草加", 1
				break
			}
		loop
	}
return
#global
