/*=============================
 * HSP麻雀クライアントMiHaJong
 *    [雑多なサブルーチン]
 *=============================
 */

/* デバッグログ */
#module debuglog

#uselib "mjlib.dll"
#func _initlog_ "?initLogger@logger@@YAHXZ"
#func _trace_msg_ "?trace_msg@logger@@YAXPBD@Z" str
#func _debug_msg_ "?debug_msg@logger@@YAXPBD@Z" str
#func _info_msg_ "?info_msg@logger@@YAXPBD@Z" str
#func _warn_msg_ "?warn_msg@logger@@YAXPBD@Z" str
#func _error_msg_ "?error_msg@logger@@YAXPBD@Z" str
#func _fatal_msg_ "?fatal_msg@logger@@YAXPBD@Z" str

#include "const.as"
#deffunc loginit
#ifdef LOGGING
	_initlog_
#endif
return

#deffunc debug_logging str debugmsg, str debugfile, int debugline, int verbosity
#ifdef LOGGING
	switch (verbosity)
		case 5: _trace_msg_ strf("(%s:%d) %s", debugfile, debugline, debugmsg): swbreak
		case 4: _debug_msg_ strf("(%s:%d) %s", debugfile, debugline, debugmsg): swbreak
		case 3: _info_msg_ strf("(%s:%d) %s", debugfile, debugline, debugmsg): swbreak
		case 2: _warn_msg_ strf("(%s:%d) %s", debugfile, debugline, debugmsg): swbreak
		case 1: _error_msg_ strf("(%s:%d) %s", debugfile, debugline, debugmsg): swbreak
		case 0: _fatal_msg_ strf("(%s:%d) %s", debugfile, debugline, debugmsg): swbreak
	swend
#endif
return
#global

#module
#defcfunc notesearch array tmpnote, str txt
/*
		val=notesearch(p1, p2)
		文字列バッファから特定の文字列を検索する

		p1 : 検索されるバッファ
		p2 : 検索する文字列

		指定したバッファから特定の文字列になっている行を検索し、見つかった行を返します。
		見つからなかった場合は-1を返します。
*/
	ans=-1
	notesel tmpnote
	repeat notemax
		noteget a, cnt
		if (a == txt) {ans = cnt: break}
	loop
	noteunsel
	return ans
#global

/* プレイヤーの自風がどれか調べる */
#module
#include "const.as"
#defcfunc playerwind int player, int currentRound
#ifdef SANMAT
	return ((player+24-(currentRound-(currentRound/4)))\NUM_OF_ACTUAL_PLAYERS)
#else
	return ((player+32-currentRound)\NUM_OF_ACTUAL_PLAYERS)
#endif
#global

/* あるプレイヤーに対して指定したプレイヤーがどこ(下家、対面、上家)にいるか調べる */
#module
#include "const.as"
#defcfunc playerRelative int targetPlayer, int basePlayer
	// ((4+targetPlayer-getPlayer(GameStat))\4)*(TileShowMode) の値
	// 0…自家 1…下家 2…対面 3…上家
	return (NUM_OF_PLAYERS+targetPlayer-basePlayer)\NUM_OF_PLAYERS
#global

/* あるプレイヤーの(下家、対面、上家)を調べる */
#module
#include "const.as"
#defcfunc RelativePositionOf int targetPlayer, int relative
	return (targetPlayer+relative)\NUM_OF_PLAYERS
#global

/* 「東家」「南家」「西家」「北家」の文字列を返す */
#module
#include "const.as"
#defcfunc windName int wind
	switch wind
		case PLAYER_EAST: return "東家": swbreak
		case PLAYER_SOUTH: return "南家": swbreak
		case PLAYER_WEST: return "西家": swbreak
		case PLAYER_NORTH: return "北家": swbreak
		default: return "????": swbreak
	swend
#global

/* 「東○局」などの文字列を返す */
#module
#include "const.as"
#include "struct.as"
#defcfunc roundName int roundNum
	roundNameTxt = ""
	switch roundNum/NUM_OF_PLAYERS
		case 0: roundNameTxt += "東": swbreak
		case 1: roundNameTxt += "南": swbreak
		case 2: roundNameTxt += "西": swbreak
		case 3: roundNameTxt += "北": swbreak
		case 4: roundNameTxt += "白": swbreak
		case 5: roundNameTxt += "発": swbreak
		case 6: roundNameTxt += "中": swbreak
		default: roundNameTxt += "??"
	swend
	if ((getRule(RULE_GAME_LENGTH) == 5)||(getRule(RULE_GAME_LENGTH) == 7)) {
		switch getRoundLoop(GameStat@)*NUM_OF_ACTUAL_PLAYERS+roundNum\NUM_OF_PLAYERS
			case 0: roundNameTxt += "一局": swbreak
			case 1: roundNameTxt += "二局": swbreak
			case 2: roundNameTxt += "三局": swbreak
			case 3: roundNameTxt += "四局": swbreak
			case 4: roundNameTxt += "五局": swbreak
			case 5: roundNameTxt += "六局": swbreak
			case 6: roundNameTxt += "七局": swbreak
			case 7: roundNameTxt += "八局": swbreak
			case 8: roundNameTxt += "九局": swbreak
			case 9: roundNameTxt += "十局": swbreak
			default: roundNameTxt += strf("%d局", getRoundLoop(GameStat@)*NUM_OF_ACTUAL_PLAYERS+roundNum\NUM_OF_PLAYERS+1)
		swend
	} else {
		switch roundNum\NUM_OF_PLAYERS
			case 0: roundNameTxt += "一局": swbreak
			case 1: roundNameTxt += "二局": swbreak
			case 2: roundNameTxt += "三局": swbreak
			case 3: roundNameTxt += "四局": swbreak
			default: roundNameTxt += "??局"
		swend
	}
	return roundNameTxt
#global

/* 一周するまでに必要な場の数 */
#module
#include "const.as"
#defcfunc roundLoopRate
	if (getRule(RULE_SUDDEN_DEATH_TYPE) == 1) {return 28}
	else {return 16}
#global

/* 牌の名前の文字列を返す */
#module
#include "const.as"
#defcfunc TileName int tile
	switch tile
		case TILE_CHARACTER_ONE: return "一萬": swbreak
		case TILE_CHARACTER_TWO: return "二萬": swbreak
		case TILE_CHARACTER_THREE: return "三萬": swbreak
		case TILE_CHARACTER_FOUR: return "四萬": swbreak
		case TILE_CHARACTER_FIVE: return "五萬": swbreak
		case TILE_CHARACTER_SIX: return "六萬": swbreak
		case TILE_CHARACTER_SEVEN: return "七萬": swbreak
		case TILE_CHARACTER_EIGHT: return "八萬": swbreak
		case TILE_CHARACTER_NINE: return "九萬": swbreak
		case TILE_CIRCLE_ONE: return "一筒": swbreak
		case TILE_CIRCLE_TWO: return "二筒": swbreak
		case TILE_CIRCLE_THREE: return "三筒": swbreak
		case TILE_CIRCLE_FOUR: return "四筒": swbreak
		case TILE_CIRCLE_FIVE: return "五筒": swbreak
		case TILE_CIRCLE_SIX: return "六筒": swbreak
		case TILE_CIRCLE_SEVEN: return "七筒": swbreak
		case TILE_CIRCLE_EIGHT: return "八筒": swbreak
		case TILE_CIRCLE_NINE: return "九筒": swbreak
		case TILE_BAMBOO_ONE: return "一索": swbreak
		case TILE_BAMBOO_TWO: return "二索": swbreak
		case TILE_BAMBOO_THREE: return "三索": swbreak
		case TILE_BAMBOO_FOUR: return "四索": swbreak
		case TILE_BAMBOO_FIVE: return "五索": swbreak
		case TILE_BAMBOO_SIX: return "六索": swbreak
		case TILE_BAMBOO_SEVEN: return "七索": swbreak
		case TILE_BAMBOO_EIGHT: return "八索": swbreak
		case TILE_BAMBOO_NINE: return "九索": swbreak
		case TILE_EAST_WIND   : return "東": swbreak
		case TILE_SOUTH_WIND  : return "南": swbreak
		case TILE_WEST_WIND   : return "西": swbreak
		case TILE_NORTH_WIND  : return "北": swbreak
		case TILE_WHITE_DRAGON: return "白": swbreak
		case TILE_GREEN_DRAGON: return "發": swbreak
		case TILE_RED_DRAGON  : return "中": swbreak
		default: return "????": swbreak
	swend
#global

/* 王牌を除いた山牌の残り枚数 */
#module
#include "struct.as"
#defcfunc tilesLeft var GameStat
	return (getRinshanPointer(GameStat) - (getDeadWallLength(GameStat)-1) - getDrawPointer(GameStat))
#global

/* 順位を計算する */
#module
#include "const.as"
#include "struct.as"
#deffunc calcRank array Rank, var GameStat
	dim Rank, NUM_OF_PLAYERS
	repeat NUM_OF_ACTUAL_PLAYERS
		Rank(cnt) = 1
		tmpcnt = cnt
		repeat NUM_OF_ACTUAL_PLAYERS
			tmpcntt = cnt
			repeat NUM_OF_DIGIT_GROUPS
				if (getScore(GameStat,tmpcntt,NUM_OF_DIGIT_GROUPS-1-cnt) > getScore(GameStat,tmpcnt,NUM_OF_DIGIT_GROUPS-1-cnt)) {Rank(tmpcnt)++: break}
				if (getScore(GameStat,tmpcntt,NUM_OF_DIGIT_GROUPS-1-cnt) < getScore(GameStat,tmpcnt,NUM_OF_DIGIT_GROUPS-1-cnt)) {break}
				if ((cnt == NUM_OF_DIGIT_GROUPS-1)&&(tmpcnt > tmpcntt)) {Rank(tmpcnt)++}
			loop
		loop
	loop
	return
#global

/* 包かどうかの判定 */
#module
#include "const.as"
#include "struct.as"
#defcfunc isPao var GameStat, int agariPlayer, int paoPlayer
	paoFlag = 0
	repeat PAO_YAKU_PAGES
		if ((paoPlayer == getPao(GameStat, PAO_PLAYER_PAO, cnt))&&(getPao(GameStat, PAO_PLAYER_AGARI, cnt) == agariPlayer)) {
			paoFlag = 1
		}
	loop
	return paoFlag
#global

#module
#include "const.as"
#include "struct.as"
#defcfunc isPaoAgari var GameStat, int agariPlayer
	paoFlag = 0
	repeat PAO_YAKU_PAGES
		if (getPao(GameStat, PAO_PLAYER_AGARI, cnt) == agariPlayer) {
			paoFlag = 1
		}
	loop
	return paoFlag
#global

#module
#include "const.as"
#include "struct.as"
#defcfunc isGotPao var GameStat, int paoPlayer
	paoFlag = 0
	repeat PAO_YAKU_PAGES
		if (paoPlayer == getPao(GameStat, PAO_PLAYER_PAO, cnt)) {
			paoFlag = 1
		}
	loop
	return paoFlag
#global

#module
#include "const.as"
#defcfunc getPaoPlayer var GameStat, int agariPlayer
	PaoPlayer = -1
	repeat PAO_YAKU_PAGES
		if ((getPao(GameStat, PAO_PLAYER_PAO, cnt) >= 0)&&(getPao(GameStat, PAO_PLAYER_AGARI, cnt) == agariPlayer)) {
			PaoPlayer = getPao(GameStat, PAO_PLAYER_PAO, cnt)
		}
	loop
	return PaoPlayer
#global

#module
#include "const.as"
#include "struct.as"
#defcfunc RonPlayers var GameStat
	return getDeclarationFlag(GameStat, DECLARATIONFLAG_RON, 0)+getDeclarationFlag(GameStat, DECLARATIONFLAG_RON, 1)+getDeclarationFlag(GameStat, DECLARATIONFLAG_RON, 2)+getDeclarationFlag(GameStat, DECLARATIONFLAG_RON, 3)
#global

/* 場風牌のリスト */
#module
#include "const.as"
#defcfunc WindMax
	return 7

#defcfunc Wind2Tile int wind
	switch wind
		case 0: return TILE_EAST_WIND: swbreak
		case 1: return TILE_SOUTH_WIND: swbreak
		case 2: return TILE_WEST_WIND: swbreak
		case 3: return TILE_NORTH_WIND: swbreak
		case 4: return TILE_WHITE_DRAGON: swbreak
		case 5: return TILE_GREEN_DRAGON: swbreak
		case 6: return TILE_RED_DRAGON: swbreak
	swend
#global

/* エラーダイアログ */
#module
#uselib "user32.dll"
#func MessageBoxA "MessageBoxA" int,sptr,sptr,int
#deffunc errordlg str message, str titletxt
	MessageBoxA hwnd,message,titletxt,0x10
	return
#global

/* Vista/7を使っているかどうか */
#module
#defcfunc isVista
	wver = sysinfo(0)
	/* sysinfo(0)で帰ってくる情報
		XP    … ver5.2
		Vista … ver6.0
		7     … ver6.1
	*/
	if (instr(wver, 0, "WindowsNT ver6") >= 0) {
		return 1
	}
	return 0
#global

/* コンフィグのパスを自動設定 */
/* Vista以降でRoamingに準備できていなければ作る */
#module
#defcfunc confPath
	configpath = ""
	if (isVista()) {
		currentPath = dir_cur
		if (instr(currentPath, 0, dirinfo(0x10026)) >= 0) {
			appdataPath = dirinfo(0x1001a)
			chdir appdataPath
			dirlist dir_roaming,"*",5
			if (notesearch(dir_roaming, "MiHaJong") == -1) {
				mkdir "MiHaJong"
				chdir "MiHaJong"
				mkdir "haifu"
				bcopy currentPath+"\\haifu\\haifu.css", appdataPath+"\\MiHaJong\\haifu\\haifu.css"
			}
			configpath = appdataPath+"\\MiHaJong\\"
			chdir currentPath
		}
	}
	return configpath
#global

/* エラーがあったときに安全に終了させるための処理 */
*errorproc
	error strf("内部エラー [%d] が発生しました。", wparam)
	errordlg "内部エラー "+wparam+" が発生しました。"
	/* FALLTHRU */

/* 終了処理 */
*cleanup
	gsel SCR_CHAT, -1
	gsel 0, -1
	dmstop: dmend
	info "DirectMusic を解放しました。"
	repeat SND_END-SND_START+1
		dsstop cnt+SND_START
	loop
	repeat VOX_END-VOX_START+1
		dsstop cnt+VOX_START
	loop
	dsend
	info "DirectSound を解放しました。"
	repeat NUM_OF_PLAYERS
		sockclose SOCK_GAME+cnt
		sockclose SOCK_CHAT+cnt
	loop
	sockclose SOCK_LOGGER
	end
